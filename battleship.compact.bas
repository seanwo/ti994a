100 REM COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
210 CALL CLEAR
220 CALL SCREEN(4)
230 RANDOMIZE
240 DIM SS(1,4,4)
250 CALL SCH
260 CALL SCS
280 CALL RBS
290 CALL DSH(SS(,,))
300 CALL PGA(SS(,,))
310 GOTO 280
320 END
340 SUB SCH
360 CALL CHAR(96,"3C4299A5A599423C")
380 CALL CHAR(97,"0000000000000000")
400 CALL CHAR(104,"3C7EFFFFFFFF7E3C")
420 CALL CHAR(112,"3C4299A5A599423C")
440 CALL CHAR(120,"004FC949494949EF")
460 CALL CHAR(128,"3C7EFFFFFFFF7E3C")
480 CALL CHAR(136,"3C7EFFFFFFFF7E3C") 
490 SUBEND
510 SUB SCS
520 CALL COLOR(9,2,8)
530 CALL COLOR(10,11,8)
540 CALL COLOR(11,2,15)
550 CALL COLOR(12,2,1)
560 CALL COLOR(13,16,8)
570 CALL COLOR(14,9,8)
580 SUBEND
600 SUB DSH(SS(,,))
610 CALL DPL(SS(,,))
620 CALL DCO(0,SS(,,))
630 SUBEND
650 SUB DPL(SS(,,))
670 A$="N"
680 CALL MAD(A$)
690 IF A$="Y" THEN CALL DAU(SS(,,)) ELSE CALL DMA(SS(,,))
700 CALL RSA(SS(,,))
710 CALL RHO
720 CALL MDG
730 SUBEND
750 SUB DAU(SS(,,))
760 R=14::C=23
770 CALL TXT("AUTO",(R+6),(C))
780 CALL TXT("DEPLOYING",(R+7),(C))
790 CALL TXT("SHIPS...",(R+8),(C))
800 CALL DCO(1,SS(,,))
820 FOR I=0 TO 5-1
830 CALL GSL(SL,(I))
840 FOR J=0 TO SL-1
850 SS(0,I,J)=SS(1,I,J)
860 NEXT J
870 NEXT I
880 SUBEND
900 SUB DMA(SS(,,))
910 CALL MCL
920 DIM S(4) :: FOR I = 0 TO 4 :: S(I)=0 :: NEXT I
940 FOR CS=0 TO 5-1
960 CALL MDE((CS))
970 CALL ISH(S(),(CS))
980 P=0
990 E=0
1000 CALL CVI(E,(P),(CS),S(),SS(,,))
1010 IF E=1 THEN 960
1020 CALL GSL(SL,(CS))
1030 FOR I=0 TO SL-1
1040 SS(0,CS,I)=S(I)
1050 NEXT I
1060 CALL RSI((CS),S())
1070 NEXT CS
1080 SUBEND
1100 SUB DCO(VI,SS(,,))
1110 DIM S(4) :: FOR I = 0 TO 4 :: S(I)=0 :: NEXT I
1130 R=5::C=3
1150 FOR CS=0 TO 5-1
1170 CALL GSL(SL,(CS))
1180 HZ=INT(RND*2)
1190 SB=INT(RND*(10-SL))+1
1200 ST=INT(RND*10)+1
1210 FOR I=0 TO SL-1
1220 IF HZ=1 THEN S(I)=16*ST+SB+I ELSE S(I)=16*(SB+I)+ST
1230 NEXT I
1240 P=1
1250 E=0
1260 CALL COV(E,(P),(CS),S(),SS(,,))
1270 IF E=1 THEN 1170
1280 FOR I=0 TO SL-1
1290 SS(1,CS,I)=S(I)
1300 NEXT I
1310 CALL GDF(D)
1320 IF D=1 THEN VI=1
1330 IF VI=1 THEN CALL RSI((CS),S())
1340 NEXT CS
1350 SUBEND
1370 SUB PGA(SS(,,))
1380 CALL MCL
1390 CALL MTA
1400 DIM SH(1,9,9)
1410 FOR I=0 TO 9 :: FOR J=0 TO 9 :: SH(0,I,J)=0 :: SH(1,I,J)=0 :: NEXT J :: NEXT I
1420 DIM HL(1,4)
1440 FOR I=0 TO 5-1
1450 CALL GSL(SL,(I))
1460 HL(0,I)=SL :: HL(1,I)=SL
1470 NEXT I
1480 DIM SM(1)
1490 SM(0)=5 :: SM(1)=5
1500 CALL QIN(Q(),QL)
1510 W=-1
1520 CALL GAP(A)
1530 IF A=1 THEN P=1 ELSE P=0
1550 IF P=0 THEN CALL PTU(R,C,SH(,,))
1560 IF P=1 THEN CALL CTU(R,C,Q(),QL,SH(,,))
1570 SH(P,R-1,C-1)=1
1580 CALL PSH(W,(R),(C),(P),HL(,),SM(),SS(,,),Q(),QL)
1590 IF W=0 OR W=1 THEN 1640
1600 CALL GAP(A)
1610 IF A=0 THEN P=1-P
1620 GOTO 1550
1640 P$="N"
1650 CALL MGO(P$,(W))
1660 IF P$="Y" THEN 1690
1670 CALL CLEAR
1680 END
1690 SUBEND
1710 SUB PTU(R,C,SH(,,))
1730 CALL ITA(R$,C)
1740 R=ASC(R$)-64
1750 CALL CVO(E,(R),(C),0,SH(,,))
1760 IF E=1 THEN 1730
1770 SUBEND
1790 SUB CTU(R,C,Q(),QL,SH(,,))
1800 CALL GAI(R,C,Q(),QL,SH(,,))
1810 MR=14::MC=23
1820 T$=CHR$(64+R)&STR$(C)
1830 CALL HCHAR(MR+8,MC,32,10)
1840 CALL TXT(T$,(MR+8),(MC))
1850 SUBEND
1870 SUB PSH(W,R,C,P,HL(,),SM(),SS(,,),Q(),QL)
1880 W=-1 :: H=0 :: S=0
1890 L=16*R+C
1900 CALL CHI(H,S,(L),(1-P),SS(,,))
1910 IF P=1 AND H=1 THEN CALL QAD(Q(),QL,(L),E)
1920 CALL RSO((P),(H),(R),(C))
1930 IF H=0 THEN 2050
1940 HL(1-P,S)=HL(1-P,S)-1
1950 IF HL(1-P,S)>0 THEN 2050
1960 SM(1-P)=SM(1-P)-1
1970 CALL RSU((1-P),(S),SS(,,))
1980 IF SM(1-P)=0 THEN W=P
1990 IF P=0 THEN 2050
2000 CALL GSL(SL,(S))
2010 FOR I=0 TO SL-1
2020 SU=SS(0,S,I)
2030 CALL QDE(Q(),QL,(SU),E)
2040 NEXT I
2050 SUBEND
2070 SUB GAI(R,C,Q(),QL,SH(,,))
2090 IF QL<=OL THEN O=0
2100 IF QL=0 THEN GOSUB 2220
2110 IF QL=0 THEN GOTO 2170
2120 IF O=0 THEN GOSUB 2260
2130 O=O+1
2140 R=INT(Q(0)/16) :: C=Q(0)-INT(Q(0)/16)*16
2150 R=R+O*RD :: C=C+O*CD
2170 CALL CVO(E,(R),(C),1,SH(,,))
2180 IF E=1 THEN O=0 :: GOTO 2090
2190 OL=QL
2200 GOTO 2330
2220 R=INT(RND*10)+1 :: C=INT(RND*10)+1
2230 RD=0 :: CD=0 :: RV=0 :: O=0
2240 RETURN
2260 IF RV=1 THEN T=RD :: RD=CD :: CD=T :: RV=0  :: RETURN
2270 IF RD<>0 THEN RD=RD*-1:: RV=1 :: RETURN
2280 IF CD<>0 THEN CD=CD*-1:: RV=1 :: RETURN
2290 DR=INT(RND*4)
2300 IF DR=0 OR DR=2 THEN CD=DR-1 ELSE RD=DR-2
2310 RV=0
2320 RETURN
2330 SUBEND
2350 SUB TXT(T$,R,C)
2360 TL=LEN(T$)
2370 FOR I=0 TO TL-1
2380 C$=SEG$(T$,I+1,1)
2390 CALL HCHAR(R,C+I,ASC(C$))
2400 NEXT I
2410 SUBEND
2430 SUB RBS
2440 CALL CLEAR
2450 DISPLAY AT(1,9):"BATTLESHIP"
2460 CALL RBO
2470 CALL RAU
2480 SUBEND
2500 SUB RBO
2510 CALL RBB
2520 CALL RAX
2530 CALL RHO
2540 SUBEND
2560 SUB RBB
2570 R=5::C=3
2590 FOR I=0 TO 18
2600 CALL HCHAR(R+I,C,97,19)
2610 NEXT I
2620 SUBEND
2640 SUB RHO
2650 R=5::C=3
2670 FOR I=0 TO 18 STEP 2
2680 FOR J=0 TO 18 STEP 2
2690 CALL HCHAR(R+I,C+J,96,1)
2700 NEXT J
2710 NEXT I
2720 SUBEND
2740 SUB RAX
2750 R=5::C=3
2780 FOR I=0 TO 8
2790 CALL HCHAR(R-2,I*2+C,49+I,1)
2800 NEXT I
2810 CALL HCHAR(R-2,C+18,120,1)
2830 FOR I=1 TO 10
2840 CALL HCHAR(R-2+I*2,C-2,64+I,1)
2850 NEXT I
2860 SUBEND
2880 SUB RAU
2890 R=3::C=23
2910 FOR I=0 TO 9
2920 CALL HCHAR(R+I,C,96,10)
2930 NEXT I
2940 SUBEND
2960 SUB RSI(CS,S())
2970 CALL GSL(SL,(CS))
2990 R=5::C=3
3000 FOR I=0 TO SL-1
3010 RO=INT(S(I)/16) :: CO=S(I)-INT(S(I)/16)*16
3020 CALL HCHAR(R+(RO-1)*2,C+(CO-1)*2,112,1)
3030 NEXT I
3040 SUBEND
3060 SUB RSA(SS(,,))
3080 R=3::C=23
3100 FOR I=0 TO 5-1
3110 CALL GSL(SL,(I))
3120 FOR J=0 TO SL-1
3130 RO=INT(SS(0,I,J)/16) :: CO=SS(0,I,J)-INT(SS(0,I,J)/16)*16
3140 CALL HCHAR(R+RO-1,C+CO-1,112,1)
3150 NEXT J
3160 NEXT I
3170 SUBEND
3190 SUB RSO(P,H,SR,SC)
3200 R=14::C=23
3210 IF P=0 THEN R=R+4 ELSE R=R+9
3220 IF H=0 THEN T$="MISS" :: N=-3 ELSE T$="HIT" :: N=-8
3230 CALL HCHAR(R,C,32,10)
3240 CALL TXT((T$),(R),(C))
3250 IF H=1 THEN CV=104 ELSE CV=128
3260 IF P=1 THEN R=3::C=23 ELSE R=5::C=3
3270 TR=SR-1 :: TC=SC-1
3280 IF P=0 THEN TR=TR*2 :: TC=TC*2
3290 CALL HCHAR(R+TR,C+TC,CV,1)
3300 CALL SOUND(500,N,2) :: CALL SOUND(1,110,30)
3310 SUBEND
3330 SUB RSU(P,S,SS(,,))
3340 CALL GSL(SL,(S))
3350 IF P=0 THEN R=3::C=23 ELSE R=5::C=3
3370 FOR I=0 TO SL-1
3380 L=SS(P,S,I)
3390 TR=INT(L/16)-1 :: TC=L-INT(L/16)*16-1
3400 IF P=1 THEN TR=TR*2 :: TC=TC*2
3410 CALL HCHAR(R+TR,C+TC,136,1)
3420 NEXT I
3430 CALL GSN(S$,(S))
3440 R=14::C=23
3450 IF P=1 THEN R=R+4 ELSE R=R+9
3460 CALL HCHAR(R,C,32,10)
3470 CALL TXT("DESTROYED",(R),(C))
3480 CALL HCHAR(R,C,32,10)
3490 CALL TXT((S$),(R),(C))
3500 CALL SOUND(1000,-6,2) :: CALL SOUND(1,110,30)
3510 SUBEND
3530 SUB MCL
3540 R=14::C=23
3550 FOR I=0 TO 9 :: CALL HCHAR(R+I,C,32,10) :: NEXT I
3560 SUBEND
3580 SUB MAD(A$)
3590 R=14::C=23
3600 CALL MCL
3610 CALL TXT("AUTO",(R),(C))
3620 CALL TXT("DEPLOY",(R+1),(C))
3630 CALL TXT("YOUR",(R+2),(C))
3640 CALL TXT("SHIPS?",(R+3),(C))
3650 CALL TXT("[Y/N]:",(R+4),(C))
3670 ACCEPT AT(R+4,C+4)VALIDATE("YN")BEEP SIZE(1):A$
3680 IF A$="" THEN  3670
3690 SUBEND
3710 SUB MDE(CS)
3720 R=14::C=23
3730 CALL TXT("INPUT THE",(R),(C))
3740 CALL TXT("LOCATIONS",(R+1),(C))
3750 CALL TXT("FOR YOUR",(R+2),(C))
3760 CALL TXT("          ",(R+3),(C))
3770 CALL GSN(S$,(CS))
3780 CALL TXT((S$),(R+3),(C))
3790 CALL TXT("[IE. C3]:",(R+4),(C))
3800 CALL GSL(SL,(CS))
3810 FOR I=1 TO 5
3820 IF I>SL THEN T$="        " ELSE T$="POS"&STR$(I)&":    "
3830 CALL TXT((T$),(R+4+I),(C))
3840 NEXT I
3850 SUBEND
3870 SUB MTA
3880 R=14::C=23
3890 CALL TXT("PLAYER",(R),(C))
3900 CALL TXT("TARGET",(R+1),(C))
3910 CALL TXT("[IE. C3]",(R+2),(C))
3920 CALL TXT("COMPUTER",(R+6),(C))
3930 CALL TXT("TARGET",(R+7),(C))
3940 SUBEND
3960 SUB MDG
3970 R=14::C=23
3980 CALL MCL
3990 CALL TXT("PLAYER'S",(R),(C))
4000 CALL TXT("SHIPS",(R+1),(C))
4010 CALL TXT("DEPLOYED.",(R+2),(C))
4020 CALL TXT("COMPUTER",(R+4),(C))
4030 CALL TXT("DEPLOYING",(R+5),(C))
4040 CALL TXT("SHIPS...",(R+6),(C))
4050 SUBEND
4070 SUB MGO(P$,W)
4080 R=14::C=23
4090 CALL MCL
4100 CALL TXT("GAME OVER",(R),(C))
4110 CALL TXT("WINNER:",(R+1),(C))
4120 IF W=0 THEN T$="PLAYER" ELSE T$="COMPUTER"
4130 CALL TXT(T$,(R+2),(C))
4140 CALL TXT("PLAY", (R+4),(C))
4150 CALL TXT("AGAIN?",(R+5),(C))
4160 CALL TXT("[Y/N]:",(R+6),(C))
4180 ACCEPT AT(R+6,C+4)VALIDATE("YN")BEEP SIZE(1):P$
4190 IF P$="" THEN  4180
4200 SUBEND
4220 SUB IPO(R,C,IR$,IC)
4230 ON WARNING NEXT
4250 ACCEPT AT(R,C-2)VALIDATE("ABCDEFGHIJ")BEEP SIZE(1):IR$
4260 IF IR$="" THEN  4250
4280 ACCEPT AT(R,C-2+1)VALIDATE(DIGIT)BEEP SIZE(2):IC
4290 IF IC>10 THEN  4280
4300 IF IC<1 THEN  4280
4310 ON WARNING PRINT
4320 SUBEND
4340 SUB ISH(S(),CS)
4350 CALL GSL(SL,(CS))
4360 R=14::C=23
4370 FOR I=0 TO SL-1
4380 IR$="" :: IC=0
4390 CALL IPO((R+5+I),(C+5),IR$,IC)
4400 S(I)=16*(ASC(IR$)-64)+IC
4410 NEXT I
4420 SUBEND
4440 SUB ITA(AR$,AC)
4450 R=14::C=23
4460 CALL HCHAR(R+3,C,32,10)
4470 CALL IPO((R+3),(C),AR$,AC)
4480 SUBEND
4500 SUB CVI(E,P,CS,S(),SS(,,))
4510 E=0
4520 HZ=0
4530 CALL GSL(SL,(CS))
4540 CALL CHO(HZ,(CS),S())
4550 DIM SQ(4) :: FOR I=0 TO 4 :: SQ(I)=0 :: NEXT I
4560 FOR I=0 TO SL-1
4570 R=INT(S(I)/16) :: C=S(I)-INT(S(I)/16)*16
4580 IF HZ=1 THEN SQ(I)=C ELSE SQ(I)=R
4590 NEXT I
4600 CALL CSE(E,(CS),SQ())
4610 IF E=1 THEN 4630
4620 CALL COV(E,(P),(CS),S(),SS(,,))
4630 SUBEND
4650 SUB CVO(E,R,C,P,SH(,,))
4660 E=0
4670 IF R<1 OR R>10 OR C<1 OR C>10 THEN E=1 :: GOTO  4690
4680 IF SH(P,R-1,C-1)<>0 THEN E=1
4690 SUBEND
4710 SUB CHI(H,S,L,P,SS(,,))
4720 H=0
4740 FOR I=0 TO 5-1
4750 CALL GSL(SL,(I))
4760 FOR J=0 TO SL-1
4770 IF SS(P,I,J)=L THEN H=1 :: S=I :: GOTO 4800
4780 NEXT J
4790 NEXT I
4800 SUBEND
4820 SUB CHO(HZ,CS,S())
4830 HZ=0
4840 CALL GSL(SL,(CS))
4850 FOR I=1 TO SL-1
4860 PV=INT(S(I-1)/16) :: CU=INT(S(I)/16)
4870 IF PV<>CU THEN 4900
4880 NEXT I
4890 HZ=1
4900 SUBEND
4920 SUB CSE(E,CS,SQ())
4930 E=0
4940 CALL GSL(SL,(CS))
4950 ML=SQ(0)
4960 FOR I=1 TO SL-1
4970 ML=MIN(ML,SQ(I))
4980 NEXT I
4990 ES=ML
5000 FOR I=1 TO SL-1
5010 ES=ES+ML+I
5020 NEXT I
5030 AS=SQ(0)
5040 FOR I=1 TO SL-1
5050 AS=AS+SQ(I)
5060 NEXT I
5070 IF ES<>AS THEN E=1
5080 SUBEND
5100 SUB COV(E,P,CS,S(),SS(,,))
5110 E=0
5120 CALL GSL(CL,(CS))
5130 FOR I=0 TO CS-1
5140 CALL GSL(SL,(I))
5150 FOR J=0 TO SL-1
5160 SD=SS(P,I,J)
5170 FOR K=0 TO CL-1
5180 L=S(K)
5190 IF L=SD THEN E=1 :: GOTO 5230
5200 NEXT K
5210 NEXT J
5220 NEXT I
5230 SUBEND
5290 SUB QIN(Q(),QL)
5310 FOR I=0 TO 11-1 :: Q(I)=-1 :: NEXT I
5320 QL=0
5330 SUBEND
5350 SUB QAD(Q(),QL,V,E)
5370 IF QL<11-1 THEN Q(QL)=V :: QL=QL+1 :: E=0 ELSE E=1
5380 SUBEND
5400 SUB QDE(Q(),QL,V,E)
5410 DI=-1 :: E=-1
5420 FOR I=0 TO QL
5430 IF V=Q(I) THEN DI=I :: GOTO 5470
5440 NEXT I
5450 GOTO 5510
5470 FOR I=DI TO QL-1
5480 Q(I)=Q(I+1)
5490 NEXT I
5500 QL=QL-1 :: E=0
5510 SUBEND
5850 SUB GSL(SL,X)
5860 SA(0)=5 :: SA(1)=4 :: SA(2)=3 :: SA(3)=3 :: SA(4)=2 
5870 SL=SA(X)
5880 SUBEND
5900 SUB GSN(S$,X)
5910 N$(0)="CARRIER" :: N$(1)="BATTLESHIP" :: N$(2)="CRUISER" :: N$(3)="SUBMARINE" :: N$(4)="DESTROYER"
5920 S$=N$(X)
5930 SUBEND
6070 SUB GDF(D)
6080 D=0
6090 SUBEND
6110 SUB GAP(A)
6120 A=0
6130 SUBEND