100 REM  COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
110 CALL CLEAR
120 CALL SCREEN(4)
130 RANDOMIZE
140 DIM SS(1,4,4)
150 CALL SCH
160 CALL SCS
170 CALL RBS
180 CALL DSH(SS(,,))
190 CALL PGA(SS(,,))
200 GOTO 170
210 END
220 SUB SCH
230 CALL CHAR(96,"3C4299A5A599423C")
240 CALL CHAR(97,"0000000000000000")
250 CALL CHAR(104,"3C7EFFFFFFFF7E3C")
260 CALL CHAR(112,"3C4299A5A599423C")
270 CALL CHAR(120,"004FC949494949EF")
280 CALL CHAR(128,"3C7EFFFFFFFF7E3C")
290 CALL CHAR(136,"3C7EFFFFFFFF7E3C")
300 SUBEND
310 SUB SCS
320 CALL COLOR(9,2,8)
330 CALL COLOR(10,11,8)
340 CALL COLOR(11,2,15)
350 CALL COLOR(12,2,1)
360 CALL COLOR(13,16,8)
370 CALL COLOR(14,9,8)
380 SUBEND
390 SUB DSH(SS(,,))
400 CALL DPL(SS(,,))
410 CALL DCO(0,SS(,,))
420 SUBEND
430 SUB DPL(SS(,,))
440 A$="N"
450 CALL MAD(A$)
460 IF A$="Y" THEN CALL DAU(SS(,,))ELSE CALL DMA(SS(,,))
470 CALL RSA(SS(,,))
480 CALL RHO
490 CALL MDG
500 SUBEND
510 SUB DAU(SS(,,))
520 R=14 :: C=23
530 CALL TXT("AUTO",(R+6),(C))
540 CALL TXT("DEPLOYING",(R+7),(C))
550 CALL TXT("SHIPS...",(R+8),(C))
560 CALL DCO(1,SS(,,))
570 FOR I=0 TO 5-1
580 CALL GSL(SL,(I))
590 FOR J=0 TO SL-1
600 SS(0,I,J)=SS(1,I,J)
610 NEXT J
620 NEXT I
630 SUBEND
640 SUB DMA(SS(,,))
650 CALL MCL
660 DIM S(4) :: FOR I=0 TO 4 :: S(I)=0 :: NEXT I
670 FOR CS=0 TO 5-1
680 CALL MDE((CS))
690 CALL ISH(S(),(CS))
700 P=0
710 E=0
720 CALL CVI(E,(P),(CS),S(),SS(,,))
730 IF E=1 THEN 680
740 CALL GSL(SL,(CS))
750 FOR I=0 TO SL-1
760 SS(0,CS,I)=S(I)
770 NEXT I
780 CALL RSI((CS),S())
790 NEXT CS
800 SUBEND
810 SUB DCO(VI,SS(,,))
820 DIM S(4) :: FOR I=0 TO 4 :: S(I)=0 :: NEXT I
830 R=5 :: C=3
840 FOR CS=0 TO 5-1
850 CALL GSL(SL,(CS))
860 HZ=INT(RND*2)
870 SB=INT(RND*(10-SL))+1
880 ST=INT(RND*10)+1
890 FOR I=0 TO SL-1
900 IF HZ=1 THEN S(I)=16*ST+SB+I ELSE S(I)=16*(SB+I)+ST
910 NEXT I
920 P=1
930 E=0
940 CALL COV(E,(P),(CS),S(),SS(,,))
950 IF E=1 THEN 850
960 FOR I=0 TO SL-1
970 SS(1,CS,I)=S(I)
980 NEXT I
990 CALL GDF(D)
1000 IF D=1 THEN VI=1
1010 IF VI=1 THEN CALL RSI((CS),S())
1020 NEXT CS
1030 SUBEND
1040 SUB PGA(SS(,,))
1050 CALL MCL
1060 CALL MTA
1070 DIM SH(1,9,9)
1080 FOR I=0 TO 9 :: FOR J=0 TO 9 :: SH(0,I,J)=0 :: SH(1,I,J)=0 :: NEXT J :: NEXT I
1090 DIM HL(1,4)
1100 FOR I=0 TO 5-1
1110 CALL GSL(SL,(I))
1120 HL(0,I)=SL :: HL(1,I)=SL
1130 NEXT I
1140 DIM SM(1)
1150 SM(0)=5 :: SM(1)=5
1160 CALL QIN(Q(),QL)
1170 W=-1
1180 CALL GAP(A)
1190 IF A=1 THEN P=1 ELSE P=0
1200 IF P=0 THEN CALL PTU(R,C,SH(,,))
1210 IF P=1 THEN CALL CTU(R,C,Q(),QL,SH(,,))
1220 SH(P,R-1,C-1)=1
1230 CALL PSH(W,(R),(C),(P),HL(,),SM(),SS(,,),Q(),QL)
1240 IF W=0 OR W=1 THEN 1280
1250 CALL GAP(A)
1260 IF A=0 THEN P=1-P
1270 GOTO 1200
1280 P$="N"
1290 CALL MGO(P$,(W))
1300 IF P$="Y" THEN 1330
1310 CALL CLEAR
1320 END
1330 SUBEND
1340 SUB PTU(R,C,SH(,,))
1350 CALL ITA(R$,C)
1360 R=ASC(R$)-64
1370 CALL CVO(E,(R),(C),0,SH(,,))
1380 IF E=1 THEN 1350
1390 SUBEND
1400 SUB CTU(R,C,Q(),QL,SH(,,))
1410 CALL GAI(R,C,Q(),QL,SH(,,))
1420 MR=14 :: MC=23
1430 T$=CHR$(64+R)&STR$(C)
1440 CALL HCHAR(MR+8,MC,32,10)
1450 CALL TXT(T$,(MR+8),(MC))
1460 SUBEND
1470 SUB PSH(W,R,C,P,HL(,),SM(),SS(,,),Q(),QL)
1480 W=-1 :: H=0 :: S=0
1490 L=16*R+C
1500 CALL CHI(H,S,(L),(1-P),SS(,,))
1510 IF P=1 AND H=1 THEN CALL QAD(Q(),QL,(L),E)
1520 CALL RSO((P),(H),(R),(C))
1530 IF H=0 THEN 1650
1540 HL(1-P,S)=HL(1-P,S)-1
1550 IF HL(1-P,S)>0 THEN 1650
1560 SM(1-P)=SM(1-P)-1
1570 CALL RSU((1-P),(S),SS(,,))
1580 IF SM(1-P)=0 THEN W=P
1590 IF P=0 THEN 1650
1600 CALL GSL(SL,(S))
1610 FOR I=0 TO SL-1
1620 SU=SS(0,S,I)
1630 CALL QDE(Q(),QL,(SU),E)
1640 NEXT I
1650 SUBEND
1660 SUB GAI(R,C,Q(),QL,SH(,,))
1670 IF QL<=OL THEN O=0
1680 IF QL=0 THEN GOSUB 1780
1690 IF QL=0 THEN GOTO 1740
1700 IF O=0 THEN GOSUB 1810
1710 O=O+1
1720 R=INT(Q(0)/16) :: C=Q(0)-INT(Q(0)/16)*16
1730 R=R+O*RD :: C=C+O*CD
1740 CALL CVO(E,(R),(C),1,SH(,,))
1750 IF E=1 THEN O=0 :: GOTO 1670
1760 OL=QL
1770 GOTO 1880
1780 R=INT(RND*10)+1 :: C=INT(RND*10)+1
1790 RD=0 :: CD=0 :: RV=0 :: O=0
1800 RETURN
1810 IF RV=1 THEN T=RD :: RD=CD :: CD=T :: RV=0 :: RETURN
1820 IF RD<>0 THEN RD=RD*-1 :: RV=1 :: RETURN
1830 IF CD<>0 THEN CD=CD*-1 :: RV=1 :: RETURN
1840 DR=INT(RND*4)
1850 IF DR=0 OR DR=2 THEN CD=DR-1 ELSE RD=DR-2
1860 RV=0
1870 RETURN
1880 SUBEND
1890 SUB TXT(T$,R,C)
1900 TL=LEN(T$)
1910 FOR I=0 TO TL-1
1920 C$=SEG$(T$,I+1,1)
1930 CALL HCHAR(R,C+I,ASC(C$))
1940 NEXT I
1950 SUBEND
1960 SUB RBS
1970 CALL CLEAR
1980 DISPLAY AT(1,9):"BATTLESHIP"
1990 CALL RBO
2000 CALL RAU
2010 SUBEND
2020 SUB RBO
2030 CALL RBB
2040 CALL RAX
2050 CALL RHO
2060 SUBEND
2070 SUB RBB
2080 R=5 :: C=3
2090 FOR I=0 TO 18
2100 CALL HCHAR(R+I,C,97,19)
2110 NEXT I
2120 SUBEND
2130 SUB RHO
2140 R=5 :: C=3
2150 FOR I=0 TO 18 STEP 2
2160 FOR J=0 TO 18 STEP 2
2170 CALL HCHAR(R+I,C+J,96,1)
2180 NEXT J
2190 NEXT I
2200 SUBEND
2210 SUB RAX
2220 R=5 :: C=3
2230 FOR I=0 TO 8
2240 CALL HCHAR(R-2,I*2+C,49+I,1)
2250 NEXT I
2260 CALL HCHAR(R-2,C+18,120,1)
2270 FOR I=1 TO 10
2280 CALL HCHAR(R-2+I*2,C-2,64+I,1)
2290 NEXT I
2300 SUBEND
2310 SUB RAU
2320 R=3 :: C=23
2330 FOR I=0 TO 9
2340 CALL HCHAR(R+I,C,96,10)
2350 NEXT I
2360 SUBEND
2370 SUB RSI(CS,S())
2380 CALL GSL(SL,(CS))
2390 R=5 :: C=3
2400 FOR I=0 TO SL-1
2410 RO=INT(S(I)/16) :: CO=S(I)-INT(S(I)/16)*16
2420 CALL HCHAR(R+(RO-1)*2,C+(CO-1)*2,112,1)
2430 NEXT I
2440 SUBEND
2450 SUB RSA(SS(,,))
2460 R=3 :: C=23
2470 FOR I=0 TO 5-1
2480 CALL GSL(SL,(I))
2490 FOR J=0 TO SL-1
2500 RO=INT(SS(0,I,J)/16) :: CO=SS(0,I,J)-INT(SS(0,I,J)/16)*16
2510 CALL HCHAR(R+RO-1,C+CO-1,112,1)
2520 NEXT J
2530 NEXT I
2540 SUBEND
2550 SUB RSO(P,H,SR,SC)
2560 R=14 :: C=23
2570 IF P=0 THEN R=R+4 ELSE R=R+9
2580 IF H=0 THEN T$="MISS" :: N=-3 ELSE T$="HIT" :: N=-8
2590 CALL HCHAR(R,C,32,10)
2600 CALL TXT((T$),(R),(C))
2610 IF H=1 THEN CV=104 ELSE CV=128
2620 IF P=1 THEN R=3 :: C=23 ELSE R=5 :: C=3
2630 TR=SR-1 :: TC=SC-1
2640 IF P=0 THEN TR=TR*2 :: TC=TC*2
2650 CALL HCHAR(R+TR,C+TC,CV,1)
2660 CALL SOUND(500,N,2) :: CALL SOUND(1,110,30)
2670 SUBEND
2680 SUB RSU(P,S,SS(,,))
2690 CALL GSL(SL,(S))
2700 IF P=0 THEN R=3 :: C=23 ELSE R=5 :: C=3
2710 FOR I=0 TO SL-1
2720 L=SS(P,S,I)
2730 TR=INT(L/16)-1 :: TC=L-INT(L/16)*16-1
2740 IF P=1 THEN TR=TR*2 :: TC=TC*2
2750 CALL HCHAR(R+TR,C+TC,136,1)
2760 NEXT I
2770 CALL GSN(S$,(S))
2780 R=14 :: C=23
2790 IF P=1 THEN R=R+4 ELSE R=R+9
2800 CALL HCHAR(R,C,32,10)
2810 CALL TXT("DESTROYED",(R),(C))
2820 CALL HCHAR(R,C,32,10)
2830 CALL TXT((S$),(R),(C))
2840 CALL SOUND(1000,-6,2) :: CALL SOUND(1,110,30)
2850 SUBEND
2860 SUB MCL
2870 R=14 :: C=23
2880 FOR I=0 TO 9 :: CALL HCHAR(R+I,C,32,10) :: NEXT I
2890 SUBEND
2900 SUB MAD(A$)
2910 R=14 :: C=23
2920 CALL MCL
2930 CALL TXT("AUTO",(R),(C))
2940 CALL TXT("DEPLOY",(R+1),(C))
2950 CALL TXT("YOUR",(R+2),(C))
2960 CALL TXT("SHIPS?",(R+3),(C))
2970 CALL TXT("[Y/N]:",(R+4),(C))
2980 ACCEPT AT(R+4,C+4)VALIDATE("YN")BEEPSIZE(1):A$
2990 IF A$="" THEN 2980
3000 SUBEND
3010 SUB MDE(CS)
3020 R=14 :: C=23
3030 CALL TXT("INPUT THE",(R),(C))
3040 CALL TXT("LOCATIONS",(R+1),(C))
3050 CALL TXT("FOR YOUR",(R+2),(C))
3060 CALL TXT("          ",(R+3),(C))
3070 CALL GSN(S$,(CS))
3080 CALL TXT((S$),(R+3),(C))
3090 CALL TXT("[IE. C3]:",(R+4),(C))
3100 CALL GSL(SL,(CS))
3110 FOR I=1 TO 5
3120 IF I>SL THEN T$="        " ELSE T$="POS"&STR$(I)&":    "
3130 CALL TXT((T$),(R+4+I),(C))
3140 NEXT I
3150 SUBEND
3160 SUB MTA
3170 R=14 :: C=23
3180 CALL TXT("PLAYER",(R),(C))
3190 CALL TXT("TARGET",(R+1),(C))
3200 CALL TXT("[IE. C3]",(R+2),(C))
3210 CALL TXT("COMPUTER",(R+6),(C))
3220 CALL TXT("TARGET",(R+7),(C))
3230 SUBEND
3240 SUB MDG
3250 R=14 :: C=23
3260 CALL MCL
3270 CALL TXT("PLAYER'S",(R),(C))
3280 CALL TXT("SHIPS",(R+1),(C))
3290 CALL TXT("DEPLOYED.",(R+2),(C))
3300 CALL TXT("COMPUTER",(R+4),(C))
3310 CALL TXT("DEPLOYING",(R+5),(C))
3320 CALL TXT("SHIPS...",(R+6),(C))
3330 SUBEND
3340 SUB MGO(P$,W)
3350 R=14 :: C=23
3360 CALL MCL
3370 CALL TXT("GAME OVER",(R),(C))
3380 CALL TXT("WINNER:",(R+1),(C))
3390 IF W=0 THEN T$="PLAYER" ELSE T$="COMPUTER"
3400 CALL TXT(T$,(R+2),(C))
3410 CALL TXT("PLAY",(R+4),(C))
3420 CALL TXT("AGAIN?",(R+5),(C))
3430 CALL TXT("[Y/N]:",(R+6),(C))
3440 ACCEPT AT(R+6,C+4)VALIDATE("YN")BEEPSIZE(1):P$
3450 IF P$="" THEN 3440
3460 SUBEND
3470 SUB IPO(R,C,IR$,IC)
3480 ON WARNING NEXT
3490 ACCEPT AT(R,C-2)VALIDATE("ABCDEFGHIJ")BEEPSIZE(1):IR$
3500 IF IR$="" THEN 3490
3510 ACCEPT AT(R,C-2+1)VALIDATE(DIGIT)BEEPSIZE(2):IC
3520 IF IC>10 THEN 3510
3530 IF IC<1 THEN 3510
3540 ON WARNING PRINT
3550 SUBEND
3560 SUB ISH(S(),CS)
3570 CALL GSL(SL,(CS))
3580 R=14 :: C=23
3590 FOR I=0 TO SL-1
3600 IR$="" :: IC=0
3610 CALL IPO((R+5+I),(C+5),IR$,IC)
3620 S(I)=16*(ASC(IR$)-64)+IC
3630 NEXT I
3640 SUBEND
3650 SUB ITA(AR$,AC)
3660 R=14 :: C=23
3670 CALL HCHAR(R+3,C,32,10)
3680 CALL IPO((R+3),(C),AR$,AC)
3690 SUBEND
3700 SUB CVI(E,P,CS,S(),SS(,,))
3710 E=0
3720 HZ=0
3730 CALL GSL(SL,(CS))
3740 CALL CHS(E,(CS),S())
3750 IF E=1 THEN 3850
3760 CALL CHO(HZ,(CS),S())
3770 DIM SQ(4) :: FOR I=0 TO 4 :: SQ(I)=0 :: NEXT I
3780 FOR I=0 TO SL-1
3790 R=INT(S(I)/16) :: C=S(I)-INT(S(I)/16)*16
3800 IF HZ=1 THEN SQ(I)=C ELSE SQ(I)=R
3810 NEXT I
3820 CALL CSE(E,(CS),SQ())
3830 IF E=1 THEN 3850
3840 CALL COV(E,(P),(CS),S(),SS(,,))
3850 SUBEND
3860 SUB CVO(E,R,C,P,SH(,,))
3870 E=0
3880 IF R<1 OR R>10 OR C<1 OR C>10 THEN E=1 :: GOTO 3900
3890 IF SH(P,R-1,C-1)<>0 THEN E=1
3900 SUBEND
3910 SUB CHI(H,S,L,P,SS(,,))
3920 H=0
3930 FOR I=0 TO 5-1
3940 CALL GSL(SL,(I))
3950 FOR J=0 TO SL-1
3960 IF SS(P,I,J)=L THEN H=1 :: S=I :: GOTO 3990
3970 NEXT J
3980 NEXT I
3990 SUBEND
4000 SUB CHO(HZ,CS,S())
4010 HZ=0
4020 CALL GSL(SL,(CS))
4030 FOR I=1 TO SL-1
4040 PV=INT(S(I-1)/16) :: CU=INT(S(I)/16)
4050 IF PV<>CU THEN 4080
4060 NEXT I
4070 HZ=1
4080 SUBEND
4090 SUB CHS(E,CS,S())
4100 E=0
4110 CALL GSL(SL,(CS))
4120 FOR I=1 TO SL-1
4130 R=INT(S(I)/16) :: C=S(I)-INT(S(I)/16)*16
4140 PR=INT(S(I-1)/16) :: PC=S(I-1)-INT(S(I-1)/16)*16
4150 IF(R<>PR)AND(C<>PC)THEN E=1 :: GOTO 4170
4160 NEXT I
4170 SUBEND
4180 SUB CSE(E,CS,SQ())
4190 E=0
4200 CALL GSL(SL,(CS))
4210 ML=SQ(0)
4220 FOR I=1 TO SL-1
4230 ML=MIN(ML,SQ(I))
4240 NEXT I
4250 ES=ML
4260 FOR I=1 TO SL-1
4270 ES=ES+ML+I
4280 NEXT I
4290 AS=SQ(0)
4300 FOR I=1 TO SL-1
4310 AS=AS+SQ(I)
4320 NEXT I
4330 IF ES<>AS THEN E=1
4340 SUBEND
4350 SUB COV(E,P,CS,S(),SS(,,))
4360 E=0
4370 CALL GSL(CL,(CS))
4380 FOR I=0 TO CS-1
4390 CALL GSL(SL,(I))
4400 FOR J=0 TO SL-1
4410 SD=SS(P,I,J)
4420 FOR K=0 TO CL-1
4430 L=S(K)
4440 IF L=SD THEN E=1 :: GOTO 4480
4450 NEXT K
4460 NEXT J
4470 NEXT I
4480 SUBEND
4490 SUB QIN(Q(),QL)
4500 FOR I=0 TO 11-1 :: Q(I)=-1 :: NEXT I
4510 QL=0
4520 SUBEND
4530 SUB QAD(Q(),QL,V,E)
4540 IF QL<11-1 THEN Q(QL)=V :: QL=QL+1 :: E=0 ELSE E=1
4550 SUBEND
4560 SUB QDE(Q(),QL,V,E)
4570 DI=-1 :: E=-1
4580 FOR I=0 TO QL
4590 IF V=Q(I)THEN DI=I :: GOTO 4620
4600 NEXT I
4610 GOTO 4660
4620 FOR I=DI TO QL-1
4630 Q(I)=Q(I+1)
4640 NEXT I
4650 QL=QL-1 :: E=0
4660 SUBEND
4670 SUB GSL(SL,X)
4680 SA(0)=5 :: SA(1)=4 :: SA(2)=3 :: SA(3)=3 :: SA(4)=2
4690 SL=SA(X)
4700 SUBEND
4710 SUB GSN(S$,X)
4720 N$(0)="CARRIER" :: N$(1)="BATTLESHIP" :: N$(2)="CRUISER" :: N$(3)="SUBMARINE" :: N$(4)="DESTROYER"
4730 S$=N$(X)
4740 SUBEND
4750 SUB GDF(D)
4760 D=0
4770 SUBEND
4780 SUB GAP(A)
4790 A=0
4800 SUBEND