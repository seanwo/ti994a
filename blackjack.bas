100 REM COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
110 REM ::
120 REM THIS PROGRAM IS FREE SOFTWARE: YOU CAN REDISTRIBUTE IT AND/OR MODIFY IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE
130 REM AS PUBLISHED BY THE FREE SOFTWARE FOUNDATION, EITHER VERSION 3 OF THE LICENSE, OR (AT YOUR OPTION) ANY LATER VERSION.
140 REM ::
150 REM THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY
160 REM OF MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
170 REM ::
180 REM YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE ALONG WITH THIS PROGRAM. IF NOT, SEE
190 REM <HTTPS://WWW.GNU.ORG/LICENSES/>.
200 REM ::
210 CALL INTRO
220 CALL SETCHARS
230 CALL SETCOLORSCHEME
240 REM LABEL ***GAMELOOP***
250 CALL PLAY
260 REM LABEL ***ASKAGAIN***
270 DISPLAY AT (22,2) BEEP: "DO YOU WISH TO PLAY AGAIN?"
280 ACCEPT AT (23,14) SIZE (1) BEEP VALIDATE ("YN") : A$
290 IF A$="" THEN 270 :: REM GOTO ASKAGAIN
300 IF A$="N" THEN CALL TERMINATE
310 GOTO 250 :: REM GOTO GAMELOOP
320 END
330 REM SUBROUTINE ***INTRO***
340 SUB INTRO
350 CALL CLEAR
360 DISPLAY AT (12,10): "BLACKJACK"
370 DISPLAY AT (13,1): "presented by Sean Wohlgemuth"
380 FOR I=1 TO 5 :: CALL SOUND(1000,110,30) :: CALL SOUND(1,110,30) :: NEXT I
390 CALL CLEAR
400 DISPLAY AT (1, 1): "PREFACE:"
410 DISPLAY AT (3, 1): "THIS IS A GAME BETWEEN YOU"
420 DISPLAY AT (4, 1): "AND THE DEALER (COMPUTER)."
430 DISPLAY AT (6, 1): "OBJECT OF THE GAME:"
440 DISPLAY AT (8, 1): "THE OBJECT OF THE GAME IS TO"
450 DISPLAY AT (9, 1): "COLLECT A SET OF CARDS LESS"
460 DISPLAY AT (10, 1): "THAN OR EQUAL TO 21. IF YOUR"
470 DISPLAY AT (11, 1): "SET OF CARDS IS HIGHER THAN"
480 DISPLAY AT (12, 1): "THE DEALER'S AND NOT OVER 21"
490 DISPLAY AT (13, 1): "THEN YOU WIN."
500 DISPLAY AT (15, 1): "CARD VALUES:"
510 DISPLAY AT (17, 1): "NUMBER CARDS = FACE VALUE"
520 DISPLAY AT (18, 1): "FACE CARDS   = 10 PTS"
530 DISPLAY AT (19, 1): "ACES         = 11 OR 1 PT(S)"
540 DISPLAY AT (24,1): "PRESS ENTER TO CONTINUE:"
550 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
560 CALL CLEAR
570 DISPLAY AT (1, 1): "HOW TO PLAY:"
580 DISPLAY AT (3, 1): "BOTH YOU AND THE DEALER WILL"
590 DISPLAY AT (4, 1): "BE GIVEN TWO CARDS TO START."
600 DISPLAY AT (5, 1): "TAKE AS MANY CARDS AS YOU"
610 DISPLAY AT (6, 1): "WISH (UP TO 5 CARDS). IF"
620 DISPLAY AT (7, 1): "THE VALUE OF YOUR HAND"
630 DISPLAY AT (8, 1): "EXCEEDS 21 YOU BUST. ONCE"
640 DISPLAY AT (9, 1): "YOU STOP TAKING CARDS, THE"
650 DISPLAY AT (10, 1): "DEALER WILL TAKE CARDS"
660 DISPLAY AT (11, 1): "TRYING NOT TO BUST AS WELL."
670 DISPLAY AT (12, 1): "THE PLAYER WITH THE HIGHEST"
680 DISPLAY AT (13, 1): "HAND VALUE WINS."
690 DISPLAY AT (24,1): "PRESS ENTER TO BEGIN:"
700 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
710 SUBEND
720 REM SUBROUTINE ***SETCHARS***
730 SUB SETCHARS
740 CALL SETCHARSRED
750 CALL SETCHARSBLACK
760 CALL SETCHARSEDGES
770 CALL GETCARDFRONT(CARDFRONT)
780 CALL CHAR (CARDFRONT, "FFFFFFFFFFFFFFFF")
790 SUBEND
800 REM SUBROUTINE ***SETCHARSRED***
810 SUB SETCHARSRED
820 CALL GETHEART(HEART)
830 CALL CHAR (HEART, "00367F7F7F3E1C08" )
840 CALL GETDIAMOND(DIAMOND)
850 CALL CHAR (DIAMOND, "00081C3E7F3E1C08" )
860 CALL GETREDVALS(REDVALS)
870 CALL SETCHARSVALS((REDVALS))
880 CALL GETCARDBACK(CARDBACK)
890 CALL CHAR (CARDBACK, "AA55AA55AA55AA55")
900 SUBEND
910 REM SUBROUTINE ***SETCHARSBLACK***
920 SUB SETCHARSBLACK
930 CALL GETSPADE(SPADE)
940 CALL CHAR (SPADE, "0010387CFEFED638" )
950 CALL GETCLUB(CLUB)
960 CALL CHAR (CLUB, "00103854EE541038" )
970 CALL GETBLACKVALS(BLACKVALS)
980 CALL SETCHARSVALS((BLACKVALS))
990 SUBEND
1000 REM SUBROUTINE ***SETCHARSVALS***
1010 SUB SETCHARSVALS(OFFSET)
1020 REM 2-10, JACK, QUEEN, KING, ACE
1030 CALL CHAR (OFFSET, "003844040830407C") :: OFFSET=OFFSET+1
1040 CALL CHAR (OFFSET, "0038040418040438") :: OFFSET=OFFSET+1
1050 CALL CHAR (OFFSET, "002424243C040404") :: OFFSET=OFFSET+1
1060 CALL CHAR (OFFSET, "003C20203C04043C") :: OFFSET=OFFSET+1
1070 CALL CHAR (OFFSET, "001C202038242418") :: OFFSET=OFFSET+1
1080 CALL CHAR (OFFSET, "003C040408081010") :: OFFSET=OFFSET+1
1090 CALL CHAR (OFFSET, "003C24243C24243C") :: OFFSET=OFFSET+1
1100 CALL CHAR (OFFSET, "001C24241C040404") :: OFFSET=OFFSET+1
1110 CALL CHAR (OFFSET, "004C52525252524C") :: OFFSET=OFFSET+1
1120 CALL CHAR (OFFSET, "003E080808084830") :: OFFSET=OFFSET+1
1130 CALL CHAR (OFFSET, "0018242424241C02") :: OFFSET=OFFSET+1
1140 CALL CHAR (OFFSET, "0024242830282424") :: OFFSET=OFFSET+1
1150 CALL CHAR (OFFSET, "001824243C242424") :: OFFSET=OFFSET+1
1160 SUBEND
1170 REM SUBROUTINE ***SETCHARSEDGES***
1180 SUB SETCHARSEDGES
1190 CALL GETCARDEDGE(CARDEDGE)
1200 CALL CHAR (CARDEDGE, "8080808080808080")
1210 CALL GETCARDTOP(CARDTOP)
1220 CALL CHAR (CARDTOP, "00000000000000FF")
1230 SUBEND
1240 REM SUBROUTINE ***SETCOLORSCHEME***
1250 SUB SETCOLORSCHEME
1260 CALL COLOR (9,7,16) :: REM DARK RED ON BLACK
1270 CALL COLOR (10,7,16) :: REM DARK RED ON BLACK
1280 CALL COLOR (11,2,16) :: REM BLACK ON WHITE
1290 CALL COLOR (12,2,16) :: REM BLACK ON WHITE
1300 CALL COLOR (13,2,1) :: REM BLACK ON TRANSPARENT
1310 CALL COLOR (14,16,16) :: REM WHITE ON WHITE
1320 CALL SCREEN (3) :: REM MED GREEN BACKGROUND
1330 SUBEND
1340 REM SUBROUTINE ***RENDERCARD***
1350 SUB RENDERCARD(PLAYER,CARD,FACEDOWN,HANDS(,,))
1360 IF FACEDOWN=1 THEN CALL GETCARDBACK(FILL) ELSE CALL GETCARDFRONT(FILL)
1370 COL=CARD*6 :: ROW=PLAYER*10
1380 REM RENDER SURFACE
1390 FOR I=1 TO 5
1400 CALL VCHAR (4+ROW, 1+COL+I, FILL, 7)
1410 NEXT I
1420 CALL GETCARDTOP(CARDTOP)
1430 CALL GETCARDEDGE(CARDEDGE)
1440 CALL HCHAR (3+ROW, 2+COL, CARDTOP, 5)
1450 CALL VCHAR (4+ROW, 7+COL, CARDEDGE, 7)
1460 IF FACEDOWN=1 THEN 1730 :: REM SUBEND
1470 REM SELECT SUIT
1480 IF HANDS(PLAYER,CARD,1)=1 THEN CALL GETHEART(SUIT)
1490 IF HANDS(PLAYER,CARD,1)=2 THEN CALL GETDIAMOND(SUIT)
1500 IF HANDS(PLAYER,CARD,1)=3 THEN CALL GETSPADE(SUIT)
1510 IF HANDS(PLAYER,CARD,1)=4 THEN CALL GETCLUB(SUIT)
1520 REM RENDER CARD VALUE
1530 IF HANDS(PLAYER,CARD,1)<3 THEN CALL GETREDVALS(CARDCHAR)
1540 IF HANDS(PLAYER,CARD,1)>2 THEN CALL GETBLACKVALS(CARDCHAR)
1550 CARDVAL=HANDS(PLAYER,CARD,0)
1560 CARDCHAR=CARDCHAR+CARDVAL-2
1570 CALL HCHAR (4+ROW, 2+COL, CARDCHAR, 1)
1580 CALL HCHAR (10+ROW, 6+COL, CARDCHAR, 1)
1590 REM RENDER CARD FACE
1600 IF CARDVAL=2 THEN CALL RENDER2(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1610 IF CARDVAL=3 THEN CALL RENDER3(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1620 IF CARDVAL=4 THEN CALL RENDER4(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1630 IF CARDVAL=5 THEN CALL RENDER5(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1640 IF CARDVAL=6 THEN CALL RENDER6(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1650 IF CARDVAL=7 THEN CALL RENDER7(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1660 IF CARDVAL=8 THEN CALL RENDER8(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1670 IF CARDVAL=9 THEN CALL RENDER9(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1680 IF CARDVAL=10 THEN CALL RENDER10(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1690 IF CARDVAL=11 THEN CALL RENDERJACK(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1700 IF CARDVAL=12 THEN CALL RENDERQUEEN(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1710 IF CARDVAL=13 THEN CALL RENDERKING(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1720 IF CARDVAL=14 THEN CALL RENDERACE(ROW,COL,SUIT) :: GOTO 1730 :: REM SUBEND
1730 SUBEND
1740 REM SUBROUTINE ***RENDER2***
1750 SUB RENDER2(ROW,COL,SUIT)
1760 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1770 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1780 SUBEND
1790 REM SUBROUTINE ***RENDER3***
1800 SUB RENDER3(ROW,COL,SUIT)
1810 CALL VCHAR (6+ROW,4+COL,SUIT,3)
1820 SUBEND
1830 REM SUBROUTINE ***RENDER4***
1840 SUB RENDER4(ROW,COL,SUIT)
1850 CALL HCHAR (6+ROW,3+COL,SUIT,1)
1860 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1870 CALL HCHAR (6+ROW,5+COL,SUIT,1)
1880 CALL HCHAR (8+ROW,5+COL,SUIT,1)
1890 SUBEND
1900 REM SUBROUTINE ***RENDER5***
1910 SUB RENDER5(ROW,COL,SUIT)
1920 CALL RENDER4((ROW),(COL),(SUIT))
1930 CALL HCHAR (7+ROW,4+COL,SUIT,1)
1940 SUBEND
1950 REM SUBROUTINE ***RENDER6***
1960 SUB RENDER6(ROW,COL,SUIT)
1970 CALL vCHAR (6+ROW,3+COL,SUIT,3)
1980 CALL vCHAR (6+ROW,5+COL,SUIT,3)
1990 SUBEND
2000 REM SUBROUTINE ***RENDER7***
2010 SUB RENDER7(ROW,COL,SUIT)
2020 CALL RENDER6((ROW),(COL),(SUIT))
2030 CALL VCHAR (7+ROW,4+COL,SUIT,1) 
2040 SUBEND
2050 REM SUBROUTINE ***RENDER8***
2060 SUB RENDER8(ROW,COL,SUIT)
2070 CALL HCHAR (5+ROW,3+COL,SUIT,1)
2080 CALL HCHAR (5+ROW,5+COL,SUIT,1)
2090 CALL HCHAR (6+ROW,4+COL,SUIT,1)
2100 CALL HCHAR (7+ROW,3+COL,SUIT,1)
2110 CALL HCHAR (7+ROW,5+COL,SUIT,1)
2120 CALL HCHAR (8+ROW,4+COL,SUIT,1)
2130 CALL HCHAR (9+ROW,3+COL,SUIT,1)
2140 CALL HCHAR (9+ROW,5+COL,SUIT,1)
2150 SUBEND
2160 REM SUBROUTINE ***RENDER9***
2170 SUB RENDER9(ROW,COL,SUIT)
2180 CALL VCHAR (5+ROW,3+COL,SUIT,2)
2190 CALL VCHAR (5+ROW,5+COL,SUIT,2)
2200 CALL VCHAR (8+ROW,3+COL,SUIT,2)
2210 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2220 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2230 SUBEND
2240 REM SUBROUTINE ***RENDER10***
2250 SUB RENDER10(ROW,COL,SUIT)
2260 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2270 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2280 SUBEND
2290 REM SUBROUTINE ***RENDERJACK***
2300 SUB RENDERJACK(ROW,COL,SUIT)
2310 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2320 CALL HCHAR (9+ROW,3+COL,SUIT,2)
2330 CALL HCHAR (8+ROW,3+COL,SUIT,1)
2340 SUBEND
2350 REM SUBROUTINE ***RENDERQUEEN***
2360 SUB RENDERQUEEN(ROW,COL,SUIT)
2370 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2380 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2390 CALL HCHAR (5+ROW,4+COL,SUIT,1)
2400 CALL HCHAR (9+ROW,4+COL,SUIT,1)
2410 SUBEND
2420 REM SUBROUTINE ***RENDERKING***
2430 SUB RENDERKING(ROW,COL,SUIT)
2440 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2450 CALL VCHAR (5+ROW,5+COL,SUIT,2)
2460 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2470 CALL VCHAR (7+ROW,4+COL,SUIT,1)
2480 SUBEND
2490 REM SUBROUTINE ***RENDERACE***
2500 SUB RENDERACE(ROW,COL,SUIT)
2510 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2520 SUBEND
2530 REM SUBROUTINE ***PLAY***
2540 SUB PLAY
2550 DIM HANDS(1,4,1) :: REM HANDS[PLAYER,CARD,SUIT]
2560 DIM CARDS(1) :: REM CARDS[PLAYER]
2570 DIM SUMS(1) :: REM SUMS[PLAYER]
2580 CALL CLEARTABLE(HANDS(,,),CARDS(),SUMS())
2590 CALL SHUFFLE(HANDS(,,))
2600 PLAYER=0 :: FACEDOWN=0
2610 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2620 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2630 PLAYER=1 :: FACEDOWN=0
2640 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2650 PLAYER=1 :: FACEDOWN=1
2660 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2670 REM PLAYERS TURNS
2680 BUSTED=0
2690 FOR J=3 TO 5
2700 DISPLAY AT (22,2): "DO YOU WANT ANOTHER CARD?"
2710 REM LABEL ***ASKANOTHER***
2720 ACCEPT AT (23,14) VALIDATE ("YN") BEEP SIZE (1) : A$
2730 IF A$="" THEN 2720 :: REM GOTO ASKANOTHER
2740 DISPLAY AT (22,2): "                         " :: DISPLAY AT (23,14): " "
2750 IF A$="N" THEN 2830 :: REM GOTO REVEAL
2760 PLAYER=0 :: FACEDOWN=0
2770 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2780 IF SUMS(PLAYER)>21 THEN BUSTED=1
2790 IF BUSTED=1 THEN DISPLAY AT (12,4) BEEP: "YOU BUSTED. YOU LOSE."
2800 IF BUSTED=1 THEN 2830 :: REM GOTO REVEAL
2810 NEXT J
2820 REM LABEL ***REVEAL***
2830 PLAYER=1 :: FACEDOWN=0 :: CARD=1
2840 CALL RENDERCARD((PLAYER),(CARD),(FACEDOWN),HANDS(,,))
2850 IF BUSTED=1 THEN 3000 :: REM GOTO GAMEOVER
2860 REM DEALERS TURNS
2870 BUSTED=0
2880 FOR J=3 TO 5
2890 PLAYER=1 :: FACEDOWN=0
2900 CALL CALCSCORE(SCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
2910 IF SCORE>=17 THEN 2980 :: REM GOTO TALLY
2920 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2930 IF SUMS(PLAYER)>21 THEN BUSTED=1
2940 IF BUSTED=1 THEN DISPLAY AT (12,3) BEEP: "DEALER BUSTED. YOU WIN."
2950 IF BUSTED=1 THEN 3000 :: REM GOTO GAMEOVER
2960 NEXT J
2970 REM LABEL ***TALLY***
2980 CALL WHOWON(SUMS(),CARDS(),HANDS(,,))
2990 REM LABEL ***GAMEOVER***
3000 SUBEND
3010 REM SUBROUTINE ***CLEARTABLE***
3020 SUB CLEARTABLE(HANDS(,,),CARDS(),SUMS())
3030 CALL CLEAR
3040 DISPLAY AT (1, 10): "BLACKJACK"
3050 FOR I=0 TO 1 :: SUMS(I)=0 :: CARDS(I)=0 :: NEXT I
3060 FOR I=0 TO 1 :: FOR J=0 TO 4 :: FOR K=0 TO 1 :: HANDS(I,J,K)=0 :: NEXT K :: NEXT J :: NEXT I
3070 SUBEND
3080 REM SUBROUTINE ***SHUFFLE***
3090 SUB SHUFFLE(HANDS(,,))
3100 RANDOMIZE
3110 DIM DECK(9) :: FOR I=0 TO 9 :: DECK(I)=0 :: NEXT I
3120 FOR H=0 TO 9
3130 REM LABEL ***DRAWLOOP***
3140 DISPLAY AT(12,6): "SHUFFLING THE DECK"
3150 X=INT(RND*4)+1
3160 X=X*16
3170 X=X+INT(RND*13)+2
3180 DUP=0
3190 CALL CHECKDUP(DUP,(X),DECK())
3200 IF DUP=1 THEN 3140 :: REM GOTO DRAWLOOP
3210 DECK(H)=X
3220 NEXT H
3230 FOR H=0 TO 4
3240 HANDS(0,H,1)=INT(DECK(H)/16)
3250 HANDS(0,H,0)=DECK(H)-HANDS(0,H,1)*16
3260 NEXT H
3270 FOR H=0 TO 4
3280 HANDS(1,H,1)=INT(DECK(H+5)/16)
3290 HANDS(1,H,0)=DECK(H+5)-HANDS(1,H,1)*16
3300 NEXT H
3310 DISPLAY AT(12,6): "                  "
3320 SUBEND
3330 REM SUBROUTINE ***CHECKDUP***
3340 SUB CHECKDUP(DUP,X,DECK())
3350 DUP=0
3360 FOR I=0 TO 9
3370 IF DECK(I)=X THEN DUP=1 :: GOTO 3390 :: REM SUBEND
3380 NEXT I
3390 SUBEND
3400 REM SUBROUTINE ***DEAL***
3410 SUB DEAL(CARDS(),SUMS(),PLAYER,FACEDOWN,HANDS(,,))
3420 CARDS(PLAYER)=CARDS(PLAYER)+1
3430 CARD=CARDS(PLAYER)-1
3440 CVAL=HANDS(PLAYER,CARD,0)
3450 IF CVAL>10 THEN IF CVAL=14 THEN CVAL=1 ELSE CVAL=10
3460 SUMS(PLAYER)=SUMS(PLAYER)+CVAL
3470 CALL RENDERCARD((PLAYER),(CARD),(FACEDOWN),HANDS(,,))
3480 SUBEND
3490 REM SUBROUTINE ***CALCSCORE***
3500 SUB CALCSCORE(SCORE,PLAYER,SUMS(),CARDS(),HANDS(,,))
3510 SCORE=SUMS(PLAYER)
3520 IF SCORE+10>21 THEN 3560 :: REM GOTO CALCSCORE.SUBEND
3530 FOR I=0 TO CARDS(PLAYER)-1
3540 IF HANDS(PLAYER,I,0)=14 THEN SCORE=SCORE+10 :: GOTO 3560 :: REM GOTO CALCSCORE.SUBEND
3550 NEXT I
3560 SUBEND
3570 REM SUBROUTINE ***WHOWON***
3580 SUB WHOWON(SUMS(),CARDS(),HANDS(,,))
3590 PLAYER=0 :: PSCORE=0
3600 CALL CALCSCORE(PSCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
3610 REM DISPLAY AT (1,24): "P:"&STR$(PSCORE)
3620 PLAYER=1 :: DSCORE=0
3630 CALL CALCSCORE(DSCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
3640 REM DISPLAY AT (2,24):"D:"&STR$(DSCORE)
3650 IF PSCORE=DSCORE THEN DISPLAY AT (12,11): "TIE GAME"
3660 IF PSCORE>DSCORE THEN DISPLAY AT (12,11): "YOU WIN!"
3670 IF PSCORE<DSCORE THEN DISPLAY AT (12,11): "YOU LOSE"
3680 SUBEND
3690 REM SUBROUTINE ***TERMINATE***
3700 SUB TERMINATE
3710 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT (12,7) BEEP: "GAME TERMINATED" :: CALL SOUND(100,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3720 FOR I=1 TO 5 :: CALL SOUND(500,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3730 END
3740 SUBEND
3750 REM SUBROUTINE ***GETHEART***
3760 SUB GETHEART(CHARVAL)
3770 CHARVAL=96
3780 SUBEND
3790 REM SUBROUTINE ***GETDIAMOND***
3800 SUB GETDIAMOND(CHARVAL)
3810 CHARVAL=97
3820 SUBEND
3830 REM SUBROUTINE ***GETREDVALS***
3840 SUB GETREDVALS(CHARVAL)
3850 CHARVAL=98
3860 SUBEND
3870 REM SUBROUTINE ***GETCARDBACK***
3880 SUB GETCARDBACK(CHARVAL)
3890 CHARVAL=111
3900 SUBEND
3910 REM SUBROUTINE ***GETSPADE***
3920 SUB GETSPADE(CHARVAL)
3930 CHARVAL=112
3940 SUBEND
3950 REM SUBROUTINE ***GETCLUB***
3960 SUB GETCLUB(CHARVAL)
3970 CHARVAL=113
3980 SUBEND
3990 REM SUBROUTINE ***GETBLACKVALS***
4000 SUB GETBLACKVALS(CHARVAL)
4010 CHARVAL=114
4020 SUBEND
4030 REM SUBROUTINE ***GETCARDEDGE***
4040 SUB GETCARDEDGE(CHARVAL)
4050 CHARVAL=128
4060 SUBEND
4070 REM SUBROUTINE ***GETCARDTOP***
4080 SUB GETCARDTOP(CHARVAL)
4090 CHARVAL=129
4100 SUBEND
4110 REM SUBROUTINE ***GETCARDFRONT***
4120 SUB GETCARDFRONT(CHARVAL)
4130 CHARVAL=136
4140 SUBEND