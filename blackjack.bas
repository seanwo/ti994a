100 CALL CLEAR
110 GOSUB 240 :: REM CALL INTRO
120 DIM HANDS(1,4,1) :: REM HANDS[PLAYER,CARD,SUIT]
130 DIM CARDS(1) :: REM CARDS[PLAYER]
140 DIM SUMS(1) :: REM SUMS[PLAYER]
150 USRDEFCHARSET=9
160 GOSUB 600 :: REM CALL SETCHARS
170 GOSUB 1050 :: REM CALL SETCOLORSCHEME
180 REM ***GAMELOOP***
190 GOSUB 2320 :: REM CALL PLAY
200 DISPLAY AT (22,1) BEEP: "DO YOU WISH TO PLAY AGAIN?"
210 ACCEPT AT (23,14) SIZE (1) BEEP VALIDATE ("YN") : A$
220 IF A$="N" THEN GOSUB 2940 :: REM CALL TERMINATE
230 GOTO 180 :: REM GOTO GAMELOOP
240 REM SUBROUTINE ***INTRO***
250 DISPLAY AT (12,10): "BLACK JACK"
260 DISPLAY AT (13,1): "presented by Sean Wohlgemuth"
270 FOR I=1 TO 500 :: NEXT I :: CALL CLEAR
280 DISPLAY AT (1, 1): "PREFACE:"
290 DISPLAY AT (3, 1): "THIS IS A GAME BETWEEN YOU"
300 DISPLAY AT (4, 1): "AND THE DEALER (COMPUTER)."
310 DISPLAY AT (6, 1): "OBJECT OF THE GAME:"
320 DISPLAY AT (8, 1): "THE OBJECT OF THE GAME IS TO"
330 DISPLAY AT (9, 1): "COLLECT A SET OF CARDS LESS"
340 DISPLAY AT (10, 1): "THAN OR EQUAL TO 21. IF YOUR"
350 DISPLAY AT (11, 1): "SET OF CARDS IS HIGHER THAN"
360 DISPLAY AT (12, 1): "THE DEALER'S AND NOT OVER 21"
370 DISPLAY AT (13, 1): "THEN YOU WIN."
380 DISPLAY AT (15, 1): "CARD VALUES:"
390 DISPLAY AT (17, 1): "NUMBER CARDS = FACE VALUE"
400 DISPLAY AT (18, 1): "FACE CARDS   = 10 PTS"
410 DISPLAY AT (19, 1): "ACES         = 11 OR 1 PT(S)"
420 DISPLAY AT (24,1): "PRESS ENTER TO CONTINUE:"
430 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
440 CALL CLEAR
450 DISPLAY AT (1, 1): "HOW TO PLAY:"
460 DISPLAY AT (3, 1): "BOTH YOU AND THE DEALER WILL"
470 DISPLAY AT (4, 1): "BE GIVEN TWO CARDS TO START."
480 DISPLAY AT (5, 1): "TAKE AS MANY CARDS AS YOU"
490 DISPLAY AT (6, 1): "WISH (UP TO 5 CARDS). IF"
500 DISPLAY AT (7, 1): "THE VALUE OF YOUR HAND"
510 DISPLAY AT (8, 1): "EXCEEDS 21 YOU BUST. ONCE"
520 DISPLAY AT (9, 1): "YOU STOP TAKING CARDS, THE"
530 DISPLAY AT (10, 1): "DEALER WILL TAKE CARDS"
540 DISPLAY AT (11, 1): "TRYING NOT TO BUST AS WELL."
550 DISPLAY AT (12, 1): "THE PLAYER WITH THE HIGHEST"
560 DISPLAY AT (13, 1): "HAND VALUE WINS."
570 DISPLAY AT (24,1): "PRESS ENTER TO BEGIN:"
580 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
590 RETURN
600 REM SUBROUTINE ***SETCHARS***
610 OFFSET=USRDEFCHARSET*8+24
620 GOSUB 700 :: REM CALL SETCHARSREDSUIT
630 OFFSET=(USRDEFCHARSET+2)*8+24
640 GOSUB 780 :: REM CALL SETCHARSBLACKSUIT
650 OFFSET=(USRDEFCHARSET+4)*8+24
660 GOSUB 1000 :: REM CALL SETCHARSEDGES
670 OFFSET=(USRDEFCHARSET+5)*8+24
680 CALL CHAR (OFFSET, "FFFFFFFFFFFFFFFF") :: CARDFRONT=OFFSET
690 RETURN
700 REM SUBROUTINE ***SETCHARSREDSUIT***
710 REM HEART, DIAMOND
720 CALL CHAR (OFFSET, "00367F7F7F3E1C08" ) :: HEART=OFFSET :: OFFSET=OFFSET+1
730 CALL CHAR (OFFSET, "00081C3E7F3E1C08" ) :: DIAMOND=OFFSET :: OFFSET=OFFSET+1 :: REDVALS=OFFSET
740 GOSUB 840 :: REM CALL SETCHARSVALS
750 REM CARDBACK
760 CALL CHAR (OFFSET, "AA55AA55AA55AA55"):: CARDBACK=OFFSET :: OFFSET=OFFSET+1
770 RETURN
780 REM SUBROUTINE ***SETCHARSBLACKSUIT***
790 REM SPADE, CLUB
800 CALL CHAR (OFFSET, "0010387CFEFED638" ) :: SPADE=OFFSET :: OFFSET=OFFSET+1
810 CALL CHAR (OFFSET, "00103854EE541038" ) :: CLUB=OFFSET :: OFFSET=OFFSET+1 :: BLACKVALS=OFFSET
820 GOSUB 840 :: REM CALL SETCHARSVALS
830 RETURN
840 REM SUBROUTINE ***SETCHARSVALS***
850 REM 2-10, JACK, QUEEN, KING, ACE
860 CALL CHAR (OFFSET, "003844040830407C") :: OFFSET=OFFSET+1
870 CALL CHAR (OFFSET, "0038040418040438") :: OFFSET=OFFSET+1
880 CALL CHAR (OFFSET, "002424243C040404") :: OFFSET=OFFSET+1
890 CALL CHAR (OFFSET, "003C20203C04043C") :: OFFSET=OFFSET+1
900 CALL CHAR (OFFSET, "001C202038242418") :: OFFSET=OFFSET+1
910 CALL CHAR (OFFSET, "003C040408081010") :: OFFSET=OFFSET+1
920 CALL CHAR (OFFSET, "003C24243C24243C") :: OFFSET=OFFSET+1
930 CALL CHAR (OFFSET, "001C24241C040404") :: OFFSET=OFFSET+1
940 CALL CHAR (OFFSET, "004C52525252524C") :: OFFSET=OFFSET+1
950 CALL CHAR (OFFSET, "003E080808084830") :: OFFSET=OFFSET+1
960 CALL CHAR (OFFSET, "0018242424241C02") :: OFFSET=OFFSET+1
970 CALL CHAR (OFFSET, "0024242830282424") :: OFFSET=OFFSET+1
980 CALL CHAR (OFFSET, "001824243C242424") :: OFFSET=OFFSET+1
990 RETURN
1000 REM SUBROUTINE ***SETCHARSEDGES***
1010 REM CARDEDGE, CARDTOP
1020 CALL CHAR (OFFSET, "8080808080808080") :: CARDEDGE=OFFSET :: OFFSET=OFFSET+1
1030 CALL CHAR (OFFSET, "00000000000000FF") :: CARDTOP=OFFSET :: OFFSET=OFFSET+1
1040 RETURN
1050 REM SUBROUTINE ***SETCOLORSCHEME***
1060 CALL COLOR (USRDEFCHARSET,7,16) :: REM DARK RED ON BLACK
1070 CALL COLOR (USRDEFCHARSET+1,7,16) :: REM DARK RED ON BLACK
1080 CALL COLOR (USRDEFCHARSET+2,2,16) :: REM BLACK ON WHITE
1090 CALL COLOR (USRDEFCHARSET+3,2,16) :: REM BLACK ON WHITE
1100 CALL COLOR (USRDEFCHARSET+4,2,1) :: REM BLACK ON TRANSPARENT
1110 CALL COLOR (USRDEFCHARSET+5,16,16) :: REM WHITE ON WHITE
1120 CALL SCREEN (3) :: REM MED GREEN BACKGROUND
1130 RETURN
1140 REM SUBROUTINE ***RENDERCARD***
1150 IF FACEDOWN=1 THEN FILL=CARDBACK ELSE FILL=CARDFRONT
1160 COL=CARD*6 :: ROW=PLAYER*10
1170 REM RENDER SURFACE
1180 FOR I=1 TO 5
1190 CALL VCHAR (4+ROW, 1+COL+I, FILL, 7)
1200 NEXT I
1210 CALL HCHAR (3+ROW, 2+COL, CARDTOP, 5)
1220 CALL VCHAR (4+ROW, 7+COL, CARDEDGE, 7)
1230 IF FACEDOWN=1 THEN RETURN
1240 REM SELECT SUIT
1250 IF HANDS(PLAYER,CARD,1)=1 THEN SUIT=HEART
1260 IF HANDS(PLAYER,CARD,1)=2 THEN SUIT=DIAMOND
1270 IF HANDS(PLAYER,CARD,1)=3 THEN SUIT=SPADE
1280 IF HANDS(PLAYER,CARD,1)=4 THEN SUIT=CLUB
1290 REM RENDER CARD VALUE
1300 IF HANDS(PLAYER,CARD,1)<3 THEN CARDCHAR=REDVALS
1310 IF HANDS(PLAYER,CARD,1)>2 THEN CARDCHAR=BLACKVALS
1320 CARDVAL=HANDS(PLAYER,CARD,0)
1330 CARDCHAR=CARDCHAR+CARDVAL-2
1340 CALL HCHAR (4+ROW, 2+COL, CARDCHAR, 1)
1350 CALL HCHAR (10+ROW, 6+COL, CARDCHAR, 1)
1360 REM RENDER CENTERPIECE
1370 ON CARDVAL-1 GOSUB 1390,1430,1460,1520,1560,1600,1640,1740,1810,1850,1900,1960,2020 :: REM CALL RENDERX
1380 RETURN
1390 REM SUBROUTINE ***RENDER2***
1400 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1410 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1420 RETURN
1430 REM SUBROUTINE ***RENDER3***
1440 CALL VCHAR (6+ROW,4+COL,SUIT,3)
1450 RETURN
1460 REM SUBROUTINE ***RENDER4***
1470 CALL HCHAR (6+ROW,3+COL,SUIT,1)
1480 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1490 CALL HCHAR (6+ROW,5+COL,SUIT,1)
1500 CALL HCHAR (8+ROW,5+COL,SUIT,1)
1510 RETURN
1520 REM SUBROUTINE ***RENDER5***
1530 GOSUB 1460 :: REM CALL RENDER4
1540 CALL HCHAR (7+ROW,4+COL,SUIT,1)
1550 RETURN
1560 REM SUBROUTINE ***RENDER6***
1570 CALL vCHAR (6+ROW,3+COL,SUIT,3)
1580 CALL vCHAR (6+ROW,5+COL,SUIT,3)
1590 RETURN
1600 REM SUBROUTINE ***RENDER7***
1610 GOSUB 1560 :: REM CALL RENDER6
1620 CALL VCHAR (7+ROW,4+COL,SUIT,1) 
1630 RETURN
1640 REM SUBROUTINE ***RENDER8***
1650 CALL HCHAR (5+ROW,3+COL,SUIT,1)
1660 CALL HCHAR (5+ROW,5+COL,SUIT,1)
1670 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1680 CALL HCHAR (7+ROW,3+COL,SUIT,1)
1690 CALL HCHAR (7+ROW,5+COL,SUIT,1)
1700 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1710 CALL HCHAR (9+ROW,3+COL,SUIT,1)
1720 CALL HCHAR (9+ROW,5+COL,SUIT,1)
1730 RETURN
1740 REM SUBROUTINE ***RENDER9***
1750 CALL VCHAR (5+ROW,3+COL,SUIT,2)
1760 CALL VCHAR (5+ROW,5+COL,SUIT,2)
1770 CALL VCHAR (8+ROW,3+COL,SUIT,2)
1780 CALL VCHAR (8+ROW,5+COL,SUIT,2)
1790 CALL HCHAR (7+ROW,4+COL,SUIT,1)
1800 RETURN
1810 REM SUBROUTINE ***RENDER10***
1820 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1830 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1840 RETURN
1850 REM SUBROUTINE ***RENDERJACK***
1860 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1870 CALL HCHAR (9+ROW,3+COL,SUIT,2)
1880 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1890 RETURN
1900 REM SUBROUTINE ***RENDERQUEEN***
1910 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1920 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1930 CALL HCHAR (5+ROW,4+COL,SUIT,1)
1940 CALL HCHAR (9+ROW,4+COL,SUIT,1)
1950 RETURN
1960 REM SUBROUTINE ***RENDERKING***
1970 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1980 CALL VCHAR (5+ROW,5+COL,SUIT,2)
1990 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2000 CALL VCHAR (7+ROW,4+COL,SUIT,1)
2010 RETURN
2020 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2030 RETURN
2040 REM SUBROUTINE ***DRAWHANDS***
2050 HANDS(0,0,0)=10 :: HANDS(0,1,0)=14 :: HANDS(0,2,0)=11 :: HANDS(0,3,0)=2 :: HANDS(0,4,0)=3
2060 HANDS(0,0,1)=1 :: HANDS(0,1,1)=2 :: HANDS(0,2,1)=3 :: HANDS(0,3,1)=4 :: HANDS(0,4,1)=1
2070 HANDS(1,0,0)=5 :: HANDS(1,1,0)=11 :: HANDS(1,2,0)=3 :: HANDS(1,3,0)=13 :: HANDS(1,4,0)=14
2080 HANDS(1,0,1)=1 :: HANDS(1,1,1)=2 :: HANDS(1,2,1)=3 :: HANDS(1,3,1)=4 :: HANDS(1,4,1)=1
2090 REM RETURN
2100 RANDOMIZE
2110 FOR I=0 TO 4
2120 HANDS(0,I,0)=INT(RND*13)+2
2130 HANDS(1,I,0)=INT(RND*13)+2
2140 HANDS(0,I,1)=INT(RND*4)+1
2150 HANDS(1,I,1)=INT(RND*4)+1
2160 NEXT I
2170 RETURN
2180 REM SUBROUTINE ***CLEARTABLE***
2190 CALL CLEAR
2200 DISPLAY AT (1, 9): "BLACK JACK"
2210 SUMS(0)=0 :: SUMS(1)=0 :: CARDS(0)=0 ::CARDS(1)=0
2220 RETURN
2230 REM SUBROUTINE ***DEAL***
2240 CARDS(PLAYER)=CARDS(PLAYER)+1
2250 CARD=CARDS(PLAYER)-1
2260 CVAL=HANDS(PLAYER,CARD,0)
2270 IF CVAL<10 THEN 2290
2280 IF CVAL<14 THEN CVAL=10 ELSE CVAL=1
2290 SUMS(PLAYER)=SUMS(PLAYER)+CVAL
2300 GOSUB 1140 :: REM CALL RENDERCARD
2310 RETURN
2320 REM SUBROUTINE ***PLAY***
2330 GOSUB 2180 :: REM CALL CLEARTABLE
2340 GOSUB 2040 :: REM CALL DRAWHANDS
2350 PLAYER=0 :: FACEDOWN=0
2360 GOSUB 2230 :: REM CALL DEAL
2370 GOSUB 2230 :: REM CALL DEAL
2380 PLAYER=1 :: FACEDOWN=0
2390 GOSUB 2230 :: REM CALL DEAL
2400 PLAYER=1 :: FACEDOWN=1
2410 GOSUB 2230 :: REM CALL DEAL
2420 REM ***PLAYER***
2430 BUSTED=0
2440 FOR H=3 TO 5
2450 DISPLAY AT (22,1): "DO YOU WANT ANOTHER CARD?"
2460 ACCEPT AT (23,14) VALIDATE ("YN") BEEP SIZE (1) : A$
2470 DISPLAY AT (22,1): "                         " :: DISPLAY AT (23,14): " "
2480 IF A$="N" THEN 2550 :: REM GOTO REVEAL
2490 PLAYER=0 :: FACEDOWN=0
2500 GOSUB 2230 :: REM CALL DEAL
2510 IF SUMS(0)>21 THEN BUSTED=1
2520 IF BUSTED=1 THEN DISPLAY AT (12,3) BEEP: "YOU BUSTED. YOU LOSE."
2530 IF BUSTED=1 THEN 2550 :: REM GOTO REVEAL
2540 NEXT H
2550 REM ***REVEAL***
2560 PLAYER=1 :: FACEDOWN=0 :: CARD=1
2570 GOSUB 1140 :: REM CALL RENDERCARD
2580 IF BUSTED=1 THEN 2720 :: REM GOTO GAMEOVER
2590 REM ***DEALER***
2600 BUSTED=0
2610 FOR H=3 TO 5
2620 PLAYER=1 :: FACEDOWN=0
2630 GOSUB 2870 :: REM CALL CALCSCORE
2640 IF SCORE>=17 THEN 2700 :: REM GOTO TALLY
2650 GOSUB 2230 :: REM CALL DEAL
2660 IF SUMS(1)>21 THEN BUSTED=1
2670 IF BUSTED=1 THEN DISPLAY AT (12,2) BEEP: "DEALER BUSTED. YOU WIN."
2680 IF BUSTED=1 THEN 2720 :: REM GOTO GAMEOVER
2690 NEXT H
2700 REM ***TALLY***
2710 GOSUB 2740 :: REM CALL WHOWON
2720 REM ***GAMEOVER***
2730 RETURN
2740 REM SUBROUTINE ***WHOWON***
2750 PLAYER=0
2760 GOSUB 2870 :: REM CALL CALCSCORE
2770 PSCORE=SCORE
2780 DISPLAY AT (1,24): "P:"&STR$(PSCORE)
2790 PLAYER=1
2800 GOSUB 2870 :: REM CALL CALCSCORE
2810 DSCORE=SCORE
2820 DISPLAY AT (2,24):"D:"&STR$(DSCORE)
2830 IF PSCORE=SCORE THEN DISPLAY AT (12,10): "TIE GAME"
2840 IF PSCORE>SCORE THEN DISPLAY AT (12,10): "YOU WIN!"
2850 IF PSCORE<SCORE THEN DISPLAY AT (12,10): "YOU LOSE"
2860 RETURN
2870 REM SUBROUTINE ***CALCSCORE***
2880 SCORE=SUMS(PLAYER)
2890 IF SCORE+10>21 THEN RETURN
2900 FOR I=0 TO CARDS(PLAYER)-1
2910 IF HANDS(PLAYER,I,0)=14 THEN SCORE=SCORE+10 :: RETURN
2920 NEXT I
2930 RETURN
2940 REM SUBROUTINE ***TERMINATE***
2950 CALL CLEAR :: FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT (12,7) BEEP: "GAME TERMINATED" :: FOR J=1 TO 100 :: NEXT J :: NEXT I
2960 FOR I=1 TO 1000 :: NEXT I 
2970 END
2980 RETURN