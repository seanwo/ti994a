100 CALL CLEAR
110 GOSUB 240 :: REM CALL INTRO
120 DIM HANDS(1,4,1) :: REM HANDS[PLAYER,CARD,SUIT]
130 DIM CARDS(1) :: REM CARDS[PLAYER]
140 DIM SUMS(1) :: REM SUMS[PLAYER]
150 USRDEFCHARSET=9
160 GOSUB 600 :: REM CALL SETCHARS
170 GOSUB 1050 :: REM CALL SETCOLORSCHEME
180 REM LABEL ***GAMELOOP***
190 GOSUB 2470 :: REM CALL PLAY
200 DISPLAY AT (22,2) BEEP: "DO YOU WISH TO PLAY AGAIN?"
210 ACCEPT AT (23,14) SIZE (1) BEEP VALIDATE ("YN") : A$
220 IF A$="N" THEN GOSUB 3090 :: REM CALL TERMINATE
230 GOTO 180 :: REM GOTO GAMELOOP
240 REM SUBROUTINE ***INTRO***
250 DISPLAY AT (12,10): "BLACKJACK"
260 DISPLAY AT (13,1): "presented by Sean Wohlgemuth"
270 FOR I=1 TO 500 :: NEXT I :: CALL CLEAR
280 DISPLAY AT (1, 1): "PREFACE:"
290 DISPLAY AT (3, 1): "THIS IS A GAME BETWEEN YOU"
300 DISPLAY AT (4, 1): "AND THE DEALER (COMPUTER)."
310 DISPLAY AT (6, 1): "OBJECT OF THE GAME:"
320 DISPLAY AT (8, 1): "THE OBJECT OF THE GAME IS TO"
330 DISPLAY AT (9, 1): "COLLECT A SET OF CARDS LESS"
340 DISPLAY AT (10, 1): "THAN OR EQUAL TO 21. IF YOUR"
350 DISPLAY AT (11, 1): "SET OF CARDS IS HIGHER THAN"
360 DISPLAY AT (12, 1): "THE DEALER'S AND NOT OVER 21"
370 DISPLAY AT (13, 1): "THEN YOU WIN."
380 DISPLAY AT (15, 1): "CARD VALUES:"
390 DISPLAY AT (17, 1): "NUMBER CARDS = FACE VALUE"
400 DISPLAY AT (18, 1): "FACE CARDS   = 10 PTS"
410 DISPLAY AT (19, 1): "ACES         = 11 OR 1 PT(S)"
420 DISPLAY AT (24,1): "PRESS ENTER TO CONTINUE:"
430 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
440 CALL CLEAR
450 DISPLAY AT (1, 1): "HOW TO PLAY:"
460 DISPLAY AT (3, 1): "BOTH YOU AND THE DEALER WILL"
470 DISPLAY AT (4, 1): "BE GIVEN TWO CARDS TO START."
480 DISPLAY AT (5, 1): "TAKE AS MANY CARDS AS YOU"
490 DISPLAY AT (6, 1): "WISH (UP TO 5 CARDS). IF"
500 DISPLAY AT (7, 1): "THE VALUE OF YOUR HAND"
510 DISPLAY AT (8, 1): "EXCEEDS 21 YOU BUST. ONCE"
520 DISPLAY AT (9, 1): "YOU STOP TAKING CARDS, THE"
530 DISPLAY AT (10, 1): "DEALER WILL TAKE CARDS"
540 DISPLAY AT (11, 1): "TRYING NOT TO BUST AS WELL."
550 DISPLAY AT (12, 1): "THE PLAYER WITH THE HIGHEST"
560 DISPLAY AT (13, 1): "HAND VALUE WINS."
570 DISPLAY AT (24,1): "PRESS ENTER TO BEGIN:"
580 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
590 RETURN
600 REM SUBROUTINE ***SETCHARS***
610 OFFSET=USRDEFCHARSET*8+24
620 GOSUB 700 :: REM CALL SETCHARSREDSUIT
630 OFFSET=(USRDEFCHARSET+2)*8+24
640 GOSUB 780 :: REM CALL SETCHARSBLACKSUIT
650 OFFSET=(USRDEFCHARSET+4)*8+24
660 GOSUB 1000 :: REM CALL SETCHARSEDGES
670 OFFSET=(USRDEFCHARSET+5)*8+24
680 CALL CHAR (OFFSET, "FFFFFFFFFFFFFFFF") :: CARDFRONT=OFFSET
690 RETURN
700 REM SUBROUTINE ***SETCHARSREDSUIT***
710 REM HEART, DIAMOND
720 CALL CHAR (OFFSET, "00367F7F7F3E1C08" ) :: HEART=OFFSET :: OFFSET=OFFSET+1
730 CALL CHAR (OFFSET, "00081C3E7F3E1C08" ) :: DIAMOND=OFFSET :: OFFSET=OFFSET+1 :: REDVALS=OFFSET
740 GOSUB 840 :: REM CALL SETCHARSVALS
750 REM CARDBACK
760 CALL CHAR (OFFSET, "AA55AA55AA55AA55"):: CARDBACK=OFFSET :: OFFSET=OFFSET+1
770 RETURN
780 REM SUBROUTINE ***SETCHARSBLACKSUIT***
790 REM SPADE, CLUB
800 CALL CHAR (OFFSET, "0010387CFEFED638" ) :: SPADE=OFFSET :: OFFSET=OFFSET+1
810 CALL CHAR (OFFSET, "00103854EE541038" ) :: CLUB=OFFSET :: OFFSET=OFFSET+1 :: BLACKVALS=OFFSET
820 GOSUB 840 :: REM CALL SETCHARSVALS
830 RETURN
840 REM SUBROUTINE ***SETCHARSVALS***
850 REM 2-10, JACK, QUEEN, KING, ACE
860 CALL CHAR (OFFSET, "003844040830407C") :: OFFSET=OFFSET+1
870 CALL CHAR (OFFSET, "0038040418040438") :: OFFSET=OFFSET+1
880 CALL CHAR (OFFSET, "002424243C040404") :: OFFSET=OFFSET+1
890 CALL CHAR (OFFSET, "003C20203C04043C") :: OFFSET=OFFSET+1
900 CALL CHAR (OFFSET, "001C202038242418") :: OFFSET=OFFSET+1
910 CALL CHAR (OFFSET, "003C040408081010") :: OFFSET=OFFSET+1
920 CALL CHAR (OFFSET, "003C24243C24243C") :: OFFSET=OFFSET+1
930 CALL CHAR (OFFSET, "001C24241C040404") :: OFFSET=OFFSET+1
940 CALL CHAR (OFFSET, "004C52525252524C") :: OFFSET=OFFSET+1
950 CALL CHAR (OFFSET, "003E080808084830") :: OFFSET=OFFSET+1
960 CALL CHAR (OFFSET, "0018242424241C02") :: OFFSET=OFFSET+1
970 CALL CHAR (OFFSET, "0024242830282424") :: OFFSET=OFFSET+1
980 CALL CHAR (OFFSET, "001824243C242424") :: OFFSET=OFFSET+1
990 RETURN
1000 REM SUBROUTINE ***SETCHARSEDGES***
1010 REM CARDEDGE, CARDTOP
1020 CALL CHAR (OFFSET, "8080808080808080") :: CARDEDGE=OFFSET :: OFFSET=OFFSET+1
1030 CALL CHAR (OFFSET, "00000000000000FF") :: CARDTOP=OFFSET :: OFFSET=OFFSET+1
1040 RETURN
1050 REM SUBROUTINE ***SETCOLORSCHEME***
1060 CALL COLOR (USRDEFCHARSET,7,16) :: REM DARK RED ON BLACK
1070 CALL COLOR (USRDEFCHARSET+1,7,16) :: REM DARK RED ON BLACK
1080 CALL COLOR (USRDEFCHARSET+2,2,16) :: REM BLACK ON WHITE
1090 CALL COLOR (USRDEFCHARSET+3,2,16) :: REM BLACK ON WHITE
1100 CALL COLOR (USRDEFCHARSET+4,2,1) :: REM BLACK ON TRANSPARENT
1110 CALL COLOR (USRDEFCHARSET+5,16,16) :: REM WHITE ON WHITE
1120 CALL SCREEN (3) :: REM MED GREEN BACKGROUND
1130 RETURN
1140 REM SUBROUTINE ***RENDERCARD***
1150 IF FACEDOWN=1 THEN FILL=CARDBACK ELSE FILL=CARDFRONT
1160 COL=CARD*6 :: ROW=PLAYER*10
1170 REM RENDER SURFACE
1180 FOR I=1 TO 5
1190 CALL VCHAR (4+ROW, 1+COL+I, FILL, 7)
1200 NEXT I
1210 CALL HCHAR (3+ROW, 2+COL, CARDTOP, 5)
1220 CALL VCHAR (4+ROW, 7+COL, CARDEDGE, 7)
1230 IF FACEDOWN=1 THEN RETURN
1240 REM SELECT SUIT
1250 IF HANDS(PLAYER,CARD,1)=1 THEN SUIT=HEART
1260 IF HANDS(PLAYER,CARD,1)=2 THEN SUIT=DIAMOND
1270 IF HANDS(PLAYER,CARD,1)=3 THEN SUIT=SPADE
1280 IF HANDS(PLAYER,CARD,1)=4 THEN SUIT=CLUB
1290 REM RENDER CARD VALUE
1300 IF HANDS(PLAYER,CARD,1)<3 THEN CARDCHAR=REDVALS
1310 IF HANDS(PLAYER,CARD,1)>2 THEN CARDCHAR=BLACKVALS
1320 CARDVAL=HANDS(PLAYER,CARD,0)
1330 CARDCHAR=CARDCHAR+CARDVAL-2
1340 CALL HCHAR (4+ROW, 2+COL, CARDCHAR, 1)
1350 CALL HCHAR (10+ROW, 6+COL, CARDCHAR, 1)
1360 REM RENDER CARD FACE
1370 ON CARDVAL-1 GOSUB 1390,1430,1460,1520,1560,1600,1640,1740,1810,1850,1900,1960,2020 :: REM CALL RENDERX
1380 RETURN
1390 REM SUBROUTINE ***RENDER2***
1400 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1410 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1420 RETURN
1430 REM SUBROUTINE ***RENDER3***
1440 CALL VCHAR (6+ROW,4+COL,SUIT,3)
1450 RETURN
1460 REM SUBROUTINE ***RENDER4***
1470 CALL HCHAR (6+ROW,3+COL,SUIT,1)
1480 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1490 CALL HCHAR (6+ROW,5+COL,SUIT,1)
1500 CALL HCHAR (8+ROW,5+COL,SUIT,1)
1510 RETURN
1520 REM SUBROUTINE ***RENDER5***
1530 GOSUB 1460 :: REM CALL RENDER4
1540 CALL HCHAR (7+ROW,4+COL,SUIT,1)
1550 RETURN
1560 REM SUBROUTINE ***RENDER6***
1570 CALL vCHAR (6+ROW,3+COL,SUIT,3)
1580 CALL vCHAR (6+ROW,5+COL,SUIT,3)
1590 RETURN
1600 REM SUBROUTINE ***RENDER7***
1610 GOSUB 1560 :: REM CALL RENDER6
1620 CALL VCHAR (7+ROW,4+COL,SUIT,1) 
1630 RETURN
1640 REM SUBROUTINE ***RENDER8***
1650 CALL HCHAR (5+ROW,3+COL,SUIT,1)
1660 CALL HCHAR (5+ROW,5+COL,SUIT,1)
1670 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1680 CALL HCHAR (7+ROW,3+COL,SUIT,1)
1690 CALL HCHAR (7+ROW,5+COL,SUIT,1)
1700 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1710 CALL HCHAR (9+ROW,3+COL,SUIT,1)
1720 CALL HCHAR (9+ROW,5+COL,SUIT,1)
1730 RETURN
1740 REM SUBROUTINE ***RENDER9***
1750 CALL VCHAR (5+ROW,3+COL,SUIT,2)
1760 CALL VCHAR (5+ROW,5+COL,SUIT,2)
1770 CALL VCHAR (8+ROW,3+COL,SUIT,2)
1780 CALL VCHAR (8+ROW,5+COL,SUIT,2)
1790 CALL HCHAR (7+ROW,4+COL,SUIT,1)
1800 RETURN
1810 REM SUBROUTINE ***RENDER10***
1820 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1830 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1840 RETURN
1850 REM SUBROUTINE ***RENDERJACK***
1860 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1870 CALL HCHAR (9+ROW,3+COL,SUIT,2)
1880 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1890 RETURN
1900 REM SUBROUTINE ***RENDERQUEEN***
1910 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1920 CALL VCHAR (5+ROW,5+COL,SUIT,5)
1930 CALL HCHAR (5+ROW,4+COL,SUIT,1)
1940 CALL HCHAR (9+ROW,4+COL,SUIT,1)
1950 RETURN
1960 REM SUBROUTINE ***RENDERKING***
1970 CALL VCHAR (5+ROW,3+COL,SUIT,5)
1980 CALL VCHAR (5+ROW,5+COL,SUIT,2)
1990 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2000 CALL VCHAR (7+ROW,4+COL,SUIT,1)
2010 RETURN
2020 REM SUBROUTINE ***RENDERACE***
2030 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2040 RETURN
2050 REM SUBROUTINE ***SHUFFLE***
2060 RANDOMIZE
2070 DIM DECK(9)
2080 FOR H=0 TO 9
2090 REM LABEL ***DRAWLOOP***
2100 DISPLAY AT(12,6): "SHUFFLING THE DECK"
2110 X=INT(RND*4)+1
2120 X=X*16
2130 X=X+INT(RND*13)+2
2140 GOSUB 2280 :: REM CALL CHECKDUP
2150 IF DUP=1 THEN 2090 :: REM GOTO DRAWLOOP
2160 DECK(H)=X
2170 NEXT H
2180 FOR H=0 TO 4
2190 HANDS(0,H,1)=INT(DECK(H)/16)
2200 HANDS(0,H,0)=DECK(H)-HANDS(0,H,1)*16
2210 NEXT H
2220 FOR H=0 TO 4
2230 HANDS(1,H,1)=INT(DECK(H+5)/16)
2240 HANDS(1,H,0)=DECK(H+5)-HANDS(1,H,1)*16
2250 NEXT H
2260 DISPLAY AT(12,6): "                  "
2270 RETURN
2280 REM SUBROUTINE ***CHECKDUP***
2290 DUP=0
2300 FOR I=0 TO 9
2310 IF DECK(I)=X THEN DUP=1 :: RETURN
2320 NEXT I
2330 RETURN
2340 REM SUBROUTINE ***CLEARTABLE***
2350 CALL CLEAR
2360 DISPLAY AT (1, 10): "BLACKJACK"
2370 SUMS(0)=0 :: SUMS(1)=0 :: CARDS(0)=0 ::CARDS(1)=0
2380 RETURN
2390 REM SUBROUTINE ***DEAL***
2400 CARDS(PLAYER)=CARDS(PLAYER)+1
2410 CARD=CARDS(PLAYER)-1
2420 CVAL=HANDS(PLAYER,CARD,0)
2430 IF CVAL>10 THEN IF CVAL=14 THEN CVAL=1 ELSE CVAL=10
2440 SUMS(PLAYER)=SUMS(PLAYER)+CVAL
2450 GOSUB 1140 :: REM CALL RENDERCARD
2460 RETURN
2470 REM SUBROUTINE ***PLAY***
2480 GOSUB 2340 :: REM CALL CLEARTABLE
2490 GOSUB 2050 :: REM CALL SHUFFLE
2500 PLAYER=0 :: FACEDOWN=0
2510 GOSUB 2390 :: REM CALL DEAL
2520 GOSUB 2390 :: REM CALL DEAL
2530 PLAYER=1 :: FACEDOWN=0
2540 GOSUB 2390 :: REM CALL DEAL
2550 PLAYER=1 :: FACEDOWN=1
2560 GOSUB 2390 :: REM CALL DEAL
2570 REM PLAYERS TURNS
2580 BUSTED=0
2590 FOR J=3 TO 5
2600 DISPLAY AT (22,2): "DO YOU WANT ANOTHER CARD?"
2610 ACCEPT AT (23,14) VALIDATE ("YN") BEEP SIZE (1) : A$
2620 DISPLAY AT (22,2): "                         " :: DISPLAY AT (23,14): " "
2630 IF A$="N" THEN 2700 :: REM GOTO REVEAL
2640 PLAYER=0 :: FACEDOWN=0
2650 GOSUB 2390 :: REM CALL DEAL
2660 IF SUMS(0)>21 THEN BUSTED=1
2670 IF BUSTED=1 THEN DISPLAY AT (12,4) BEEP: "YOU BUSTED. YOU LOSE."
2680 IF BUSTED=1 THEN 2700 :: REM GOTO REVEAL
2690 NEXT J
2700 REM LABEL ***REVEAL***
2710 PLAYER=1 :: FACEDOWN=0 :: CARD=1
2720 GOSUB 1140 :: REM CALL RENDERCARD
2730 IF BUSTED=1 THEN 2870 :: REM GOTO GAMEOVER
2740 REM DEALERS TURNS
2750 BUSTED=0
2760 FOR J=3 TO 5
2770 PLAYER=1 :: FACEDOWN=0
2780 GOSUB 3020 :: REM CALL CALCSCORE
2790 IF SCORE>=17 THEN 2850 :: REM GOTO TALLY
2800 GOSUB 2390 :: REM CALL DEAL
2810 IF SUMS(1)>21 THEN BUSTED=1
2820 IF BUSTED=1 THEN DISPLAY AT (12,3) BEEP: "DEALER BUSTED. YOU WIN."
2830 IF BUSTED=1 THEN 2870 :: REM GOTO GAMEOVER
2840 NEXT J
2850 REM LABEL ***TALLY***
2860 GOSUB 2890 :: REM CALL WHOWON
2870 REM LABEL ***GAMEOVER***
2880 RETURN
2890 REM SUBROUTINE ***WHOWON***
2900 PLAYER=0
2910 GOSUB 3020 :: REM CALL CALCSCORE
2920 PSCORE=SCORE
2930 REM DISPLAY AT (1,24): "P:"&STR$(PSCORE)
2940 PLAYER=1
2950 GOSUB 3020 :: REM CALL CALCSCORE
2960 DSCORE=SCORE
2970 REM DISPLAY AT (2,24):"D:"&STR$(DSCORE)
2980 IF PSCORE=SCORE THEN DISPLAY AT (12,11): "TIE GAME"
2990 IF PSCORE>SCORE THEN DISPLAY AT (12,11): "YOU WIN!"
3000 IF PSCORE<SCORE THEN DISPLAY AT (12,11): "YOU LOSE"
3010 RETURN
3020 REM SUBROUTINE ***CALCSCORE***
3030 SCORE=SUMS(PLAYER)
3040 IF SCORE+10>21 THEN RETURN
3050 FOR I=0 TO CARDS(PLAYER)-1
3060 IF HANDS(PLAYER,I,0)=14 THEN SCORE=SCORE+10 :: RETURN
3070 NEXT I
3080 RETURN
3090 REM SUBROUTINE ***TERMINATE***
3100 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT (12,7) BEEP: "GAME TERMINATED" :: FOR J=1 TO 100 :: NEXT J :: NEXT I
3110 FOR I=1 TO 1000 :: NEXT I 
3120 END
3130 RETURN