100 REM COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
110 REM ::
120 REM THIS PROGRAM IS FREE SOFTWARE: YOU CAN REDISTRIBUTE IT AND/OR MODIFY IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE
130 REM AS PUBLISHED BY THE FREE SOFTWARE FOUNDATION, EITHER VERSION 3 OF THE LICENSE, OR (AT YOUR OPTION) ANY LATER VERSION.
140 REM ::
150 REM THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY
160 REM OF MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU GENERAL PUBLIC LICENSE FOR MORE DETAILS.
170 REM ::
180 REM YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE ALONG WITH THIS PROGRAM. IF NOT, SEE
190 REM <HTTPS://WWW.GNU.ORG/LICENSES/>.
200 REM ::
210 CALL INTRO
220 CALL SETCHARS
230 CALL SETCOLORSCHEME
240 REM LABEL ***GAMELOOP***
250 CALL PLAY
260 DISPLAY AT (22,2) BEEP: "DO YOU WISH TO PLAY AGAIN?"
270 A$=""
280 CALL HCHAR(23,16,30)
290 REM LABEL ***ASKAGAIN***
300 CALL KEY(0,K,S)
310 IF S=0  THEN 300 :: REM GOTO ASKAGAIN
320 IF K<>78 AND K<>89 AND K<>13 THEN 300 :: REM GOTO ASKAGAIN
330 IF K=78 THEN CALL HCHAR (23,16,78) :: A$="N" ELSE IF K=89 THEN CALL HCHAR (23,16,89) :: A$="Y"
340 IF K<>13 THEN 300 :: REM GOTO ASKAGAIN
350 IF A$="" THEN 300 :: REM GOTO ASKAGAIN
360 CALL HCHAR (22,4,32,25) :: CALL HCHAR (23,16,32)
370 IF A$="N" THEN CALL TERMINATE
380 GOTO 250 :: REM GOTO GAMELOOP
390 END
400 REM SUBROUTINE ***INTRO***
410 SUB INTRO
420 CALL CLEAR
430 DISPLAY AT (12,10): "BLACKJACK"
440 DISPLAY AT (13,1): "presented by Sean Wohlgemuth"
450 FOR I=1 TO 5 :: CALL SOUND(1000,110,30) :: CALL SOUND(1,110,30) :: NEXT I
460 CALL CLEAR
470 DISPLAY AT (1, 1): "PREFACE:"
480 DISPLAY AT (3, 1): "THIS IS A GAME BETWEEN YOU"
490 DISPLAY AT (4, 1): "AND THE DEALER (COMPUTER)."
500 DISPLAY AT (6, 1): "OBJECT OF THE GAME:"
510 DISPLAY AT (8, 1): "THE OBJECT OF THE GAME IS TO"
520 DISPLAY AT (9, 1): "COLLECT A SET OF CARDS LESS"
530 DISPLAY AT (10, 1): "THAN OR EQUAL TO 21. IF YOUR"
540 DISPLAY AT (11, 1): "SET OF CARDS IS HIGHER THAN"
550 DISPLAY AT (12, 1): "THE DEALER'S AND NOT OVER 21"
560 DISPLAY AT (13, 1): "THEN YOU WIN."
570 DISPLAY AT (15, 1): "CARD VALUES:"
580 DISPLAY AT (17, 1): "NUMBER CARDS = FACE VALUE"
590 DISPLAY AT (18, 1): "FACE CARDS   = 10 PTS"
600 DISPLAY AT (19, 1): "ACES         = 11 OR 1 PT(S)"
610 DISPLAY AT (24,1): "PRESS ENTER TO CONTINUE:"
620 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
630 CALL CLEAR
640 DISPLAY AT (1, 1): "HOW TO PLAY:"
650 DISPLAY AT (3, 1): "BOTH YOU AND THE DEALER WILL"
660 DISPLAY AT (4, 1): "BE GIVEN TWO CARDS TO START."
670 DISPLAY AT (5, 1): "TAKE AS MANY CARDS AS YOU"
680 DISPLAY AT (6, 1): "WISH (UP TO 5 CARDS). IF"
690 DISPLAY AT (7, 1): "THE VALUE OF YOUR HAND"
700 DISPLAY AT (8, 1): "EXCEEDS 21 YOU BUST. ONCE"
710 DISPLAY AT (9, 1): "YOU STOP TAKING CARDS, THE"
720 DISPLAY AT (10, 1): "DEALER WILL TAKE CARDS"
730 DISPLAY AT (11, 1): "TRYING NOT TO BUST AS WELL."
740 DISPLAY AT (12, 1): "THE PLAYER WITH THE HIGHEST"
750 DISPLAY AT (13, 1): "HAND VALUE WINS."
760 DISPLAY AT (24,1): "PRESS ENTER TO BEGIN:"
770 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
780 SUBEND
790 REM SUBROUTINE ***SETCHARS***
800 SUB SETCHARS
810 CALL SETCHARSRED
820 CALL SETCHARSBLACK
830 CALL SETCHARSEDGES
840 CALL GETCARDFRONT(CARDFRONT)
850 CALL CHAR (CARDFRONT, "FFFFFFFFFFFFFFFF")
860 SUBEND
870 REM SUBROUTINE ***SETCHARSRED***
880 SUB SETCHARSRED
890 CALL GETHEART(HEART)
900 CALL CHAR (HEART, "00367F7F7F3E1C08" )
910 CALL GETDIAMOND(DIAMOND)
920 CALL CHAR (DIAMOND, "00081C3E7F3E1C08" )
930 CALL GETREDVALS(REDVALS)
940 CALL SETCHARSVALS((REDVALS))
950 CALL GETCARDBACK(CARDBACK)
960 CALL CHAR (CARDBACK, "AA55AA55AA55AA55")
970 SUBEND
980 REM SUBROUTINE ***SETCHARSBLACK***
990 SUB SETCHARSBLACK
1000 CALL GETSPADE(SPADE)
1010 CALL CHAR (SPADE, "0010387CFEFED638" )
1020 CALL GETCLUB(CLUB)
1030 CALL CHAR (CLUB, "00103854EE541038" )
1040 CALL GETBLACKVALS(BLACKVALS)
1050 CALL SETCHARSVALS((BLACKVALS))
1060 SUBEND
1070 REM SUBROUTINE ***SETCHARSVALS***
1080 SUB SETCHARSVALS(OFFSET)
1090 REM 2-10, JACK, QUEEN, KING, ACE
1100 CALL CHAR (OFFSET, "003844040830407C") :: OFFSET=OFFSET+1
1110 CALL CHAR (OFFSET, "0038040418040438") :: OFFSET=OFFSET+1
1120 CALL CHAR (OFFSET, "002424243C040404") :: OFFSET=OFFSET+1
1130 CALL CHAR (OFFSET, "003C20203C04043C") :: OFFSET=OFFSET+1
1140 CALL CHAR (OFFSET, "001C202038242418") :: OFFSET=OFFSET+1
1150 CALL CHAR (OFFSET, "003C040408081010") :: OFFSET=OFFSET+1
1160 CALL CHAR (OFFSET, "003C24243C24243C") :: OFFSET=OFFSET+1
1170 CALL CHAR (OFFSET, "001C24241C040404") :: OFFSET=OFFSET+1
1180 CALL CHAR (OFFSET, "004C52525252524C") :: OFFSET=OFFSET+1
1190 CALL CHAR (OFFSET, "003E080808084830") :: OFFSET=OFFSET+1
1200 CALL CHAR (OFFSET, "0018242424241C02") :: OFFSET=OFFSET+1
1210 CALL CHAR (OFFSET, "0024242830282424") :: OFFSET=OFFSET+1
1220 CALL CHAR (OFFSET, "001824243C242424") :: OFFSET=OFFSET+1
1230 SUBEND
1240 REM SUBROUTINE ***SETCHARSEDGES***
1250 SUB SETCHARSEDGES
1260 CALL GETCARDEDGE(CARDEDGE)
1270 CALL CHAR (CARDEDGE, "8080808080808080")
1280 CALL GETCARDTOP(CARDTOP)
1290 CALL CHAR (CARDTOP, "00000000000000FF")
1300 SUBEND
1310 REM SUBROUTINE ***SETCOLORSCHEME***
1320 SUB SETCOLORSCHEME
1330 CALL COLOR (9,7,16) :: REM DARK RED ON BLACK
1340 CALL COLOR (10,7,16) :: REM DARK RED ON BLACK
1350 CALL COLOR (11,2,16) :: REM BLACK ON WHITE
1360 CALL COLOR (12,2,16) :: REM BLACK ON WHITE
1370 CALL COLOR (13,2,1) :: REM BLACK ON TRANSPARENT
1380 CALL COLOR (14,16,16) :: REM WHITE ON WHITE
1390 CALL SCREEN (3) :: REM MED GREEN BACKGROUND
1400 SUBEND
1410 REM SUBROUTINE ***RENDERCARD***
1420 SUB RENDERCARD(PLAYER,CARD,FACEDOWN,HANDS(,,))
1430 IF FACEDOWN=1 THEN CALL GETCARDBACK(FILL) ELSE CALL GETCARDFRONT(FILL)
1440 COL=CARD*6 :: ROW=PLAYER*10
1450 REM RENDER SURFACE
1460 FOR I=1 TO 5
1470 CALL VCHAR (4+ROW, 1+COL+I, FILL, 7)
1480 NEXT I
1490 CALL GETCARDTOP(CARDTOP)
1500 CALL GETCARDEDGE(CARDEDGE)
1510 CALL HCHAR (3+ROW, 2+COL, CARDTOP, 5)
1520 CALL VCHAR (4+ROW, 7+COL, CARDEDGE, 7)
1530 IF FACEDOWN=1 THEN 1800 :: REM SUBEND
1540 REM SELECT SUIT
1550 IF HANDS(PLAYER,CARD,1)=1 THEN CALL GETHEART(SUIT)
1560 IF HANDS(PLAYER,CARD,1)=2 THEN CALL GETDIAMOND(SUIT)
1570 IF HANDS(PLAYER,CARD,1)=3 THEN CALL GETSPADE(SUIT)
1580 IF HANDS(PLAYER,CARD,1)=4 THEN CALL GETCLUB(SUIT)
1590 REM RENDER CARD VALUE
1600 IF HANDS(PLAYER,CARD,1)<3 THEN CALL GETREDVALS(CARDCHAR)
1610 IF HANDS(PLAYER,CARD,1)>2 THEN CALL GETBLACKVALS(CARDCHAR)
1620 CARDVAL=HANDS(PLAYER,CARD,0)
1630 CARDCHAR=CARDCHAR+CARDVAL-2
1640 CALL HCHAR (4+ROW, 2+COL, CARDCHAR, 1)
1650 CALL HCHAR (10+ROW, 6+COL, CARDCHAR, 1)
1660 REM RENDER CARD FACE
1670 IF CARDVAL=2 THEN CALL RENDER2(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1680 IF CARDVAL=3 THEN CALL RENDER3(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1690 IF CARDVAL=4 THEN CALL RENDER4(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1700 IF CARDVAL=5 THEN CALL RENDER5(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1710 IF CARDVAL=6 THEN CALL RENDER6(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1720 IF CARDVAL=7 THEN CALL RENDER7(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1730 IF CARDVAL=8 THEN CALL RENDER8(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1740 IF CARDVAL=9 THEN CALL RENDER9(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1750 IF CARDVAL=10 THEN CALL RENDER10(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1760 IF CARDVAL=11 THEN CALL RENDERJACK(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1770 IF CARDVAL=12 THEN CALL RENDERQUEEN(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1780 IF CARDVAL=13 THEN CALL RENDERKING(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1790 IF CARDVAL=14 THEN CALL RENDERACE(ROW,COL,SUIT) :: GOTO 1800 :: REM SUBEND
1800 SUBEND
1810 REM SUBROUTINE ***RENDER2***
1820 SUB RENDER2(ROW,COL,SUIT)
1830 CALL HCHAR (6+ROW,4+COL,SUIT,1)
1840 CALL HCHAR (8+ROW,4+COL,SUIT,1)
1850 SUBEND
1860 REM SUBROUTINE ***RENDER3***
1870 SUB RENDER3(ROW,COL,SUIT)
1880 CALL VCHAR (6+ROW,4+COL,SUIT,3)
1890 SUBEND
1900 REM SUBROUTINE ***RENDER4***
1910 SUB RENDER4(ROW,COL,SUIT)
1920 CALL HCHAR (6+ROW,3+COL,SUIT,1)
1930 CALL HCHAR (8+ROW,3+COL,SUIT,1)
1940 CALL HCHAR (6+ROW,5+COL,SUIT,1)
1950 CALL HCHAR (8+ROW,5+COL,SUIT,1)
1960 SUBEND
1970 REM SUBROUTINE ***RENDER5***
1980 SUB RENDER5(ROW,COL,SUIT)
1990 CALL RENDER4((ROW),(COL),(SUIT))
2000 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2010 SUBEND
2020 REM SUBROUTINE ***RENDER6***
2030 SUB RENDER6(ROW,COL,SUIT)
2040 CALL vCHAR (6+ROW,3+COL,SUIT,3)
2050 CALL vCHAR (6+ROW,5+COL,SUIT,3)
2060 SUBEND
2070 REM SUBROUTINE ***RENDER7***
2080 SUB RENDER7(ROW,COL,SUIT)
2090 CALL RENDER6((ROW),(COL),(SUIT))
2100 CALL VCHAR (7+ROW,4+COL,SUIT,1) 
2110 SUBEND
2120 REM SUBROUTINE ***RENDER8***
2130 SUB RENDER8(ROW,COL,SUIT)
2140 CALL HCHAR (5+ROW,3+COL,SUIT,1)
2150 CALL HCHAR (5+ROW,5+COL,SUIT,1)
2160 CALL HCHAR (6+ROW,4+COL,SUIT,1)
2170 CALL HCHAR (7+ROW,3+COL,SUIT,1)
2180 CALL HCHAR (7+ROW,5+COL,SUIT,1)
2190 CALL HCHAR (8+ROW,4+COL,SUIT,1)
2200 CALL HCHAR (9+ROW,3+COL,SUIT,1)
2210 CALL HCHAR (9+ROW,5+COL,SUIT,1)
2220 SUBEND
2230 REM SUBROUTINE ***RENDER9***
2240 SUB RENDER9(ROW,COL,SUIT)
2250 CALL VCHAR (5+ROW,3+COL,SUIT,2)
2260 CALL VCHAR (5+ROW,5+COL,SUIT,2)
2270 CALL VCHAR (8+ROW,3+COL,SUIT,2)
2280 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2290 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2300 SUBEND
2310 REM SUBROUTINE ***RENDER10***
2320 SUB RENDER10(ROW,COL,SUIT)
2330 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2340 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2350 SUBEND
2360 REM SUBROUTINE ***RENDERJACK***
2370 SUB RENDERJACK(ROW,COL,SUIT)
2380 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2390 CALL HCHAR (9+ROW,3+COL,SUIT,2)
2400 CALL HCHAR (8+ROW,3+COL,SUIT,1)
2410 SUBEND
2420 REM SUBROUTINE ***RENDERQUEEN***
2430 SUB RENDERQUEEN(ROW,COL,SUIT)
2440 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2450 CALL VCHAR (5+ROW,5+COL,SUIT,5)
2460 CALL HCHAR (5+ROW,4+COL,SUIT,1)
2470 CALL HCHAR (9+ROW,4+COL,SUIT,1)
2480 SUBEND
2490 REM SUBROUTINE ***RENDERKING***
2500 SUB RENDERKING(ROW,COL,SUIT)
2510 CALL VCHAR (5+ROW,3+COL,SUIT,5)
2520 CALL VCHAR (5+ROW,5+COL,SUIT,2)
2530 CALL VCHAR (8+ROW,5+COL,SUIT,2)
2540 CALL VCHAR (7+ROW,4+COL,SUIT,1)
2550 SUBEND
2560 REM SUBROUTINE ***RENDERACE***
2570 SUB RENDERACE(ROW,COL,SUIT)
2580 CALL HCHAR (7+ROW,4+COL,SUIT,1)
2590 SUBEND
2600 REM SUBROUTINE ***PLAY***
2610 SUB PLAY
2620 DIM HANDS(1,4,1) :: REM HANDS[PLAYER,CARD,SUIT]
2630 DIM CARDS(1) :: REM CARDS[PLAYER]
2640 DIM SUMS(1) :: REM SUMS[PLAYER]
2650 CALL CLEARTABLE(HANDS(,,),CARDS(),SUMS())
2660 CALL SHUFFLE(HANDS(,,))
2670 PLAYER=0 :: FACEDOWN=0
2680 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2690 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2700 PLAYER=1 :: FACEDOWN=0
2710 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2720 PLAYER=1 :: FACEDOWN=1
2730 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2740 REM PLAYERS TURNS
2750 BUSTED=0
2760 FOR J=3 TO 5
2770 DISPLAY AT (22,2) BEEP: "DO YOU WANT ANOTHER CARD?"
2780 A$=""
2790 CALL HCHAR(23,16,30)
2800 REM LABEL ***ASKANOTHER***
2810 CALL KEY(0,K,S)
2820 IF S=0  THEN 2810 :: REM GOTO ASKANOTHER
2830 IF K<>78 AND K<>89 AND K<>13 THEN 2810 :: REM GOTO ASKANOTHER
2840 IF K=78 THEN CALL HCHAR (23,16,78) :: A$="N" ELSE IF K=89 THEN CALL HCHAR (23,16,89) :: A$="Y"
2850 IF K<>13 THEN 2810 :: REM GOTO ASKANOTHER
2860 IF A$="" THEN 2810 :: REM GOTO ASKANOTHER
2870 CALL HCHAR (22,4,32,25) :: CALL HCHAR (23,16,32)
2880 IF A$="N" THEN 2960 :: REM GOTO REVEAL
2890 PLAYER=0 :: FACEDOWN=0
2900 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
2910 IF SUMS(PLAYER)>21 THEN BUSTED=1
2920 IF BUSTED=1 THEN DISPLAY AT (12,4) BEEP: "YOU BUSTED. YOU LOSE."
2930 IF BUSTED=1 THEN 2960 :: REM GOTO REVEAL
2940 NEXT J
2950 REM LABEL ***REVEAL***
2960 PLAYER=1 :: FACEDOWN=0 :: CARD=1
2970 CALL RENDERCARD((PLAYER),(CARD),(FACEDOWN),HANDS(,,))
2980 IF BUSTED=1 THEN 3130 :: REM GOTO GAMEOVER
2990 REM DEALERS TURNS
3000 BUSTED=0
3010 FOR J=3 TO 5
3020 PLAYER=1 :: FACEDOWN=0
3030 CALL CALCSCORE(SCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
3040 IF SCORE>=17 THEN 3110 :: REM GOTO TALLY
3050 CALL DEAL(CARDS(),SUMS(),(PLAYER),(FACEDOWN),HANDS(,,))
3060 IF SUMS(PLAYER)>21 THEN BUSTED=1
3070 IF BUSTED=1 THEN DISPLAY AT (12,3) BEEP: "DEALER BUSTED. YOU WIN."
3080 IF BUSTED=1 THEN 3130 :: REM GOTO GAMEOVER
3090 NEXT J
3100 REM LABEL ***TALLY***
3110 CALL WHOWON(SUMS(),CARDS(),HANDS(,,))
3120 REM LABEL ***GAMEOVER***
3130 SUBEND
3140 REM SUBROUTINE ***CLEARTABLE***
3150 SUB CLEARTABLE(HANDS(,,),CARDS(),SUMS())
3160 CALL CLEAR
3170 DISPLAY AT (1, 10): "BLACKJACK"
3180 FOR I=0 TO 1 :: SUMS(I)=0 :: CARDS(I)=0 :: NEXT I
3190 FOR I=0 TO 1 :: FOR J=0 TO 4 :: FOR K=0 TO 1 :: HANDS(I,J,K)=0 :: NEXT K :: NEXT J :: NEXT I
3200 SUBEND
3210 REM SUBROUTINE ***SHUFFLE***
3220 SUB SHUFFLE(HANDS(,,))
3230 RANDOMIZE
3240 DIM DECK(9) :: FOR I=0 TO 9 :: DECK(I)=0 :: NEXT I
3250 FOR H=0 TO 9
3260 REM LABEL ***DRAWLOOP***
3270 DISPLAY AT(12,6): "SHUFFLING THE DECK"
3280 X=INT(RND*4)+1
3290 X=X*16
3300 X=X+INT(RND*13)+2
3310 DUP=0
3320 CALL CHECKDUP(DUP,(X),DECK())
3330 IF DUP=1 THEN 3270 :: REM GOTO DRAWLOOP
3340 DECK(H)=X
3350 NEXT H
3360 FOR H=0 TO 4
3370 HANDS(0,H,1)=INT(DECK(H)/16)
3380 HANDS(0,H,0)=DECK(H)-HANDS(0,H,1)*16
3390 NEXT H
3400 FOR H=0 TO 4
3410 HANDS(1,H,1)=INT(DECK(H+5)/16)
3420 HANDS(1,H,0)=DECK(H+5)-HANDS(1,H,1)*16
3430 NEXT H
3440 DISPLAY AT(12,6): "                  "
3450 SUBEND
3460 REM SUBROUTINE ***CHECKDUP***
3470 SUB CHECKDUP(DUP,X,DECK())
3480 DUP=0
3490 FOR I=0 TO 9
3500 IF DECK(I)=X THEN DUP=1 :: GOTO 3520 :: REM SUBEND
3510 NEXT I
3520 SUBEND
3530 REM SUBROUTINE ***DEAL***
3540 SUB DEAL(CARDS(),SUMS(),PLAYER,FACEDOWN,HANDS(,,))
3550 CARDS(PLAYER)=CARDS(PLAYER)+1
3560 CARD=CARDS(PLAYER)-1
3570 CVAL=HANDS(PLAYER,CARD,0)
3580 IF CVAL>10 THEN IF CVAL=14 THEN CVAL=1 ELSE CVAL=10
3590 SUMS(PLAYER)=SUMS(PLAYER)+CVAL
3600 CALL RENDERCARD((PLAYER),(CARD),(FACEDOWN),HANDS(,,))
3610 SUBEND
3620 REM SUBROUTINE ***CALCSCORE***
3630 SUB CALCSCORE(SCORE,PLAYER,SUMS(),CARDS(),HANDS(,,))
3640 SCORE=SUMS(PLAYER)
3650 IF SCORE+10>21 THEN 3690 :: REM GOTO CALCSCORE.SUBEND
3660 FOR I=0 TO CARDS(PLAYER)-1
3670 IF HANDS(PLAYER,I,0)=14 THEN SCORE=SCORE+10 :: GOTO 3690 :: REM GOTO CALCSCORE.SUBEND
3680 NEXT I
3690 SUBEND
3700 REM SUBROUTINE ***WHOWON***
3710 SUB WHOWON(SUMS(),CARDS(),HANDS(,,))
3720 PLAYER=0 :: PSCORE=0
3730 CALL CALCSCORE(PSCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
3740 REM DISPLAY AT (1,24): "P:"&STR$(PSCORE)
3750 PLAYER=1 :: DSCORE=0
3760 CALL CALCSCORE(DSCORE,(PLAYER),SUMS(),CARDS(),HANDS(,,))
3770 REM DISPLAY AT (2,24):"D:"&STR$(DSCORE)
3780 IF PSCORE=DSCORE THEN DISPLAY AT (12,11): "TIE GAME"
3790 IF PSCORE>DSCORE THEN DISPLAY AT (12,11): "YOU WIN!"
3800 IF PSCORE<DSCORE THEN DISPLAY AT (12,11): "YOU LOSE"
3810 SUBEND
3820 REM SUBROUTINE ***TERMINATE***
3830 SUB TERMINATE
3840 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT (12,7) BEEP: "GAME TERMINATED" :: CALL SOUND(100,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3850 FOR I=1 TO 5 :: CALL SOUND(500,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3860 END
3870 SUBEND
3880 REM SUBROUTINE ***GETHEART***
3890 SUB GETHEART(CHARVAL)
3900 CHARVAL=96
3910 SUBEND
3920 REM SUBROUTINE ***GETDIAMOND***
3930 SUB GETDIAMOND(CHARVAL)
3940 CHARVAL=97
3950 SUBEND
3960 REM SUBROUTINE ***GETREDVALS***
3970 SUB GETREDVALS(CHARVAL)
3980 CHARVAL=98
3990 SUBEND
4000 REM SUBROUTINE ***GETCARDBACK***
4010 SUB GETCARDBACK(CHARVAL)
4020 CHARVAL=111
4030 SUBEND
4040 REM SUBROUTINE ***GETSPADE***
4050 SUB GETSPADE(CHARVAL)
4060 CHARVAL=112
4070 SUBEND
4080 REM SUBROUTINE ***GETCLUB***
4090 SUB GETCLUB(CHARVAL)
4100 CHARVAL=113
4110 SUBEND
4120 REM SUBROUTINE ***GETBLACKVALS***
4130 SUB GETBLACKVALS(CHARVAL)
4140 CHARVAL=114
4150 SUBEND
4160 REM SUBROUTINE ***GETCARDEDGE***
4170 SUB GETCARDEDGE(CHARVAL)
4180 CHARVAL=128
4190 SUBEND
4200 REM SUBROUTINE ***GETCARDTOP***
4210 SUB GETCARDTOP(CHARVAL)
4220 CHARVAL=129
4230 SUBEND
4240 REM SUBROUTINE ***GETCARDFRONT***
4250 SUB GETCARDFRONT(CHARVAL)
4260 CHARVAL=136
4270 SUBEND