100 CALL CLEAR
110 GOSUB 180 :: REM CALL INIT
120 GOSUB 320 :: REM CALL SETCHARS
130 GOSUB 400 :: REM CALL SETCOLORSCHEME
140 GOSUB 470 :: REM CALL RENDERBOARDS
150 GOSUB 540 :: REM CALL DEPLOYSHIPS
160 FOR I=1 TO 1000 :: NEXT I 
170 END
180 REM SUBROUTINE ***INIT***
190 RANDOMIZE
200 DEBUG=1
210 NUMSHIPS=5
220 DIM SHIPLENS(4) :: DIM SHIPNAMES$(4)
230 SHIPLENS(0)=5 :: SHIPLENS(1)=4 :: SHIPLENS(2)=3 :: SHIPLENS(3)=3 :: SHIPLENS(4)=2 
240 SHIPNAMES$(0)="CARRIER" :: SHIPNAMES$(1)="BATTLESHIP" :: SHIPNAMES$(2)="CRUISER" :: SHIPNAMES$(3)="SUBMARINE" :: SHIPNAMES$(4)="DESTROYER"
250 DIM BCOORDS(1,4,4) :: DIM BCOORDS$(1,4,4) :: REM BCOORDS[PLAYER,SHIP,POS], BCOORDS$[PLAYER,SHIP,POS]
260 DIM COORDS(4) :: DIM COORDS$(4)
270 XBOARD=3 :: YBOARD=5 :: REM BOARD ORIGINS
280 XAUX=23 :: YAUX=3 :: REM AUX BOARD ORIGINS
290 XMENU=23 :: YMENU=14 :: REM MENU ORIGINS
300 DIM SERIAL(4)
310 RETURN
320 REM SUBROUTINE ***SETCHARS***
330 HOLECHAR=96 :: CALL CHAR(HOLECHAR,"3C4299A5A599423C") :: REM SET 9
340 TENCHAR=97 :: CALL CHAR(TENCHAR,"004FC949494949EF") :: REM SET 9
350 HITCHAR=104 :: CALL CHAR(HITCHAR,"003C7E7E7E7E3C00") :: REM SET 10
360 SHIPCHAR=112 :: CALL CHAR(SHIPCHAR,"3C4299A5A599423C") :: REM SET 11
370 MISSCHAR=128 :: CALL CHAR(MISSCHAR,"3C4299A5A599423C") :: REM SET 13
380 SUNKCHAR=136 :: CALL CHAR(SUNKCHAR,"3C4299A5A599423C") :: REM SET 14
390 RETURN
400 REM SUBROUTINE ***SETCOLORSCHEME***
410 CALL COLOR(9,2,1) :: REM BLACK ON TRANSPARENT
420 CALL COLOR(10,2,12) :: REM BLACK ON LIGHT YELLOW
430 CALL COLOR(11,2,3) :: REM BLACK ON MED GREEN
440 CALL COLOR(13,2,16) :: REM BLACK ON WHITE
450 CALL COLOR(14,2,9) :: REM BLACK ON MED RED
460 RETURN
470 REM SUBROUTINE ***RENDERBOARDS***
480 CALL CLEAR
490 DISPLAY AT(1,9):"BATTLESHIP"
500 GOSUB 580 :: REM CALL RENDERBOARD
510 GOSUB 660 :: REM CALL RENDERAXES
520 GOSUB 770 :: REM CALL RENDERAUX
530 RETURN
540 REM SUBROUTINE ***DEPLOYSHIPS***
550 GOSUB 830 :: REM CALL PLAYERDELOY
560 GOSUB 1200 :: REM CALL COMPUTERDEPLOY
570 RETURN
580 REM SUBROUTINE ***RENDERBOARD***
590 X=XBOARD :: Y=YBOARD
600 FOR I=0 TO 18 STEP 2
610 FOR J=0 TO 18 STEP 2
620 CALL HCHAR(Y+I,X+J,HOLECHAR,1)
630 NEXT J
640 NEXT I
650 RETURN
660 REM SUBROUTINE ***RENDERAXES***
670 REM RENDER X-AXIS (NUMBERS)
680 FOR I=0 TO 8
690 CALL HCHAR(Y-2,I*2+X,49+I,1)
700 NEXT I
710 CALL HCHAR(Y-2,X+18,TENCHAR,1)
720 REM RENDER Y-AXIS (LETTERS)
730 FOR I=1 TO 10
740 CALL HCHAR(Y-2+I*2,X-2,64+I,1)
750 NEXT I
760 RETURN
770 REM SUBROUTINE ***RENDERAUX***
780 X=XAUX :: Y=YAUX
790 FOR I=0 TO 9
800 CALL HCHAR(Y+I,X,HOLECHAR,10)
810 NEXT I
820 RETURN
830 REM SUBROUTINE ***PLAYERDELOY***
840 GOSUB 1400 :: REM CALL CLEARMENU
850 CALL RENDERTEXT("AUTO",YMENU,XMENU)
860 CALL RENDERTEXT("DEPLOY",YMENU+1,XMENU)
870 CALL RENDERTEXT("YOUR",YMENU+2,XMENU)
880 CALL RENDERTEXT("SHIPS?",YMENU+3,XMENU)
890 CALL RENDERTEXT("[Y/N]:",YMENU+4,XMENU)
900 ACCEPT AT(YMENU+4,XMENU+4)VALIDATE("YN")BEEP SIZE(1):AUTODEPLOY$
910 IF AUTODEPLOY$="N" THEN 1020 :: REM GOTO PLAYERDEPLOYSHIPS
920 CALL RENDERTEXT("AUTO",YMENU+6,XMENU)
930 CALL RENDERTEXT("DEPLOYING",YMENU+7,XMENU)
940 CALL RENDERTEXT("SHIPS...",YMENU+8,XMENU)
950 GOSUB 1200 :: REM CALL COMPUTERDEPLOY
960 FOR I=0 TO NUMSHIPS-1
970 FOR J=0 TO SHIPLENS(I)-1
980 BCOORDS(0,I,J)=BCOORDS(1,I,J) :: BCOORDS$(0,I,J)=BCOORDS$(1,I,J)
990 NEXT J
1000 NEXT I
1010 GOTO 1160 :: REM GOTO PLAYERRENDERSHIPS
1020 REM LABEL ***PLAYERDEPLOYSHIPS***
1030 GOSUB 1400 :: REM CALL CLEARMENU
1040 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1050 REM LABEL ***PLAYERDEPLOYSHIP***
1060 GOSUB 1430 :: REM CALL PLAYERDEPLOYMENU
1070 GOSUB 1550 :: REM CALL PLAYERDEPLOYINPUT
1080 PLAYER=0
1090 GOSUB 1680 :: REM CALL VALIDATESHIP
1100 IF SHIPERR=1 THEN 1050 :: REM GOTO PLAYERDEPLOYSHIP
1110 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1120 BCOORDS(0,CURRENTSHIP,I)=COORDS(I) :: BCOORDS$(0,CURRENTSHIP,I)=COORDS$(I)
1130 NEXT I
1140 GOSUB 1780 :: REM CALL RENDERSHIP
1150 NEXT CURRENTSHIP
1160 GOSUB 1840 :: REM CALL RENDERSHIPSAUX
1170 GOSUB 580 :: REM CALL RENDERBOARD
1180 GOSUB 1920 :: REM CALL PLAYERDEPLOYED
1190 RETURN
1200 REM SUBROUTINE ***COMPUTERDEPLOY***
1210 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1220 REM LABEL ***COMPUTERDEPLOYSHIP***
1230 HORIZONTAL=INT(RND*2)
1240 SHIPBEGIN=INT(RND*(10-SHIPLENS(CURRENTSHIP)))+1
1250 STATIC=INT(RND*10)+1
1260 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1270 IF HORIZONTAL=1 THEN COORDS(I)=SHIPBEGIN+I ELSE COORDS$(I)=CHR$(64+SHIPBEGIN+I)
1280 IF HORIZONTAL=1 THEN COORDS$(I)=CHR$(64+STATIC) ELSE COORDS(I)=STATIC
1290 NEXT I
1300 SHIPERR=0
1310 PLAYER=1
1320 GOSUB 2010 :: REM CALL CHECKOVERLAP
1330 IF SHIPERR=1 THEN 1220 :: REM GOTO COMPUTERDEPLOYSHIP
1340 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1350 BCOORDS(1,CURRENTSHIP,I)=COORDS(I) :: BCOORDS$(1,CURRENTSHIP,I)=COORDS$(I)
1360 NEXT I
1370 IF DEBUG=1 THEN GOSUB 1780 :: REM CALL RENDERSHIP
1380 NEXT CURRENTSHIP
1390 RETURN
1400 REM SUBROUTINE ***CLEARMENU***
1410 FOR I=0 TO 9 :: CALL HCHAR(YMENU+I,XMENU,32,10) :: NEXT I
1420 RETURN
1430 REM SUBROUTINE ***PLAYERDEPLOYMENU***
1440 CALL RENDERTEXT("INPUT THE",YMENU,XMENU)
1450 CALL RENDERTEXT("LOCATIONS",YMENU+1,XMENU)
1460 CALL RENDERTEXT("FOR YOUR",YMENU+2,XMENU)
1470 CALL RENDERTEXT("          ",YMENU+3,XMENU)
1480 CALL RENDERTEXT(SHIPNAMES$(CURRENTSHIP),YMENU+3,XMENU)
1490 CALL RENDERTEXT("[IE. C3]:",YMENU+4,XMENU)
1500 FOR I=1 TO 5
1510 IF I>SHIPLENS(CURRENTSHIP) THEN TEXT$="        " ELSE TEXT$="POS"&STR$(I)&":    "
1520 CALL RENDERTEXT(TEXT$,YMENU+4+I,XMENU)
1530 NEXT I
1540 RETURN
1550 REM SUBROUTINE ***PLAYERDEPLOYINPUT***
1560 ON WARNING NEXT
1570 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1580 REM LABEL ***INPUTROW***
1590 ACCEPT AT(YMENU+5+I,XMENU+3)VALIDATE("ABCDEFGHIJ")BEEP SIZE(1):COORDS$(I)
1600 IF COORDS$(I)="" THEN  1580 :: REM GOTO INPUTROW
1610 REM LABEL ***INPUTCOL***
1620 ACCEPT AT(YMENU+5+I,XMENU+4)VALIDATE(DIGIT)BEEP SIZE(2):COORDS(I)
1630 IF COORDS(I)>10 THEN  1610 :: REM GOTO INPUTCOL
1640 IF COORDS(I)<1 THEN  1610 :: REM GOTO INPUTCOL
1650 NEXT I
1660 ON WARNING PRINT
1670 RETURN
1680 REM SUBROUTINE ***VALIDATESHIP***
1690 SHIPERR=0
1700 GOSUB 2120 :: REM CALL CHECKHORIZONTAL
1710 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1720 IF HORIZONTAL=1 THEN SERIAL(I)=COORDS(I) ELSE SERIAL(I)=ASC(COORDS$(I))-64
1730 NEXT I
1740 GOSUB 2190 :: REM CALL CHECKSEQUENCE
1750 IF SHIPERR=1 THEN RETURN
1760 GOSUB 2010 :: REM CALL CHECKOVERLAP
1770 RETURN
1780 REM SUBROUTINE ***RENDERSHIP***
1790 X=XBOARD :: Y=YBOARD
1800 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1810 CALL HCHAR(Y+(ASC(COORDS$(I))-65)*2,X+(COORDS(I)-1)*2,SHIPCHAR,1)
1820 NEXT I
1830 RETURN
1840 REM SUBROUTINE ***RENDERSHIPSAUX***
1850 X=XAUX :: Y=YAUX
1860 FOR I=0 TO NUMSHIPS-1
1870 FOR J=0 TO SHIPLENS(I)-1
1880 CALL HCHAR(Y+ASC(BCOORDS$(0,I,J))-65,X+BCOORDS(0,I,J)-1,SHIPCHAR,1)
1890 NEXT J
1900 NEXT I
1910 RETURN
1920 REM SUBROUTINE ***PLAYERDEPLOYED***
1930 GOSUB 1400 :: REM CALL CLEARMENU
1940 CALL RENDERTEXT("PLAYER'S",YMENU,XMENU)
1950 CALL RENDERTEXT("SHIPS",YMENU+1,XMENU)
1960 CALL RENDERTEXT("DEPLOYED.",YMENU+2,XMENU)
1970 CALL RENDERTEXT("COMPUTER",YMENU+4,XMENU)
1980 CALL RENDERTEXT("DEPLOYING",YMENU+5,XMENU)
1990 CALL RENDERTEXT("SHIPS...",YMENU+6,XMENU)
2000 RETURN
2010 REM SUBROUTINE ***CHECKOVERLAP***
2020 FOR I=0 TO CURRENTSHIP-1
2030 FOR J=0 TO SHIPLENS(I)-1
2040 STOREDLOC=(ASC(BCOORDS$(PLAYER,I,J))-64)*16+BCOORDS(PLAYER,I,J)
2050 FOR K=0 TO SHIPLENS(CURRENTSHIP)-1
2060 LOC=(ASC(COORDS$(K))-64)*16+COORDS(K)
2070 IF LOC=STOREDLOC THEN SHIPERR=1 :: RETURN
2080 NEXT K
2090 NEXT J
2100 NEXT I
2110 RETURN
2120 REM SUBROUTINE ***CHECKHORIZONTAL***
2130 HORIZONTAL=0
2140 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2150 IF COORDS$(I-1)<>COORDS$(I) THEN RETURN
2160 NEXT I
2170 HORIZONTAL=1
2180 RETURN
2190 REM SUBROUTINE ***CHECKSEQUENCE***
2200 MINCOORD=SERIAL(0)
2210 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2220 MINCOORD=MIN(MINCOORD,SERIAL(I))
2230 NEXT I
2240 EXPSUM=MINCOORD
2250 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2260 EXPSUM=EXPSUM+MINCOORD+I
2270 NEXT I
2280 ACTSUM=SERIAL(0)
2290 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2300 ACTSUM=ACTSUM+SERIAL(I)
2310 NEXT I
2320 IF EXPSUM<>ACTSUM THEN SHIPERR=1
2330 RETURN
2340 REM SUBROUTINE ***RENDERTEXT***
2350 SUB RENDERTEXT(TEXT$,ROW,COL)
2360 STRLEN=LEN(TEXT$)
2370 FOR I=0 TO STRLEN-1
2380 C$=SEG$(TEXT$,I+1,1)
2390 CALL HCHAR(ROW,COL+I,ASC(C$))
2400 NEXT I
2410 SUBEND