100 CALL CLEAR
110 CALL SCREEN(12)
120 GOSUB 190 :: REM CALL INIT
130 GOSUB 330 :: REM CALL SETCHARS
140 GOSUB 420 :: REM CALL SETCOLORSCHEME
150 GOSUB 500 :: REM CALL RENDERBOARDS
160 GOSUB 560 :: REM CALL DEPLOYSHIPS
170 FOR I=1 TO 1000 :: NEXT I 
180 END
190 REM SUBROUTINE ***INIT***
200 RANDOMIZE
210 DEBUG=1
220 NUMSHIPS=5
230 DIM SHIPLENS(4) :: DIM SHIPNAMES$(4)
240 SHIPLENS(0)=5 :: SHIPLENS(1)=4 :: SHIPLENS(2)=3 :: SHIPLENS(3)=3 :: SHIPLENS(4)=2 
250 SHIPNAMES$(0)="CARRIER" :: SHIPNAMES$(1)="BATTLESHIP" :: SHIPNAMES$(2)="CRUISER" :: SHIPNAMES$(3)="SUBMARINE" :: SHIPNAMES$(4)="DESTROYER"
260 DIM BCOORDS(1,4,4) :: DIM BCOORDS$(1,4,4) :: REM BCOORDS[PLAYER,SHIP,POS], BCOORDS$[PLAYER,SHIP,POS]
270 DIM COORDS(4) :: DIM COORDS$(4)
280 XBOARD=3 :: YBOARD=5 :: REM BOARD ORIGINS
290 XAUX=23 :: YAUX=3 :: REM AUX BOARD ORIGINS
300 XMENU=23 :: YMENU=14 :: REM MENU ORIGINS
310 DIM SERIAL(4)
320 RETURN
330 REM SUBROUTINE ***SETCHARS***
340 HOLECHAR=96 :: CALL CHAR(HOLECHAR,"3C4299A5A599423C") :: REM SET 9
350 FILLCHAR=97 :: CALL CHAR(FILLCHAR,"0000000000000000") :: REM SET 9
360 HITCHAR=104 :: CALL CHAR(HITCHAR,"003C7E7E7E7E3C00") :: REM SET 10
370 SHIPCHAR=112 :: CALL CHAR(SHIPCHAR,"3C4299A5A599423C") :: REM SET 11
380 TENCHAR=120 :: CALL CHAR(TENCHAR,"004FC949494949EF") :: REM SET 12
390 MISSCHAR=128 :: CALL CHAR(MISSCHAR,"3C4299A5A599423C") :: REM SET 13
400 SUNKCHAR=136 :: CALL CHAR(SUNKCHAR,"3C4299A5A599423C") :: REM SET 14
410 RETURN
420 REM SUBROUTINE ***SETCOLORSCHEME***
430 CALL COLOR(9,2,8) :: REM BLACK ON CYAN
440 CALL COLOR(10,2,11) :: REM BLACK ON DARK YELLOW
450 CALL COLOR(11,2,15) :: REM BLACK ON GREY
460 CALL COLOR(12,2,1) :: REM BLACK ON TRANSPARENT
470 CALL COLOR(13,2,16) :: REM BLACK ON WHITE
480 CALL COLOR(14,2,9) :: REM BLACK ON MED RED
490 RETURN
500 REM SUBROUTINE ***RENDERBOARDS***
510 CALL CLEAR
520 DISPLAY AT(1,9):"BATTLESHIP"
530 GOSUB 600 :: REM CALL RENDERBOARD
540 GOSUB 900 :: REM CALL RENDERAUX
550 RETURN
560 REM SUBROUTINE ***DEPLOYSHIPS***
570 GOSUB 960 :: REM CALL PLAYERDELOY
580 GOSUB 1330 :: REM CALL COMPUTERDEPLOY
590 RETURN
600 REM SUBROUTINE ***RENDERBOARD***
610 GOSUB 650 :: REM CALL RENDERBACKBOARD
620 GOSUB 790 :: REM CALL RENDERAXES
630 GOSUB 710 :: REM CALL RENDERHOLES
640 RETURN
650 REM SUBROUTINE ***RENDERBACKBOARD***
660 X=XBOARD :: Y=YBOARD
670 FOR I=0 TO 18
680 CALL HCHAR(Y+I,X,FILLCHAR,19)
690 NEXT I
700 RETURN
710 REM SUBROUTINE ***RENDERHOLES***
720 X=XBOARD :: Y=YBOARD
730 FOR I=0 TO 18 STEP 2
740 FOR J=0 TO 18 STEP 2
750 CALL HCHAR(Y+I,X+J,HOLECHAR,1)
760 NEXT J
770 NEXT I
780 RETURN
790 REM SUBROUTINE ***RENDERAXES***
800 REM RENDER X-AXIS (NUMBERS)
810 FOR I=0 TO 8
820 CALL HCHAR(Y-2,I*2+X,49+I,1)
830 NEXT I
840 CALL HCHAR(Y-2,X+18,TENCHAR,1)
850 REM RENDER Y-AXIS (LETTERS)
860 FOR I=1 TO 10
870 CALL HCHAR(Y-2+I*2,X-2,64+I,1)
880 NEXT I
890 RETURN
900 REM SUBROUTINE ***RENDERAUX***
910 X=XAUX :: Y=YAUX
920 FOR I=0 TO 9
930 CALL HCHAR(Y+I,X,HOLECHAR,10)
940 NEXT I
950 RETURN
960 REM SUBROUTINE ***PLAYERDELOY***
970 GOSUB 1530 :: REM CALL CLEARMENU
980 CALL RENDERTEXT("AUTO",YMENU,XMENU)
990 CALL RENDERTEXT("DEPLOY",YMENU+1,XMENU)
1000 CALL RENDERTEXT("YOUR",YMENU+2,XMENU)
1010 CALL RENDERTEXT("SHIPS?",YMENU+3,XMENU)
1020 CALL RENDERTEXT("[Y/N]:",YMENU+4,XMENU)
1030 ACCEPT AT(YMENU+4,XMENU+4)VALIDATE("YN")BEEP SIZE(1):AUTODEPLOY$
1040 IF AUTODEPLOY$="N" THEN 1150 :: REM GOTO PLAYERDEPLOYSHIPS
1050 CALL RENDERTEXT("AUTO",YMENU+6,XMENU)
1060 CALL RENDERTEXT("DEPLOYING",YMENU+7,XMENU)
1070 CALL RENDERTEXT("SHIPS...",YMENU+8,XMENU)
1080 GOSUB 1330 :: REM CALL COMPUTERDEPLOY
1090 FOR I=0 TO NUMSHIPS-1
1100 FOR J=0 TO SHIPLENS(I)-1
1110 BCOORDS(0,I,J)=BCOORDS(1,I,J) :: BCOORDS$(0,I,J)=BCOORDS$(1,I,J)
1120 NEXT J
1130 NEXT I
1140 GOTO 1290 :: REM GOTO PLAYERRENDERSHIPS
1150 REM LABEL ***PLAYERDEPLOYSHIPS***
1160 GOSUB 1530 :: REM CALL CLEARMENU
1170 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1180 REM LABEL ***PLAYERDEPLOYSHIP***
1190 GOSUB 1560 :: REM CALL PLAYERDEPLOYMENU
1200 GOSUB 1680 :: REM CALL PLAYERDEPLOYINPUT
1210 PLAYER=0
1220 GOSUB 1810 :: REM CALL VALIDATESHIP
1230 IF SHIPERR=1 THEN 1180 :: REM GOTO PLAYERDEPLOYSHIP
1240 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1250 BCOORDS(0,CURRENTSHIP,I)=COORDS(I) :: BCOORDS$(0,CURRENTSHIP,I)=COORDS$(I)
1260 NEXT I
1270 GOSUB 1910 :: REM CALL RENDERSHIP
1280 NEXT CURRENTSHIP
1290 GOSUB 1970 :: REM CALL RENDERSHIPSAUX
1300 GOSUB 710 :: REM CALL RENDERHOLES
1310 GOSUB 2050 :: REM CALL PLAYERDEPLOYED
1320 RETURN
1330 REM SUBROUTINE ***COMPUTERDEPLOY***
1340 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1350 REM LABEL ***COMPUTERDEPLOYSHIP***
1360 HORIZONTAL=INT(RND*2)
1370 SHIPBEGIN=INT(RND*(10-SHIPLENS(CURRENTSHIP)))+1
1380 STATIC=INT(RND*10)+1
1390 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1400 IF HORIZONTAL=1 THEN COORDS(I)=SHIPBEGIN+I ELSE COORDS$(I)=CHR$(64+SHIPBEGIN+I)
1410 IF HORIZONTAL=1 THEN COORDS$(I)=CHR$(64+STATIC) ELSE COORDS(I)=STATIC
1420 NEXT I
1430 SHIPERR=0
1440 PLAYER=1
1450 GOSUB 2140 :: REM CALL CHECKOVERLAP
1460 IF SHIPERR=1 THEN 1350 :: REM GOTO COMPUTERDEPLOYSHIP
1470 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1480 BCOORDS(1,CURRENTSHIP,I)=COORDS(I) :: BCOORDS$(1,CURRENTSHIP,I)=COORDS$(I)
1490 NEXT I
1500 IF DEBUG=1 THEN GOSUB 1910 :: REM CALL RENDERSHIP
1510 NEXT CURRENTSHIP
1520 RETURN
1530 REM SUBROUTINE ***CLEARMENU***
1540 FOR I=0 TO 9 :: CALL HCHAR(YMENU+I,XMENU,32,10) :: NEXT I
1550 RETURN
1560 REM SUBROUTINE ***PLAYERDEPLOYMENU***
1570 CALL RENDERTEXT("INPUT THE",YMENU,XMENU)
1580 CALL RENDERTEXT("LOCATIONS",YMENU+1,XMENU)
1590 CALL RENDERTEXT("FOR YOUR",YMENU+2,XMENU)
1600 CALL RENDERTEXT("          ",YMENU+3,XMENU)
1610 CALL RENDERTEXT(SHIPNAMES$(CURRENTSHIP),YMENU+3,XMENU)
1620 CALL RENDERTEXT("[IE. C3]:",YMENU+4,XMENU)
1630 FOR I=1 TO 5
1640 IF I>SHIPLENS(CURRENTSHIP) THEN TEXT$="        " ELSE TEXT$="POS"&STR$(I)&":    "
1650 CALL RENDERTEXT(TEXT$,YMENU+4+I,XMENU)
1660 NEXT I
1670 RETURN
1680 REM SUBROUTINE ***PLAYERDEPLOYINPUT***
1690 ON WARNING NEXT
1700 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1710 REM LABEL ***INPUTROW***
1720 ACCEPT AT(YMENU+5+I,XMENU+3)VALIDATE("ABCDEFGHIJ")BEEP SIZE(1):COORDS$(I)
1730 IF COORDS$(I)="" THEN  1710 :: REM GOTO INPUTROW
1740 REM LABEL ***INPUTCOL***
1750 ACCEPT AT(YMENU+5+I,XMENU+4)VALIDATE(DIGIT)BEEP SIZE(2):COORDS(I)
1760 IF COORDS(I)>10 THEN  1740 :: REM GOTO INPUTCOL
1770 IF COORDS(I)<1 THEN  1740 :: REM GOTO INPUTCOL
1780 NEXT I
1790 ON WARNING PRINT
1800 RETURN
1810 REM SUBROUTINE ***VALIDATESHIP***
1820 SHIPERR=0
1830 GOSUB 2250 :: REM CALL CHECKHORIZONTAL
1840 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1850 IF HORIZONTAL=1 THEN SERIAL(I)=COORDS(I) ELSE SERIAL(I)=ASC(COORDS$(I))-64
1860 NEXT I
1870 GOSUB 2320 :: REM CALL CHECKSEQUENCE
1880 IF SHIPERR=1 THEN RETURN
1890 GOSUB 2140 :: REM CALL CHECKOVERLAP
1900 RETURN
1910 REM SUBROUTINE ***RENDERSHIP***
1920 X=XBOARD :: Y=YBOARD
1930 FOR I=0 TO SHIPLENS(CURRENTSHIP)-1
1940 CALL HCHAR(Y+(ASC(COORDS$(I))-65)*2,X+(COORDS(I)-1)*2,SHIPCHAR,1)
1950 NEXT I
1960 RETURN
1970 REM SUBROUTINE ***RENDERSHIPSAUX***
1980 X=XAUX :: Y=YAUX
1990 FOR I=0 TO NUMSHIPS-1
2000 FOR J=0 TO SHIPLENS(I)-1
2010 CALL HCHAR(Y+ASC(BCOORDS$(0,I,J))-65,X+BCOORDS(0,I,J)-1,SHIPCHAR,1)
2020 NEXT J
2030 NEXT I
2040 RETURN
2050 REM SUBROUTINE ***PLAYERDEPLOYED***
2060 GOSUB 1530 :: REM CALL CLEARMENU
2070 CALL RENDERTEXT("PLAYER'S",YMENU,XMENU)
2080 CALL RENDERTEXT("SHIPS",YMENU+1,XMENU)
2090 CALL RENDERTEXT("DEPLOYED.",YMENU+2,XMENU)
2100 CALL RENDERTEXT("COMPUTER",YMENU+4,XMENU)
2110 CALL RENDERTEXT("DEPLOYING",YMENU+5,XMENU)
2120 CALL RENDERTEXT("SHIPS...",YMENU+6,XMENU)
2130 RETURN
2140 REM SUBROUTINE ***CHECKOVERLAP***
2150 FOR I=0 TO CURRENTSHIP-1
2160 FOR J=0 TO SHIPLENS(I)-1
2170 STOREDLOC=(ASC(BCOORDS$(PLAYER,I,J))-64)*16+BCOORDS(PLAYER,I,J)
2180 FOR K=0 TO SHIPLENS(CURRENTSHIP)-1
2190 LOC=(ASC(COORDS$(K))-64)*16+COORDS(K)
2200 IF LOC=STOREDLOC THEN SHIPERR=1 :: RETURN
2210 NEXT K
2220 NEXT J
2230 NEXT I
2240 RETURN
2250 REM SUBROUTINE ***CHECKHORIZONTAL***
2260 HORIZONTAL=0
2270 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2280 IF COORDS$(I-1)<>COORDS$(I) THEN RETURN
2290 NEXT I
2300 HORIZONTAL=1
2310 RETURN
2320 REM SUBROUTINE ***CHECKSEQUENCE***
2330 MINCOORD=SERIAL(0)
2340 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2350 MINCOORD=MIN(MINCOORD,SERIAL(I))
2360 NEXT I
2370 EXPSUM=MINCOORD
2380 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2390 EXPSUM=EXPSUM+MINCOORD+I
2400 NEXT I
2410 ACTSUM=SERIAL(0)
2420 FOR I=1 TO SHIPLENS(CURRENTSHIP)-1
2430 ACTSUM=ACTSUM+SERIAL(I)
2440 NEXT I
2450 IF EXPSUM<>ACTSUM THEN SHIPERR=1
2460 RETURN
2470 REM SUBROUTINE ***RENDERTEXT***
2480 SUB RENDERTEXT(TEXT$,ROW,COL)
2490 STRLEN=LEN(TEXT$)
2500 FOR I=0 TO STRLEN-1
2510 C$=SEG$(TEXT$,I+1,1)
2520 CALL HCHAR(ROW,COL+I,ASC(C$))
2530 NEXT I
2540 SUBEND