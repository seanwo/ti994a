100 CALL CLEAR
110 CALL SCREEN(4)
120 RANDOMIZE
130 DIM SHIPS(1,4,4) :: REM SHIPS[PLAYER,SHIP,POS]
140 DIM SHOTS(1,9,9) :: REM SHOTS[PLAYER, ROW,COL]
150 DIM HITS(1,4,4) :: REM HITS[PLAYER,SHIP,POS]
160 CALL SETCHARS
170 CALL SETCOLORSCHEME
180 CALL RENDERBOARDS
190 CALL DEPLOYSHIPS(SHIPS(,,))
200 CALL PLAYGAME
210 FOR I=1 TO 1000 :: NEXT I 
220 END
230 REM SUBROUTINE ***SETCHARS***
240 SUB SETCHARS
250 CALL GETHOLECHAR(HOLECHAR)
260 CALL CHAR(HOLECHAR,"3C4299A5A599423C")
270 CALL GETFILLCHAR(FILLCHAR)
280 CALL CHAR(FILLCHAR,"0000000000000000")
290 CALL GETHITCHAR(HITCHAR)
300 CALL CHAR(HITCHAR,"003C7E7E7E7E3C00")
310 CALL GETSHIPCHAR(SHIPCHAR)
320 CALL CHAR(SHIPCHAR,"3C4299A5A599423C")
330 CALL GETTENCHAR(TENCHAR)
340 CALL CHAR(TENCHAR,"004FC949494949EF")
350 CALL GETMISSCHAR(MISSCHAR)
360 CALL CHAR(MISSCHAR,"3C4299A5A599423C")
370 CALL GETSUNKCHAR(SUNKCHAR)
380 CALL CHAR(SUNKCHAR,"3C4299A5A599423C") 
390 SUBEND
400 REM SUBROUTINE ***SETCOLORSCHEME***
410 SUB SETCOLORSCHEME
420 CALL COLOR(9,2,8) :: REM BLACK ON CYAN
430 CALL COLOR(10,2,11) :: REM BLACK ON DARK YELLOW
440 CALL COLOR(11,2,15) :: REM BLACK ON GREY
450 CALL COLOR(12,2,1) :: REM BLACK ON TRANSPARENT
460 CALL COLOR(13,2,16) :: REM BLACK ON WHITE
470 CALL COLOR(14,2,9) :: REM BLACK ON MED RED
480 SUBEND
490 REM SUBROUTINE ***RENDERBOARDS***
500 SUB RENDERBOARDS
510 CALL CLEAR
520 DISPLAY AT(1,9):"BATTLESHIP"
530 CALL RENDERBOARD
540 CALL RENDERAUX
550 SUBEND
560 REM SUBROUTINE ***DEPLOYSHIPS***
570 SUB DEPLOYSHIPS(SHIPS(,,))
580 CALL PLAYERDELOY(SHIPS(,,))
590 CALL COMPUTERDEPLOY(SHIPS(,,))
600 SUBEND
610 REM SUBROUTINE ***PLAYGAME***
620 SUB PLAYGAME
630 CALL CLEARMENU
640 CALL TARGETMENU
650 CALL GETMENUORIG(ROW,COL)
660 WINNER=0
670 REM LABEL ***SHOTLOOP***
680 CALL HCHAR(ROW+3,COL,32,10)
690 CALL INPUTPOS(ROW+3,COL,INPUTROW$,INPUTCOL)
700 GOTO 670 :: REM GOTO SHOTLOOP
710 SUBEND
720 REM SUBROUTINE ***TARGETMENU***
730 SUB TARGETMENU
740 CALL GETMENUORIG(ROW,COL)
750 CALL RENDERTEXT("PLAYER",ROW,COL)
760 CALL RENDERTEXT("TARGET",ROW+1,COL)
770 CALL RENDERTEXT("[IE. C3]",ROW+2,COL)
780 CALL RENDERTEXT("COMPUTER",ROW+6,COL)
790 CALL RENDERTEXT("TARGET",ROW+7,COL)
800 SUBEND
810 REM SUBROUTINE ***RENDERBOARD***
820 SUB RENDERBOARD
830 CALL RENDERBACKBOARD
840 CALL RENDERAXES
850 CALL RENDERHOLES
860 SUBEND
870 REM SUBROUTINE ***RENDERBACKBOARD***
880 SUB RENDERBACKBOARD
890 CALL GETBOARDORIG(ROW,COL)
900 CALL GETFILLCHAR(FILLCHAR)
910 FOR I=0 TO 18
920 CALL HCHAR(ROW+I,COL,FILLCHAR,19)
930 NEXT I
940 SUBEND
950 REM SUBROUTINE ***RENDERHOLES***
960 SUB RENDERHOLES
970 CALL GETBOARDORIG(ROW,COL)
980 CALL GETHOLECHAR(HOLECHAR)
990 FOR I=0 TO 18 STEP 2
1000 FOR J=0 TO 18 STEP 2
1010 CALL HCHAR(ROW+I,COL+J,HOLECHAR,1)
1020 NEXT J
1030 NEXT I
1040 SUBEND
1050 REM SUBROUTINE ***RENDERAXES***
1060 SUB RENDERAXES
1070 CALL GETBOARDORIG(ROW,COL)
1080 CALL GETTENCHAR(TENCHAR)
1090 REM RENDER X-AXIS (NUMBERS)
1100 FOR I=0 TO 8
1110 CALL HCHAR(ROW-2,I*2+COL,49+I,1)
1120 NEXT I
1130 CALL HCHAR(ROW-2,COL+18,TENCHAR,1)
1140 REM RENDER Y-AXIS (LETTERS)
1150 FOR I=1 TO 10
1160 CALL HCHAR(ROW-2+I*2,COL-2,64+I,1)
1170 NEXT I
1180 SUBEND
1190 REM SUBROUTINE ***RENDERAUX***
1200 SUB RENDERAUX
1210 CALL GETAUXORIG(ROW,COL)
1220 CALL GETHOLECHAR(HOLECHAR)
1230 FOR I=0 TO 9
1240 CALL HCHAR(ROW+I,COL,HOLECHAR,10)
1250 NEXT I
1260 SUBEND
1270 REM SUBROUTINE ***PLAYERDELOY***
1280 SUB PLAYERDELOY(SHIPS(,,))
1290 CALL GETNUMSHIPS(NUMSHIPS)
1300 CALL GETMENUORIG(ROW,COL)
1310 CALL CLEARMENU
1320 CALL RENDERTEXT("AUTO",ROW,COL)
1330 CALL RENDERTEXT("DEPLOY",ROW+1,COL)
1340 CALL RENDERTEXT("YOUR",ROW+2,COL)
1350 CALL RENDERTEXT("SHIPS?",ROW+3,COL)
1360 CALL RENDERTEXT("[Y/N]:",ROW+4,COL)
1370 ACCEPT AT(ROW+4,COL+4)VALIDATE("YN")BEEP SIZE(1):AUTODEPLOY$
1380 IF AUTODEPLOY$="N" THEN 1500 :: REM GOTO PLAYERDEPLOYSHIPS
1390 CALL RENDERTEXT("AUTO",ROW+6,COL)
1400 CALL RENDERTEXT("DEPLOYING",ROW+7,COL)
1410 CALL RENDERTEXT("SHIPS...",ROW+8,COL)
1420 CALL COMPUTERDEPLOY(SHIPS(,,))
1430 FOR I=0 TO NUMSHIPS-1
1440 CALL GETSHIPLEN(SHIPLEN,I)
1450 FOR J=0 TO SHIPLEN-1
1460 SHIPS(0,I,J)=SHIPS(1,I,J)
1470 NEXT J
1480 NEXT I
1490 GOTO 1670 :: REM GOTO PLAYERRENDERSHIPS
1500 REM LABEL ***PLAYERDEPLOYSHIPS***
1510 CALL CLEARMENU
1520 DIM SHIP(4)
1530 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1540 REM LABEL ***PLAYERDEPLOYSHIP***
1550 CALL DEPLOYMENU(CURRENTSHIP)
1560 CALL INPUTSHIP(SHIP(),CURRENTSHIP)
1570 PLAYER=0
1580 SHIPERR=0
1590 CALL VALIDATESHIP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
1600 IF SHIPERR=1 THEN 1540 :: REM GOTO PLAYERDEPLOYSHIP
1610 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
1620 FOR I=0 TO SHIPLEN-1
1630 SHIPS(0,CURRENTSHIP,I)=SHIP(I)
1640 NEXT I
1650 CALL RENDERSHIP(CURRENTSHIP,SHIP())
1660 NEXT CURRENTSHIP
1670 CALL RENDERSHIPSAUX(SHIPS(,,))
1680 CALL RENDERHOLES
1690 CALL PLAYERDEPLOYED
1700 SUBEND
1710 REM SUBROUTINE ***COMPUTERDEPLOY***
1720 SUB COMPUTERDEPLOY(SHIPS(,,))
1730 DIM SHIP(4)
1740 CALL GETNUMSHIPS(NUMSHIPS)
1750 CALL GETBOARDORIG(ROW,COL)
1760 CALL GETSHIPCHAR(SHIPCHAR)
1770 FOR CURRENTSHIP=0 TO NUMSHIPS-1
1780 REM LABEL ***COMPUTERDEPLOYSHIP***
1790 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
1800 HORIZONTAL=INT(RND*2)
1810 SHIPBEGIN=INT(RND*(10-SHIPLEN))+1
1820 STATIC=INT(RND*10)+1
1830 FOR I=0 TO SHIPLEN-1
1840 IF HORIZONTAL=1 THEN SHIP(I)=16*STATIC+SHIPBEGIN+I ELSE SHIP(I)=16*(SHIPBEGIN+I)+STATIC
1850 NEXT I
1860 PLAYER=1
1870 SHIPERR=0
1880 CALL CHECKOVERLAP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
1890 IF SHIPERR=1 THEN 1780 :: REM GOTO COMPUTERDEPLOYSHIP
1900 FOR I=0 TO SHIPLEN-1
1910 SHIPS(1,CURRENTSHIP,I)=SHIP(I)
1920 NEXT I
1930 CALL GETDEBUGFLAG(DEBUG)
1940 IF DEBUG=1 THEN CALL RENDERSHIP(CURRENTSHIP,SHIP())
1950 NEXT CURRENTSHIP
1960 SUBEND
1970 REM SUBROUTINE ***DEPLOYMENU***
1980 SUB DEPLOYMENU(CURRENTSHIP)
1990 CALL GETMENUORIG(ROW,COL)
2000 CALL RENDERTEXT("INPUT THE",ROW,COL)
2010 CALL RENDERTEXT("LOCATIONS",ROW+1,COL)
2020 CALL RENDERTEXT("FOR YOUR",ROW+2,COL)
2030 CALL RENDERTEXT("          ",ROW+3,COL)
2040 CALL GETSHIPNAME(SHIPNAME$,CURRENTSHIP)
2050 CALL RENDERTEXT(SHIPNAME$,ROW+3,COL)
2060 CALL RENDERTEXT("[IE. C3]:",ROW+4,COL)
2070 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
2080 FOR I=1 TO 5
2090 IF I>SHIPLEN THEN TEXT$="        " ELSE TEXT$="POS"&STR$(I)&":    "
2100 CALL RENDERTEXT(TEXT$,ROW+4+I,COL)
2110 NEXT I
2120 SUBEND
2130 REM SUBROUTINE ***INPUTSHIP***
2140 SUB INPUTSHIP(SHIP(),CURRENTSHIP)
2150 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
2160 CALL GETMENUORIG(ROW,COL)
2170 FOR I=0 TO SHIPLEN-1
2180 INPUTROW$="" :: INPUTCOL=0
2190 CALL INPUTPOS(ROW+5+I,COL+5,INPUTROW$,INPUTCOL)
2200 SHIP(I)=16*(ASC(INPUTROW$)-64)+INPUTCOL
2210 NEXT I
2220 SUBEND
2230 REM SUBROUTINE ***INPUTPOS***
2240 SUB INPUTPOS(ROW,COL,INPUTROW$,INPUTCOL)
2250 ON WARNING NEXT
2260 REM LABEL ***GETROW***
2270 ACCEPT AT(ROW,COL-2)VALIDATE("ABCDEFGHIJ")BEEP SIZE(1):INPUTROW$
2280 IF INPUTROW$="" THEN  2260 :: REM GOTO GETROW
2290 REM LABEL ***GETCOL***
2300 ACCEPT AT(ROW,COL-2+1)VALIDATE(DIGIT)BEEP SIZE(2):INPUTCOL
2310 IF INPUTCOL>10 THEN  2290 :: REM GOTO GETCOL
2320 IF INPUTCOL<1 THEN  2290 :: REM GOTO GETCOL
2330 ON WARNING PRINT
2340 SUBEND
2350 REM SUBROUTINE ***VALIDATESHIP***
2360 SUB VALIDATESHIP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
2370 SHIPERR=0
2380 HORIZONTAL=0
2390 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
2400 CALL CHECKHORIZONTAL(HORIZONTAL,CURRENTSHIP,SHIP())
2410 DIM SEQUENCE(4)
2420 FOR I=0 TO SHIPLEN-1
2430 COL=SHIP(I)-INT(SHIP(I)/16)*16 :: ROW=INT(SHIP(I)/16)
2440 IF HORIZONTAL=1 THEN SEQUENCE(I)=COL ELSE SEQUENCE(I)=ROW
2450 NEXT I
2460 CALL CHECKSEQUENCE(SHIPERR,CURRENTSHIP,SEQUENCE())
2470 IF SHIPERR=1 THEN 2490 :: REM SUBEND
2480 CALL CHECKOVERLAP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
2490 SUBEND
2500 REM SUBROUTINE ***RENDERSHIP***
2510 SUB RENDERSHIP(CURRENTSHIP,SHIP())
2520 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
2530 CALL GETSHIPCHAR(SHIPCHAR)
2540 CALL GETBOARDORIG(ROW,COL)
2550 FOR I=0 TO SHIPLEN-1
2560 COFFSET=SHIP(I)-INT(SHIP(I)/16)*16 :: ROFFSET=INT(SHIP(I)/16)
2570 CALL HCHAR(ROW+(ROFFSET-1)*2,COL+(COFFSET-1)*2,SHIPCHAR,1)
2580 NEXT I
2590 SUBEND
2600 REM SUBROUTINE ***RENDERSHIPSAUX***
2610 SUB RENDERSHIPSAUX(SHIPS(,,))
2620 CALL GETNUMSHIPS(NUMSHIPS)
2630 CALL GETAUXORIG(ROW,COL)
2640 CALL GETSHIPCHAR(SHIPCHAR)
2650 FOR I=0 TO NUMSHIPS-1
2660 CALL GETSHIPLEN(SHIPLEN,I)
2670 FOR J=0 TO SHIPLEN-1
2680 COFFSET=SHIPS(0,I,J)-INT(SHIPS(0,I,J)/16)*16 :: ROFFSET=INT(SHIPS(0,I,J)/16)
2690 CALL HCHAR(ROW+ROFFSET-1,COL+COFFSET-1,SHIPCHAR,1)
2700 NEXT J
2710 NEXT I
2720 SUBEND
2730 REM SUBROUTINE ***PLAYERDEPLOYED***
2740 SUB PLAYERDEPLOYED
2750 CALL GETMENUORIG(ROW,COL)
2760 CALL CLEARMENU
2770 CALL RENDERTEXT("PLAYER'S",ROW,COL)
2780 CALL RENDERTEXT("SHIPS",ROW+1,COL)
2790 CALL RENDERTEXT("DEPLOYED.",ROW+2,COL)
2800 CALL RENDERTEXT("COMPUTER",ROW+4,COL)
2810 CALL RENDERTEXT("DEPLOYING",ROW+5,COL)
2820 CALL RENDERTEXT("SHIPS...",ROW+6,COL)
2830 SUBEND
2840 REM SUBROUTINE ***CLEARMENU***
2850 SUB CLEARMENU
2860 CALL GETMENUORIG(ROW,COL)
2870 FOR I=0 TO 9 :: CALL HCHAR(ROW+I,COL,32,10) :: NEXT I
2880 SUBEND
2890 REM SUBROUTINE ***CHECKOVERLAP***
2900 SUB CHECKOVERLAP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
2910 SHIPERR=0
2920 CALL GETSHIPLEN(CURRENTSHIPLEN,CURRENTSHIP)
2930 FOR I=0 TO CURRENTSHIP-1
2940 CALL GETSHIPLEN(SHIPLEN,I)
2950 FOR J=0 TO SHIPLEN-1
2960 STOREDLOC=SHIPS(PLAYER,I,J)
2970 FOR K=0 TO CURRENTSHIPLEN-1
2980 LOC=SHIP(K)
2990 IF LOC=STOREDLOC THEN SHIPERR=1 :: GOTO 3030 :: REM SUBEND
3000 NEXT K
3010 NEXT J
3020 NEXT I
3030 SUBEND
3040 REM SUBROUTINE ***CHECKHORIZONTAL***
3050 SUB CHECKHORIZONTAL(HORIZONTAL,CURRENTSHIP,SHIP())
3060 HORIZONTAL=0
3070 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
3080 FOR I=1 TO SHIPLEN-1
3090 PREV=INT(SHIP(I-1)/16) :: CURR=INT(SHIP(I)/16)
3100 IF PREV<>CURR THEN 3130 :: REM SUBEND
3110 NEXT I
3120 HORIZONTAL=1
3130 SUBEND
3140 REM SUBROUTINE ***CHECKSEQUENCE***
3150 SUB CHECKSEQUENCE(SHIPERR,CURRENTSHIP,SEQUENCE())
3160 SHIPERR=0
3170 CALL GETSHIPLEN(SHIPLEN,CURRENTSHIP)
3180 MINCOORD=SEQUENCE(0)
3190 FOR I=1 TO SHIPLEN-1
3200 MINCOORD=MIN(MINCOORD,SEQUENCE(I))
3210 NEXT I
3220 EXPSUM=MINCOORD
3230 FOR I=1 TO SHIPLEN-1
3240 EXPSUM=EXPSUM+MINCOORD+I
3250 NEXT I
3260 ACTSUM=SEQUENCE(0)
3270 FOR I=1 TO SHIPLEN-1
3280 ACTSUM=ACTSUM+SEQUENCE(I)
3290 NEXT I
3300 IF EXPSUM<>ACTSUM THEN SHIPERR=1
3310 SUBEND
3320 REM SUBROUTINE ***RENDERTEXT***
3330 SUB RENDERTEXT(TEXT$,ROW,COL)
3340 STRLEN=LEN(TEXT$)
3350 FOR I=0 TO STRLEN-1
3360 C$=SEG$(TEXT$,I+1,1)
3370 CALL HCHAR(ROW,COL+I,ASC(C$))
3380 NEXT I
3390 SUBEND
3400 REM SUBROUTINE ***GETNUMSHIPS***
3410 SUB GETNUMSHIPS(NUMSHIPS)
3420 NUMSHIPS=5
3430 SUBEND
3440 REM SUBROUTINE ***GETSHIPLEN***
3450 SUB GETSHIPLEN(SHIPLEN,INDEX)
3460 SHIPLENS(0)=5 :: SHIPLENS(1)=4 :: SHIPLENS(2)=3 :: SHIPLENS(3)=3 :: SHIPLENS(4)=2 
3470 SHIPLEN=SHIPLENS(INDEX)
3480 SUBEND
3490 REM SUBROUTINE ***GETSHIPNAME***
3500 SUB GETSHIPNAME(SHIPNAME$,INDEX)
3510 SHIPNAMES$(0)="CARRIER" :: SHIPNAMES$(1)="BATTLESHIP" :: SHIPNAMES$(2)="CRUISER" :: SHIPNAMES$(3)="SUBMARINE" :: SHIPNAMES$(4)="DESTROYER"
3520 SHIPNAME$=SHIPNAMES$(INDEX)
3530 SUBEND
3540 REM SUBROUTINE ***GETBOARDORIG***
3550 SUB GETBOARDORIG(ROW,COL)
3560 ROW=5 :: COL=3
3570 SUBEND
3580 REM SUBROUTINE ***GETAUXORIG***
3590 SUB GETAUXORIG(ROW,COL)
3600 ROW=3 :: COL=23
3610 SUBEND
3620 REM SUBROUTINE ***GETMENUORIG***
3630 SUB GETMENUORIG(ROW,COL)
3640 ROW=14 :: COL=23
3650 SUBEND
3660 REM SUBROUTINE ***GETHOLECHAR***
3670 SUB GETHOLECHAR(CHARVAL)
3680 CHARVAL=96
3690 SUBEND
3700 REM SUBROUTINE ***GETFILLCHAR***
3710 SUB GETFILLCHAR(CHARVAL)
3720 CHARVAL=97
3730 SUBEND
3740 REM SUBROUTINE ***GETHITCHAR***
3750 SUB GETHITCHAR(CHARVAL)
3760 CHARVAL=104
3770 SUBEND
3780 REM SUBROUTINE ***GETSHIPCHAR***
3790 SUB GETSHIPCHAR(CHARVAL)
3800 CHARVAL=112
3810 SUBEND
3820 REM SUBROUTINE ***GETTENCHAR***
3830 SUB GETTENCHAR(CHARVAL)
3840 CHARVAL=120
3850 SUBEND
3860 REM SUBROUTINE ***GETMISSCHAR***
3870 SUB GETMISSCHAR(CHARVAL)
3880 CHARVAL=128
3890 SUBEND
3900 REM SUBROUTINE ***GETSUNKCHAR***
3910 SUB GETSUNKCHAR(CHARVAL)
3920 CHARVAL=136
3930 SUBEND
3940 REM SUBROUTINE ***GETDEBUGFLAG***
3950 SUB GETDEBUGFLAG(DEBUG)
3960 DEBUG=1
3970 SUBEND