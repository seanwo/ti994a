100 CALL CLEAR
110 CALL SCREEN(4)
120 RANDOMIZE
130 CALL SETCHARS
140 CALL SETCOLORSCHEME
150 CALL RENDERBOARDS
160 DIM SHIPS(1,4,4) :: REM SHIPS[PLAYER,SHIP,LOC]
170 CALL DEPLOYSHIPS(SHIPS(,,))
180 CALL PLAYGAME(SHIPS(,,))
190 FOR I=1 TO 1000 :: NEXT I 
200 END
210 REM SUBROUTINE ***SETCHARS***
220 SUB SETCHARS
230 CALL GETHOLECHAR(HOLECHAR)
240 CALL CHAR(HOLECHAR,"3C4299A5A599423C")
250 CALL GETFILLCHAR(FILLCHAR)
260 CALL CHAR(FILLCHAR,"0000000000000000")
270 CALL GETHITCHAR(HITCHAR)
280 CALL CHAR(HITCHAR,"3C7EFFFFFFFF7E3C")
290 CALL GETSHIPCHAR(SHIPCHAR)
300 CALL CHAR(SHIPCHAR,"3C4299A5A599423C")
310 CALL GETTENCHAR(TENCHAR)
320 CALL CHAR(TENCHAR,"004FC949494949EF")
330 CALL GETMISSCHAR(MISSCHAR)
340 CALL CHAR(MISSCHAR,"3C7EFFFFFFFF7E3C")
350 CALL GETSUNKCHAR(SUNKCHAR)
360 CALL CHAR(SUNKCHAR,"3C7EFFFFFFFF7E3C") 
370 SUBEND
380 REM SUBROUTINE ***SETCOLORSCHEME***
390 SUB SETCOLORSCHEME
400 CALL COLOR(9,2,8) :: REM BLACK ON CYAN
410 CALL COLOR(10,11,8) :: REM DARK YELLOW ON CYAN
420 CALL COLOR(11,2,15) :: REM BLACK ON GREY
430 CALL COLOR(12,2,1) :: REM BLACK ON TRANSPARENT
440 CALL COLOR(13,16,8) :: REM WHITE ON CYAN
450 CALL COLOR(14,9,8) :: REM MED RED ON CYAN
460 SUBEND
470 REM SUBROUTINE ***RENDERBOARDS***
480 SUB RENDERBOARDS
490 CALL CLEAR
500 DISPLAY AT(1,9):"BATTLESHIP"
510 CALL RENDERBOARD
520 CALL RENDERAUX
530 SUBEND
540 REM SUBROUTINE ***DEPLOYSHIPS***
550 SUB DEPLOYSHIPS(SHIPS(,,))
560 CALL PLAYERDELOY(SHIPS(,,))
570 CALL COMPUTERDEPLOY(0,SHIPS(,,))
580 SUBEND
590 REM SUBROUTINE ***PLAYGAME***
600 SUB PLAYGAME(SHIPS(,,))
610 CALL CLEARMENU
620 CALL TARGETMENU
630 DIM SHOTS(1,9,9) :: REM SHOTS[PLAYER, ROW,COL]
640 DIM HITS(1,4) :: REM HITS[PLAYER,SHIP]
650 WINNER=0
660 REM LABEL ***FIRELOOP***
670 CALL INPUTTARGET(ROW$,COL)
680 ROW=ASC(ROW$)-64
690 LOC=16*ROW+COL
700 PLAYER=0 :: ERRORVAL=0
710 CALL VALIDSHOT(ERRORVAL,(ROW),(COL),(PLAYER),SHOTS(,,))
720 IF ERRORVAL=1 THEN 660 :: REM GOTO FIRELOOP
730 SHOTS(PLAYER,ROW-1,COL-1)=1
740 PLAYER=1 :: HIT=0 :: SHIP=0
750 CALL CHECKHIT(HIT,SHIP,(LOC),(PLAYER),SHIPS(,,))
760 PLAYER=0
770 CALL RENDERSHOT((PLAYER),(HIT),(ROW),(COL))
780 IF HIT=0 THEN 820 :: REM GOTO ENDTURN
790 PLAYER=1
800 HITS(PLAYER,SHIP)=HITS(PLAYER,SHIP)+1
810 REM ***DETECT SUNK SHIP***
820 REM ***TBD***
830 GOTO 660 :: REM GOTO FIRELOOP
840 SUBEND
850 REM SUBROUTINE ***RENDERSHOT***
860 SUB RENDERSHOT(PLAYER,HIT,SROW,SCOL)
870 CALL GETMENUORIG(ROW,COL)
880 IF PLAYER=0 THEN ROW=ROW+4 ELSE ROW=ROW+9
890 IF HIT=0 THEN TEXT$="(MISS)" :: NOISE=-3 ELSE TEXT$="(HIT)" :: NOISE=-8
900 CALL HCHAR(ROW,COL,32,10)
910 CALL RENDERTEXT((TEXT$),(ROW),(COL))
920 IF HIT=1 THEN CALL GETHITCHAR(CHARVAL) ELSE CALL GETMISSCHAR(CHARVAL)
930 IF PLAYER=1 THEN CALL GETAUXORIG(ROW,COL) ELSE CALL GETBOARDORIG(ROW,COL)
940 TROW=SROW-1 :: TCOL=SCOL-1
950 IF PLAYER=0 THEN TROW=TROW*2 :: TCOL=TCOL*2
960 CALL HCHAR(ROW+TROW,COL+TCOL,CHARVAL,1)
970 CALL SOUND(500,NOISE,2)
980 SUBEND
990 REM SUBROUTINE ***VALIDSHOT***
1000 SUB VALIDSHOT(ERRORVAL,ROW,COL,PLAYER,SHOTS(,,))
1010 ERRORVAL=0
1020 IF SHOTS(PLAYER,ROW-1,COL-1)<>0 THEN ERRORVAL=1
1030 SUBEND
1040 REM SUBROUTINE ***CHECKHIT***
1050 SUB CHECKHIT(HIT,SHIP,LOC,PLAYER,SHIPS(,,))
1060 HIT=0
1070 CALL GETNUMSHIPS(NUMSHIPS)
1080 FOR I=0 TO NUMSHIPS-1
1090 CALL GETSHIPLEN(SHIPLEN,(I))
1100 FOR J=0 TO SHIPLEN-1
1110 IF SHIPS(PLAYER,I,J)=LOC THEN HIT=1 :: SHIP=I :: GOTO 1140 :: REM SUBEND
1120 NEXT J
1130 NEXT I
1140 SUBEND
1150 REM SUBROUTINE ***INPUTTARGET***
1160 SUB INPUTTARGET(TARGETROW$,TARGETCOL)
1170 CALL GETMENUORIG(ROW,COL)
1180 CALL HCHAR(ROW+3,COL,32,10)
1190 CALL INPUTPOS((ROW+3),(COL),TARGETROW$,TARGETCOL)
1200 SUBEND
1210 REM SUBROUTINE ***TARGETMENU***
1220 SUB TARGETMENU
1230 CALL GETMENUORIG(ROW,COL)
1240 CALL RENDERTEXT("PLAYER",(ROW),(COL))
1250 CALL RENDERTEXT("TARGET",(ROW+1),(COL))
1260 CALL RENDERTEXT("[IE. C3]",(ROW+2),(COL))
1270 CALL RENDERTEXT("COMPUTER",(ROW+6),(COL))
1280 CALL RENDERTEXT("TARGET",(ROW+7),(COL))
1290 SUBEND
1300 REM SUBROUTINE ***RENDERBOARD***
1310 SUB RENDERBOARD
1320 CALL RENDERBACKBOARD
1330 CALL RENDERAXES
1340 CALL RENDERHOLES
1350 SUBEND
1360 REM SUBROUTINE ***RENDERBACKBOARD***
1370 SUB RENDERBACKBOARD
1380 CALL GETBOARDORIG(ROW,COL)
1390 CALL GETFILLCHAR(FILLCHAR)
1400 FOR I=0 TO 18
1410 CALL HCHAR(ROW+I,COL,FILLCHAR,19)
1420 NEXT I
1430 SUBEND
1440 REM SUBROUTINE ***RENDERHOLES***
1450 SUB RENDERHOLES
1460 CALL GETBOARDORIG(ROW,COL)
1470 CALL GETHOLECHAR(HOLECHAR)
1480 FOR I=0 TO 18 STEP 2
1490 FOR J=0 TO 18 STEP 2
1500 CALL HCHAR(ROW+I,COL+J,HOLECHAR,1)
1510 NEXT J
1520 NEXT I
1530 SUBEND
1540 REM SUBROUTINE ***RENDERAXES***
1550 SUB RENDERAXES
1560 CALL GETBOARDORIG(ROW,COL)
1570 CALL GETTENCHAR(TENCHAR)
1580 REM RENDER X-AXIS (NUMBERS)
1590 FOR I=0 TO 8
1600 CALL HCHAR(ROW-2,I*2+COL,49+I,1)
1610 NEXT I
1620 CALL HCHAR(ROW-2,COL+18,TENCHAR,1)
1630 REM RENDER Y-AXIS (LETTERS)
1640 FOR I=1 TO 10
1650 CALL HCHAR(ROW-2+I*2,COL-2,64+I,1)
1660 NEXT I
1670 SUBEND
1680 REM SUBROUTINE ***RENDERAUX***
1690 SUB RENDERAUX
1700 CALL GETAUXORIG(ROW,COL)
1710 CALL GETHOLECHAR(HOLECHAR)
1720 FOR I=0 TO 9
1730 CALL HCHAR(ROW+I,COL,HOLECHAR,10)
1740 NEXT I
1750 SUBEND
1760 REM SUBROUTINE ***PLAYERDELOY***
1770 SUB PLAYERDELOY(SHIPS(,,))
1780 CALL GETNUMSHIPS(NUMSHIPS)
1790 CALL GETMENUORIG(ROW,COL)
1800 CALL CLEARMENU
1810 CALL RENDERTEXT("AUTO",(ROW),(COL))
1820 CALL RENDERTEXT("DEPLOY",(ROW+1),(COL))
1830 CALL RENDERTEXT("YOUR",(ROW+2),(COL))
1840 CALL RENDERTEXT("SHIPS?",(ROW+3),(COL))
1850 CALL RENDERTEXT("[Y/N]:",(ROW+4),(COL))
1860 ACCEPT AT(ROW+4,COL+4)VALIDATE("YN")BEEP SIZE(1):AUTODEPLOY$
1870 IF AUTODEPLOY$="N" THEN 1990 :: REM GOTO PLAYERDEPLOYSHIPS
1880 CALL RENDERTEXT("AUTO",(ROW+6),(COL))
1890 CALL RENDERTEXT("DEPLOYING",(ROW+7),(COL))
1900 CALL RENDERTEXT("SHIPS...",(ROW+8),(COL))
1910 CALL COMPUTERDEPLOY(1,SHIPS(,,))
1920 FOR I=0 TO NUMSHIPS-1
1930 CALL GETSHIPLEN(SHIPLEN,(I))
1940 FOR J=0 TO SHIPLEN-1
1950 SHIPS(0,I,J)=SHIPS(1,I,J)
1960 NEXT J
1970 NEXT I
1980 GOTO 2160 :: REM GOTO PLAYERRENDERSHIPS
1990 REM LABEL ***PLAYERDEPLOYSHIPS***
2000 CALL CLEARMENU
2010 DIM SHIP(4)
2020 FOR CURRENTSHIP=0 TO NUMSHIPS-1
2030 REM LABEL ***PLAYERDEPLOYSHIP***
2040 CALL DEPLOYMENU((CURRENTSHIP))
2050 CALL INPUTSHIP(SHIP(),(CURRENTSHIP))
2060 PLAYER=0
2070 SHIPERR=0
2080 CALL VALIDATESHIP(SHIPERR,(PLAYER),(CURRENTSHIP),SHIP(),SHIPS(,,))
2090 IF SHIPERR=1 THEN 2030 :: REM GOTO PLAYERDEPLOYSHIP
2100 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
2110 FOR I=0 TO SHIPLEN-1
2120 SHIPS(0,CURRENTSHIP,I)=SHIP(I)
2130 NEXT I
2140 CALL RENDERSHIP((CURRENTSHIP),SHIP())
2150 NEXT CURRENTSHIP
2160 CALL RENDERSHIPSAUX(SHIPS(,,))
2170 CALL RENDERHOLES
2180 CALL PLAYERDEPLOYED
2190 SUBEND
2200 REM SUBROUTINE ***COMPUTERDEPLOY***
2210 SUB COMPUTERDEPLOY(VISIBLE,SHIPS(,,))
2220 DIM SHIP(4)
2230 CALL GETNUMSHIPS(NUMSHIPS)
2240 CALL GETBOARDORIG(ROW,COL)
2250 CALL GETSHIPCHAR(SHIPCHAR)
2260 FOR CURRENTSHIP=0 TO NUMSHIPS-1
2270 REM LABEL ***COMPUTERDEPLOYSHIP***
2280 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
2290 HORIZONTAL=INT(RND*2)
2300 SHIPBEGIN=INT(RND*(10-SHIPLEN))+1
2310 STATIC=INT(RND*10)+1
2320 FOR I=0 TO SHIPLEN-1
2330 IF HORIZONTAL=1 THEN SHIP(I)=16*STATIC+SHIPBEGIN+I ELSE SHIP(I)=16*(SHIPBEGIN+I)+STATIC
2340 NEXT I
2350 PLAYER=1
2360 SHIPERR=0
2370 CALL CHECKOVERLAP(SHIPERR,(PLAYER),(CURRENTSHIP),SHIP(),SHIPS(,,))
2380 IF SHIPERR=1 THEN 2270 :: REM GOTO COMPUTERDEPLOYSHIP
2390 FOR I=0 TO SHIPLEN-1
2400 SHIPS(1,CURRENTSHIP,I)=SHIP(I)
2410 NEXT I
2420 CALL GETDEBUGFLAG(DEBUG)
2430 IF DEBUG=1 THEN VISIBLE=1
2440 IF VISIBLE=1 THEN CALL RENDERSHIP((CURRENTSHIP),SHIP())
2450 NEXT CURRENTSHIP
2460 SUBEND
2470 REM SUBROUTINE ***DEPLOYMENU***
2480 SUB DEPLOYMENU(CURRENTSHIP)
2490 CALL GETMENUORIG(ROW,COL)
2500 CALL RENDERTEXT("INPUT THE",(ROW),(COL))
2510 CALL RENDERTEXT("LOCATIONS",(ROW+1),(COL))
2520 CALL RENDERTEXT("FOR YOUR",(ROW+2),(COL))
2530 CALL RENDERTEXT("          ",(ROW+3),(COL))
2540 CALL GETSHIPNAME(SHIPNAME$,(CURRENTSHIP))
2550 CALL RENDERTEXT((SHIPNAME$),(ROW+3),(COL))
2560 CALL RENDERTEXT("[IE. C3]:",(ROW+4),(COL))
2570 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
2580 FOR I=1 TO 5
2590 IF I>SHIPLEN THEN TEXT$="        " ELSE TEXT$="POS"&STR$(I)&":    "
2600 CALL RENDERTEXT((TEXT$),(ROW+4+I),(COL))
2610 NEXT I
2620 SUBEND
2630 REM SUBROUTINE ***INPUTSHIP***
2640 SUB INPUTSHIP(SHIP(),CURRENTSHIP)
2650 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
2660 CALL GETMENUORIG(ROW,COL)
2670 FOR I=0 TO SHIPLEN-1
2680 INPUTROW$="" :: INPUTCOL=0
2690 CALL INPUTPOS((ROW+5+I),(COL+5),INPUTROW$,INPUTCOL)
2700 SHIP(I)=16*(ASC(INPUTROW$)-64)+INPUTCOL
2710 NEXT I
2720 SUBEND
2730 REM SUBROUTINE ***INPUTPOS***
2740 SUB INPUTPOS(ROW,COL,INPUTROW$,INPUTCOL)
2750 ON WARNING NEXT
2760 REM LABEL ***GETROW***
2770 ACCEPT AT(ROW,COL-2)VALIDATE("ABCDEFGHIJ")BEEP SIZE(1):INPUTROW$
2780 IF INPUTROW$="" THEN  2760 :: REM GOTO GETROW
2790 REM LABEL ***GETCOL***
2800 ACCEPT AT(ROW,COL-2+1)VALIDATE(DIGIT)BEEP SIZE(2):INPUTCOL
2810 IF INPUTCOL>10 THEN  2790 :: REM GOTO GETCOL
2820 IF INPUTCOL<1 THEN  2790 :: REM GOTO GETCOL
2830 ON WARNING PRINT
2840 SUBEND
2850 REM SUBROUTINE ***VALIDATESHIP***
2860 SUB VALIDATESHIP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
2870 SHIPERR=0
2880 HORIZONTAL=0
2890 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
2900 CALL CHECKHORIZONTAL(HORIZONTAL,(CURRENTSHIP),SHIP())
2910 DIM SEQUENCE(4)
2920 FOR I=0 TO SHIPLEN-1
2930 COL=SHIP(I)-INT(SHIP(I)/16)*16 :: ROW=INT(SHIP(I)/16)
2940 IF HORIZONTAL=1 THEN SEQUENCE(I)=COL ELSE SEQUENCE(I)=ROW
2950 NEXT I
2960 CALL CHECKSEQUENCE(SHIPERR,(CURRENTSHIP),SEQUENCE())
2970 IF SHIPERR=1 THEN 2990 :: REM SUBEND
2980 CALL CHECKOVERLAP(SHIPERR,(PLAYER),(CURRENTSHIP),SHIP(),SHIPS(,,))
2990 SUBEND
3000 REM SUBROUTINE ***RENDERSHIP***
3010 SUB RENDERSHIP(CURRENTSHIP,SHIP())
3020 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
3030 CALL GETSHIPCHAR(SHIPCHAR)
3040 CALL GETBOARDORIG(ROW,COL)
3050 FOR I=0 TO SHIPLEN-1
3060 COFFSET=SHIP(I)-INT(SHIP(I)/16)*16 :: ROFFSET=INT(SHIP(I)/16)
3070 CALL HCHAR(ROW+(ROFFSET-1)*2,COL+(COFFSET-1)*2,SHIPCHAR,1)
3080 NEXT I
3090 SUBEND
3100 REM SUBROUTINE ***RENDERSHIPSAUX***
3110 SUB RENDERSHIPSAUX(SHIPS(,,))
3120 CALL GETNUMSHIPS(NUMSHIPS)
3130 CALL GETAUXORIG(ROW,COL)
3140 CALL GETSHIPCHAR(SHIPCHAR)
3150 FOR I=0 TO NUMSHIPS-1
3160 CALL GETSHIPLEN(SHIPLEN,(I))
3170 FOR J=0 TO SHIPLEN-1
3180 COFFSET=SHIPS(0,I,J)-INT(SHIPS(0,I,J)/16)*16 :: ROFFSET=INT(SHIPS(0,I,J)/16)
3190 CALL HCHAR(ROW+ROFFSET-1,COL+COFFSET-1,SHIPCHAR,1)
3200 NEXT J
3210 NEXT I
3220 SUBEND
3230 REM SUBROUTINE ***PLAYERDEPLOYED***
3240 SUB PLAYERDEPLOYED
3250 CALL GETMENUORIG(ROW,COL)
3260 CALL CLEARMENU
3270 CALL RENDERTEXT("PLAYER'S",(ROW),(COL))
3280 CALL RENDERTEXT("SHIPS",(ROW+1),(COL))
3290 CALL RENDERTEXT("DEPLOYED.",(ROW+2),(COL))
3300 CALL RENDERTEXT("COMPUTER",(ROW+4),(COL))
3310 CALL RENDERTEXT("DEPLOYING",(ROW+5),(COL))
3320 CALL RENDERTEXT("SHIPS...",(ROW+6),(COL))
3330 SUBEND
3340 REM SUBROUTINE ***CLEARMENU***
3350 SUB CLEARMENU
3360 CALL GETMENUORIG(ROW,COL)
3370 FOR I=0 TO 9 :: CALL HCHAR(ROW+I,COL,32,10) :: NEXT I
3380 SUBEND
3390 REM SUBROUTINE ***CHECKOVERLAP***
3400 SUB CHECKOVERLAP(SHIPERR,PLAYER,CURRENTSHIP,SHIP(),SHIPS(,,))
3410 SHIPERR=0
3420 CALL GETSHIPLEN(CURRENTSHIPLEN,(CURRENTSHIP))
3430 FOR I=0 TO CURRENTSHIP-1
3440 CALL GETSHIPLEN(SHIPLEN,(I))
3450 FOR J=0 TO SHIPLEN-1
3460 STOREDLOC=SHIPS(PLAYER,I,J)
3470 FOR K=0 TO CURRENTSHIPLEN-1
3480 LOC=SHIP(K)
3490 IF LOC=STOREDLOC THEN SHIPERR=1 :: GOTO 3530 :: REM SUBEND
3500 NEXT K
3510 NEXT J
3520 NEXT I
3530 SUBEND
3540 REM SUBROUTINE ***CHECKHORIZONTAL***
3550 SUB CHECKHORIZONTAL(HORIZONTAL,CURRENTSHIP,SHIP())
3560 HORIZONTAL=0
3570 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
3580 FOR I=1 TO SHIPLEN-1
3590 PREV=INT(SHIP(I-1)/16) :: CURR=INT(SHIP(I)/16)
3600 IF PREV<>CURR THEN 3630 :: REM SUBEND
3610 NEXT I
3620 HORIZONTAL=1
3630 SUBEND
3640 REM SUBROUTINE ***CHECKSEQUENCE***
3650 SUB CHECKSEQUENCE(SHIPERR,CURRENTSHIP,SEQUENCE())
3660 SHIPERR=0
3670 CALL GETSHIPLEN(SHIPLEN,(CURRENTSHIP))
3680 MINCOORD=SEQUENCE(0)
3690 FOR I=1 TO SHIPLEN-1
3700 MINCOORD=MIN(MINCOORD,SEQUENCE(I))
3710 NEXT I
3720 EXPSUM=MINCOORD
3730 FOR I=1 TO SHIPLEN-1
3740 EXPSUM=EXPSUM+MINCOORD+I
3750 NEXT I
3760 ACTSUM=SEQUENCE(0)
3770 FOR I=1 TO SHIPLEN-1
3780 ACTSUM=ACTSUM+SEQUENCE(I)
3790 NEXT I
3800 IF EXPSUM<>ACTSUM THEN SHIPERR=1
3810 SUBEND
3820 REM SUBROUTINE ***RENDERTEXT***
3830 SUB RENDERTEXT(TEXT$,ROW,COL)
3840 STRLEN=LEN(TEXT$)
3850 FOR I=0 TO STRLEN-1
3860 C$=SEG$(TEXT$,I+1,1)
3870 CALL HCHAR(ROW,COL+I,ASC(C$))
3880 NEXT I
3890 SUBEND
3900 REM SUBROUTINE ***GETNUMSHIPS***
3910 SUB GETNUMSHIPS(NUMSHIPS)
3920 NUMSHIPS=5
3930 SUBEND
3940 REM SUBROUTINE ***GETSHIPLEN***
3950 SUB GETSHIPLEN(SHIPLEN,INDEX)
3960 SHIPLENS(0)=5 :: SHIPLENS(1)=4 :: SHIPLENS(2)=3 :: SHIPLENS(3)=3 :: SHIPLENS(4)=2 
3970 SHIPLEN=SHIPLENS(INDEX)
3980 SUBEND
3990 REM SUBROUTINE ***GETSHIPNAME***
4000 SUB GETSHIPNAME(SHIPNAME$,INDEX)
4010 SHIPNAMES$(0)="CARRIER" :: SHIPNAMES$(1)="BATTLESHIP" :: SHIPNAMES$(2)="CRUISER" :: SHIPNAMES$(3)="SUBMARINE" :: SHIPNAMES$(4)="DESTROYER"
4020 SHIPNAME$=SHIPNAMES$(INDEX)
4030 SUBEND
4040 REM SUBROUTINE ***GETBOARDORIG***
4050 SUB GETBOARDORIG(ROW,COL)
4060 ROW=5 :: COL=3
4070 SUBEND
4080 REM SUBROUTINE ***GETAUXORIG***
4090 SUB GETAUXORIG(ROW,COL)
4100 ROW=3 :: COL=23
4110 SUBEND
4120 REM SUBROUTINE ***GETMENUORIG***
4130 SUB GETMENUORIG(ROW,COL)
4140 ROW=14 :: COL=23
4150 SUBEND
4160 REM SUBROUTINE ***GETHOLECHAR***
4170 SUB GETHOLECHAR(CHARVAL)
4180 CHARVAL=96
4190 SUBEND
4200 REM SUBROUTINE ***GETFILLCHAR***
4210 SUB GETFILLCHAR(CHARVAL)
4220 CHARVAL=97
4230 SUBEND
4240 REM SUBROUTINE ***GETHITCHAR***
4250 SUB GETHITCHAR(CHARVAL)
4260 CHARVAL=104
4270 SUBEND
4280 REM SUBROUTINE ***GETSHIPCHAR***
4290 SUB GETSHIPCHAR(CHARVAL)
4300 CHARVAL=112
4310 SUBEND
4320 REM SUBROUTINE ***GETTENCHAR***
4330 SUB GETTENCHAR(CHARVAL)
4340 CHARVAL=120
4350 SUBEND
4360 REM SUBROUTINE ***GETMISSCHAR***
4370 SUB GETMISSCHAR(CHARVAL)
4380 CHARVAL=128
4390 SUBEND
4400 REM SUBROUTINE ***GETSUNKCHAR***
4410 SUB GETSUNKCHAR(CHARVAL)
4420 CHARVAL=136
4430 SUBEND
4440 REM SUBROUTINE ***GETDEBUGFLAG***
4450 SUB GETDEBUGFLAG(DEBUG)
4460 DEBUG=1
4470 SUBEND