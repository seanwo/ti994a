100 REM  COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
110 CALL TRO
120 CALL SCH
130 CALL SCS
140 CALL PLY
150 DISPLAY AT(22,2)BEEP:"DO YOU WISH TO PLAY AGAIN?"
160 A$=""
170 CALL HCHAR(23,16,30)
180 CALL KEY(0,K,S)
190 IF S=0 THEN 180
200 IF K<>78 AND K<>89 AND K<>13 THEN 180
210 IF K=78 THEN CALL HCHAR(23,16,78) :: A$="N" ELSE IF K=89 THEN CALL HCHAR(23,16,89) :: A$="Y"
220 IF K<>13 THEN 180
230 IF A$="" THEN 180
240 CALL HCHAR(22,4,32,25) :: CALL HCHAR(23,16,32)
250 IF A$="N" THEN CALL TRM
260 GOTO 140
270 END
280 SUB TRO
290 CALL CLEAR
300 DISPLAY AT(12,10):"BLACKJACK"
310 DISPLAY AT(13,1):"presented by Sean Wohlgemuth"
320 FOR I=1 TO 5 :: CALL SOUND(1000,110,30) :: CALL SOUND(1,110,30) :: NEXT I
330 CALL CLEAR
340 DISPLAY AT(1,1):"PREFACE:"
350 DISPLAY AT(3,1):"THIS IS A GAME BETWEEN YOU"
360 DISPLAY AT(4,1):"AND THE DEALER (COMPUTER)."
370 DISPLAY AT(6,1):"OBJECT OF THE GAME:"
380 DISPLAY AT(8,1):"THE OBJECT OF THE GAME IS TO"
390 DISPLAY AT(9,1):"COLLECT A SET OF CARDS LESS"
400 DISPLAY AT(10,1):"THAN OR EQUAL TO 21. IF YOUR"
410 DISPLAY AT(11,1):"SET OF CARDS IS HIGHER THAN"
420 DISPLAY AT(12,1):"THE DEALER'S AND NOT OVER 21"
430 DISPLAY AT(13,1):"THEN YOU WIN."
440 DISPLAY AT(15,1):"CARD VALUES:"
450 DISPLAY AT(17,1):"NUMBER CARDS = FACE VALUE"
460 DISPLAY AT(18,1):"FACE CARDS   = 10 PTS"
470 DISPLAY AT(19,1):"ACES         = 11 OR 1 PT(S)"
480 DISPLAY AT(24,1):"PRESS ENTER TO CONTINUE:"
490 ACCEPT AT(24,28)BEEPVALIDATE(""):A$
500 CALL CLEAR
510 DISPLAY AT(1,1):"HOW TO PLAY:"
520 DISPLAY AT(3,1):"BOTH YOU AND THE DEALER WILL"
530 DISPLAY AT(4,1):"BE GIVEN TWO CARDS TO START."
540 DISPLAY AT(5,1):"TAKE AS MANY CARDS AS YOU"
550 DISPLAY AT(6,1):"WISH (UP TO 5 CARDS). IF"
560 DISPLAY AT(7,1):"THE VALUE OF YOUR HAND"
570 DISPLAY AT(8,1):"EXCEEDS 21 YOU BUST. ONCE"
580 DISPLAY AT(9,1):"YOU STOP TAKING CARDS, THE"
590 DISPLAY AT(10,1):"DEALER WILL TAKE CARDS"
600 DISPLAY AT(11,1):"TRYING NOT TO BUST AS WELL."
610 DISPLAY AT(12,1):"THE PLAYER WITH THE HIGHEST"
620 DISPLAY AT(13,1):"HAND VALUE WINS."
630 DISPLAY AT(24,1):"PRESS ENTER TO BEGIN:"
640 ACCEPT AT(24,28)BEEPVALIDATE(""):A$
650 SUBEND
660 SUB SCH
670 CALL SCR
680 CALL SCB
690 CALL SCE
700 CALL CHAR(136,"FFFFFFFFFFFFFFFF")
710 SUBEND
720 SUB SCR
730 CALL CHAR(96,"00367F7F7F3E1C08")
740 CALL CHAR(97,"00081C3E7F3E1C08")
750 CALL SCV((98))
760 CALL CHAR(111,"AA55AA55AA55AA55")
770 SUBEND
780 SUB SCB
790 CALL CHAR(112,"0010387CFEFED638")
800 CALL CHAR(113,"00103854EE541038")
810 CALL SCV((114))
820 SUBEND
830 SUB SCV(OF)
840 CALL CHAR(OF,"003844040830407C") :: OF=OF+1
850 CALL CHAR(OF,"0038040418040438") :: OF=OF+1
860 CALL CHAR(OF,"002424243C040404") :: OF=OF+1
870 CALL CHAR(OF,"003C20203C04043C") :: OF=OF+1
880 CALL CHAR(OF,"001C202038242418") :: OF=OF+1
890 CALL CHAR(OF,"003C040408081010") :: OF=OF+1
900 CALL CHAR(OF,"003C24243C24243C") :: OF=OF+1
910 CALL CHAR(OF,"001C24241C040404") :: OF=OF+1
920 CALL CHAR(OF,"004C52525252524C") :: OF=OF+1
930 CALL CHAR(OF,"003E080808084830") :: OF=OF+1
940 CALL CHAR(OF,"0018242424241C02") :: OF=OF+1
950 CALL CHAR(OF,"0024242830282424") :: OF=OF+1
960 CALL CHAR(OF,"001824243C242424") :: OF=OF+1
970 SUBEND
980 SUB SCE
990 CALL CHAR(128,"8080808080808080")
1000 CALL CHAR(129,"00000000000000FF")
1010 SUBEND
1020 SUB SCS
1030 CALL COLOR(9,7,16)
1040 CALL COLOR(10,7,16)
1050 CALL COLOR(11,2,16)
1060 CALL COLOR(12,2,16)
1070 CALL COLOR(13,2,1)
1080 CALL COLOR(14,16,16)
1090 CALL SCREEN(3)
1100 SUBEND
1110 SUB RCD(P,CARD,FD,HD(,,))
1120 IF FD=1 THEN FI=111 ELSE FI=136
1130 COL=CARD*6 :: RO=P*10
1140 FOR I=1 TO 5
1150 CALL VCHAR(4+RO,1+COL+I,FI,7)
1160 NEXT I
1170 CALL HCHAR(3+RO,2+COL,129,5)
1180 CALL VCHAR(4+RO,7+COL,128,7)
1190 IF FD=1 THEN 1430
1200 IF HD(P,CARD,1)=1 THEN SU=96
1210 IF HD(P,CARD,1)=2 THEN SU=97
1220 IF HD(P,CARD,1)=3 THEN SU=112
1230 IF HD(P,CARD,1)=4 THEN SU=113
1240 IF HD(P,CARD,1)<3 THEN CC=98
1250 IF HD(P,CARD,1)>2 THEN CC=114
1260 CV=HD(P,CARD,0)
1270 CC=CC+CV-2
1280 CALL HCHAR(4+RO,2+COL,CC,1)
1290 CALL HCHAR(10+RO,6+COL,CC,1)
1300 IF CV=2 THEN CALL R02(RO,COL,SU) :: GOTO 1430
1310 IF CV=3 THEN CALL R03(RO,COL,SU) :: GOTO 1430
1320 IF CV=4 THEN CALL R04(RO,COL,SU) :: GOTO 1430
1330 IF CV=5 THEN CALL R05(RO,COL,SU) :: GOTO 1430
1340 IF CV=6 THEN CALL R06(RO,COL,SU) :: GOTO 1430
1350 IF CV=7 THEN CALL R07(RO,COL,SU) :: GOTO 1430
1360 IF CV=8 THEN CALL R08(RO,COL,SU) :: GOTO 1430
1370 IF CV=9 THEN CALL R09(RO,COL,SU) :: GOTO 1430
1380 IF CV=10 THEN CALL R0A(RO,COL,SU) :: GOTO 1430
1390 IF CV=11 THEN CALL RJK(RO,COL,SU) :: GOTO 1430
1400 IF CV=12 THEN CALL RQN(RO,COL,SU) :: GOTO 1430
1410 IF CV=13 THEN CALL RKG(RO,COL,SU) :: GOTO 1430
1420 IF CV=14 THEN CALL RAC(RO,COL,SU) :: GOTO 1430
1430 SUBEND
1440 SUB R02(RO,COL,SU)
1450 CALL HCHAR(6+RO,4+COL,SU,1)
1460 CALL HCHAR(8+RO,4+COL,SU,1)
1470 SUBEND
1480 SUB R03(RO,COL,SU)
1490 CALL VCHAR(6+RO,4+COL,SU,3)
1500 SUBEND
1510 SUB R04(RO,COL,SU)
1520 CALL HCHAR(6+RO,3+COL,SU,1)
1530 CALL HCHAR(8+RO,3+COL,SU,1)
1540 CALL HCHAR(6+RO,5+COL,SU,1)
1550 CALL HCHAR(8+RO,5+COL,SU,1)
1560 SUBEND
1570 SUB R05(RO,COL,SU)
1580 CALL R04((RO),(COL),(SU))
1590 CALL HCHAR(7+RO,4+COL,SU,1)
1600 SUBEND
1610 SUB R06(RO,COL,SU)
1620 CALL VCHAR(6+RO,3+COL,SU,3)
1630 CALL VCHAR(6+RO,5+COL,SU,3)
1640 SUBEND
1650 SUB R07(RO,COL,SU)
1660 CALL R06((RO),(COL),(SU))
1670 CALL VCHAR(7+RO,4+COL,SU,1)
1680 SUBEND
1690 SUB R08(RO,COL,SU)
1700 CALL HCHAR(5+RO,3+COL,SU,1)
1710 CALL HCHAR(5+RO,5+COL,SU,1)
1720 CALL HCHAR(6+RO,4+COL,SU,1)
1730 CALL HCHAR(7+RO,3+COL,SU,1)
1740 CALL HCHAR(7+RO,5+COL,SU,1)
1750 CALL HCHAR(8+RO,4+COL,SU,1)
1760 CALL HCHAR(9+RO,3+COL,SU,1)
1770 CALL HCHAR(9+RO,5+COL,SU,1)
1780 SUBEND
1790 SUB R09(RO,COL,SU)
1800 CALL VCHAR(5+RO,3+COL,SU,2)
1810 CALL VCHAR(5+RO,5+COL,SU,2)
1820 CALL VCHAR(8+RO,3+COL,SU,2)
1830 CALL VCHAR(8+RO,5+COL,SU,2)
1840 CALL HCHAR(7+RO,4+COL,SU,1)
1850 SUBEND
1860 SUB R0A(RO,COL,SU)
1870 CALL VCHAR(5+RO,3+COL,SU,5)
1880 CALL VCHAR(5+RO,5+COL,SU,5)
1890 SUBEND
1900 SUB RJK(RO,COL,SU)
1910 CALL VCHAR(5+RO,5+COL,SU,5)
1920 CALL HCHAR(9+RO,3+COL,SU,2)
1930 CALL HCHAR(8+RO,3+COL,SU,1)
1940 SUBEND
1950 SUB RQN(RO,COL,SU)
1960 CALL VCHAR(5+RO,3+COL,SU,5)
1970 CALL VCHAR(5+RO,5+COL,SU,5)
1980 CALL HCHAR(5+RO,4+COL,SU,1)
1990 CALL HCHAR(9+RO,4+COL,SU,1)
2000 SUBEND
2010 SUB RKG(RO,COL,SU)
2020 CALL VCHAR(5+RO,3+COL,SU,5)
2030 CALL VCHAR(5+RO,5+COL,SU,2)
2040 CALL VCHAR(8+RO,5+COL,SU,2)
2050 CALL VCHAR(7+RO,4+COL,SU,1)
2060 SUBEND
2070 SUB RAC(RO,COL,SU)
2080 CALL HCHAR(7+RO,4+COL,SU,1)
2090 SUBEND
2100 SUB PLY
2110 DIM HD(1,4,1)
2120 DIM CA(1)
2130 DIM SM(1)
2140 CALL CTB(HD(,,),CA(),SM())
2150 CALL SHF(HD(,,))
2160 P=0 :: FD=0
2170 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2180 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2190 P=1 :: FD=0
2200 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2210 P=1 :: FD=1
2220 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2230 BU=0
2240 FOR J=3 TO 5
2250 DISPLAY AT(22,2)BEEP:"DO YOU WANT ANOTHER CARD?"
2260 A$=""
2270 CALL HCHAR(23,16,30)
2280 CALL KEY(0,K,S)
2290 IF S=0 THEN 2280
2300 IF K<>78 AND K<>89 AND K<>13 THEN 2280
2310 IF K=78 THEN CALL HCHAR(23,16,78) :: A$="N" ELSE IF K=89 THEN CALL HCHAR(23,16,89) :: A$="Y"
2320 IF K<>13 THEN 2280
2330 IF A$="" THEN 2280
2340 CALL HCHAR(22,4,32,25) :: CALL HCHAR(23,16,32)
2350 IF A$="N" THEN 2420
2360 P=0 :: FD=0
2370 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2380 IF SM(P)>21 THEN BU=1
2390 IF BU=1 THEN DISPLAY AT(12,4)BEEP:"YOU BUSTED. YOU LOSE."
2400 IF BU=1 THEN 2420
2410 NEXT J
2420 P=1 :: FD=0 :: CARD=1
2430 CALL RCD((P),(CARD),(FD),HD(,,))
2440 IF BU=1 THEN 2560
2450 BU=0
2460 FOR J=3 TO 5
2470 P=1 :: FD=0
2480 CALL CAS(SC,(P),SM(),CA(),HD(,,))
2490 IF SC>=17 THEN 2550
2500 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2510 IF SM(P)>21 THEN BU=1
2520 IF BU=1 THEN DISPLAY AT(12,3)BEEP:"DEALER BUSTED. YOU WIN."
2530 IF BU=1 THEN 2560
2540 NEXT J
2550 CALL WHW(SM(),CA(),HD(,,))
2560 SUBEND
2570 SUB CTB(HD(,,),CA(),SM())
2580 CALL CLEAR
2590 DISPLAY AT(1,10):"BLACKJACK"
2600 FOR I=0 TO 1 :: SM(I)=0 :: CA(I)=0 :: NEXT I
2610 FOR I=0 TO 1 :: FOR J=0 TO 4 :: FOR K=0 TO 1 :: HD(I,J,K)=0 :: NEXT K :: NEXT J :: NEXT I
2620 SUBEND
2630 SUB SHF(HD(,,))
2640 RANDOMIZE
2650 DIM DK(9) :: FOR I=0 TO 9 :: DK(I)=0 :: NEXT I
2660 FOR H=0 TO 9
2670 DISPLAY AT(12,6):"SHUFFLING THE DECK"
2680 X=INT(RND*4)+1
2690 X=X*16
2700 X=X+INT(RND*13)+2
2710 DU=0
2720 CALL CKD(DU,(X),DK())
2730 IF DU=1 THEN 2670
2740 DK(H)=X
2750 NEXT H
2760 FOR H=0 TO 4
2770 HD(0,H,1)=INT(DK(H)/16)
2780 HD(0,H,0)=DK(H)-HD(0,H,1)*16
2790 NEXT H
2800 FOR H=0 TO 4
2810 HD(1,H,1)=INT(DK(H+5)/16)
2820 HD(1,H,0)=DK(H+5)-HD(1,H,1)*16
2830 NEXT H
2840 DISPLAY AT(12,6):"                  "
2850 SUBEND
2860 SUB CKD(DU,X,DK())
2870 DU=0
2880 FOR I=0 TO 9
2890 IF DK(I)=X THEN DU=1 :: GOTO 2910
2900 NEXT I
2910 SUBEND
2920 SUB DEA(CA(),SM(),P,FD,HD(,,))
2930 CA(P)=CA(P)+1
2940 CARD=CA(P)-1
2950 CL=HD(P,CARD,0)
2960 IF CL>10 THEN IF CL=14 THEN CL=1 ELSE CL=10
2970 SM(P)=SM(P)+CL
2980 CALL RCD((P),(CARD),(FD),HD(,,))
2990 SUBEND
3000 SUB CAS(SC,P,SM(),CA(),HD(,,))
3010 SC=SM(P)
3020 IF SC+10>21 THEN 3060
3030 FOR I=0 TO CA(P)-1
3040 IF HD(P,I,0)=14 THEN SC=SC+10 :: GOTO 3060
3050 NEXT I
3060 SUBEND
3070 SUB WHW(SM(),CA(),HD(,,))
3080 P=0 :: PS=0
3090 CALL CAS(PS,(P),SM(),CA(),HD(,,))
3100 P=1 :: DS=0
3110 CALL CAS(DS,(P),SM(),CA(),HD(,,))
3120 IF PS=DS THEN DISPLAY AT(12,11):"TIE GAME"
3130 IF PS>DS THEN DISPLAY AT(12,11):"YOU WIN!"
3140 IF PS<DS THEN DISPLAY AT(12,11):"YOU LOSE"
3150 SUBEND
3160 SUB TRM
3170 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT(12,7)BEEP:"GAME TERMINATED" :: CALL SOUND(100,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3180 FOR I=1 TO 5 :: CALL SOUND(500,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3190 END
3200 SUBEND
