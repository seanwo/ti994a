100 REM  COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
110 CALL TRO
120 CALL SCH
130 CALL SCS
140 CALL PLY
150 DISPLAY AT(22,2)BEEP:"DO YOU WISH TO PLAY AGAIN?"
160 ACCEPT AT(23,14)SIZE(1)BEEPVALIDATE("YN"):A$
170 IF A$="" THEN 150
180 IF A$="N" THEN CALL TRM
190 GOTO 140
200 END
210 SUB TRO
220 CALL CLEAR
230 DISPLAY AT(12,10):"BLACKJACK"
240 DISPLAY AT(13,1):"presented by Sean Wohlgemuth"
250 FOR I=1 TO 5 :: CALL SOUND(1000,110,30) :: CALL SOUND(1,110,30) :: NEXT I
260 CALL CLEAR
270 DISPLAY AT(1,1):"PREFACE:"
280 DISPLAY AT(3,1):"THIS IS A GAME BETWEEN YOU"
290 DISPLAY AT(4,1):"AND THE DEALER (COMPUTER)."
300 DISPLAY AT(6,1):"OBJECT OF THE GAME:"
310 DISPLAY AT(8,1):"THE OBJECT OF THE GAME IS TO"
320 DISPLAY AT(9,1):"COLLECT A SET OF CARDS LESS"
330 DISPLAY AT(10,1):"THAN OR EQUAL TO 21. IF YOUR"
340 DISPLAY AT(11,1):"SET OF CARDS IS HIGHER THAN"
350 DISPLAY AT(12,1):"THE DEALER'S AND NOT OVER 21"
360 DISPLAY AT(13,1):"THEN YOU WIN."
370 DISPLAY AT(15,1):"CARD VALUES:"
380 DISPLAY AT(17,1):"NUMBER CARDS = FACE VALUE"
390 DISPLAY AT(18,1):"FACE CARDS   = 10 PTS"
400 DISPLAY AT(19,1):"ACES         = 11 OR 1 PT(S)"
410 DISPLAY AT(24,1):"PRESS ENTER TO CONTINUE:"
420 ACCEPT AT(24,28)BEEPVALIDATE(""):A$
430 CALL CLEAR
440 DISPLAY AT(1,1):"HOW TO PLAY:"
450 DISPLAY AT(3,1):"BOTH YOU AND THE DEALER WILL"
460 DISPLAY AT(4,1):"BE GIVEN TWO CARDS TO START."
470 DISPLAY AT(5,1):"TAKE AS MANY CARDS AS YOU"
480 DISPLAY AT(6,1):"WISH (UP TO 5 CARDS). IF"
490 DISPLAY AT(7,1):"THE VALUE OF YOUR HAND"
500 DISPLAY AT(8,1):"EXCEEDS 21 YOU BUST. ONCE"
510 DISPLAY AT(9,1):"YOU STOP TAKING CARDS, THE"
520 DISPLAY AT(10,1):"DEALER WILL TAKE CARDS"
530 DISPLAY AT(11,1):"TRYING NOT TO BUST AS WELL."
540 DISPLAY AT(12,1):"THE PLAYER WITH THE HIGHEST"
550 DISPLAY AT(13,1):"HAND VALUE WINS."
560 DISPLAY AT(24,1):"PRESS ENTER TO BEGIN:"
570 ACCEPT AT(24,28)BEEPVALIDATE(""):A$
580 SUBEND
590 SUB SCH
600 CALL SCR
610 CALL SCB
620 CALL SCE
630 CALL CHAR(136,"FFFFFFFFFFFFFFFF")
640 SUBEND
650 SUB SCR
660 CALL CHAR(96,"00367F7F7F3E1C08")
670 CALL CHAR(97,"00081C3E7F3E1C08")
680 CALL SCV((98))
690 CALL CHAR(111,"AA55AA55AA55AA55")
700 SUBEND
710 SUB SCB
720 CALL CHAR(112,"0010387CFEFED638")
730 CALL CHAR(113,"00103854EE541038")
740 CALL SCV((114))
750 SUBEND
760 SUB SCV(OF)
770 CALL CHAR(OF,"003844040830407C") :: OF=OF+1
780 CALL CHAR(OF,"0038040418040438") :: OF=OF+1
790 CALL CHAR(OF,"002424243C040404") :: OF=OF+1
800 CALL CHAR(OF,"003C20203C04043C") :: OF=OF+1
810 CALL CHAR(OF,"001C202038242418") :: OF=OF+1
820 CALL CHAR(OF,"003C040408081010") :: OF=OF+1
830 CALL CHAR(OF,"003C24243C24243C") :: OF=OF+1
840 CALL CHAR(OF,"001C24241C040404") :: OF=OF+1
850 CALL CHAR(OF,"004C52525252524C") :: OF=OF+1
860 CALL CHAR(OF,"003E080808084830") :: OF=OF+1
870 CALL CHAR(OF,"0018242424241C02") :: OF=OF+1
880 CALL CHAR(OF,"0024242830282424") :: OF=OF+1
890 CALL CHAR(OF,"001824243C242424") :: OF=OF+1
900 SUBEND
910 SUB SCE
920 CALL CHAR(128,"8080808080808080")
930 CALL CHAR(129,"00000000000000FF")
940 SUBEND
950 SUB SCS
960 CALL COLOR(9,7,16)
970 CALL COLOR(10,7,16)
980 CALL COLOR(11,2,16)
990 CALL COLOR(12,2,16)
1000 CALL COLOR(13,2,1)
1010 CALL COLOR(14,16,16)
1020 CALL SCREEN(3)
1030 SUBEND
1040 SUB RCD(P,CARD,FD,HD(,,))
1050 IF FD=1 THEN FI=111 ELSE FI=136
1060 COL=CARD*6 :: RO=P*10
1070 FOR I=1 TO 5
1080 CALL VCHAR(4+RO,1+COL+I,FI,7)
1090 NEXT I
1100 CALL HCHAR(3+RO,2+COL,129,5)
1110 CALL VCHAR(4+RO,7+COL,128,7)
1120 IF FD=1 THEN 1360
1130 IF HD(P,CARD,1)=1 THEN SU=96
1140 IF HD(P,CARD,1)=2 THEN SU=97
1150 IF HD(P,CARD,1)=3 THEN SU=112
1160 IF HD(P,CARD,1)=4 THEN SU=113
1170 IF HD(P,CARD,1)<3 THEN CC=98
1180 IF HD(P,CARD,1)>2 THEN CC=114
1190 CV=HD(P,CARD,0)
1200 CC=CC+CV-2
1210 CALL HCHAR(4+RO,2+COL,CC,1)
1220 CALL HCHAR(10+RO,6+COL,CC,1)
1230 IF CV=2 THEN CALL R02(RO,COL,SU) :: GOTO 1360
1240 IF CV=3 THEN CALL R03(RO,COL,SU) :: GOTO 1360
1250 IF CV=4 THEN CALL R04(RO,COL,SU) :: GOTO 1360
1260 IF CV=5 THEN CALL R05(RO,COL,SU) :: GOTO 1360
1270 IF CV=6 THEN CALL R06(RO,COL,SU) :: GOTO 1360
1280 IF CV=7 THEN CALL R07(RO,COL,SU) :: GOTO 1360
1290 IF CV=8 THEN CALL R08(RO,COL,SU) :: GOTO 1360
1300 IF CV=9 THEN CALL R09(RO,COL,SU) :: GOTO 1360
1310 IF CV=10 THEN CALL R0A(RO,COL,SU) :: GOTO 1360
1320 IF CV=11 THEN CALL RJK(RO,COL,SU) :: GOTO 1360
1330 IF CV=12 THEN CALL RQN(RO,COL,SU) :: GOTO 1360
1340 IF CV=13 THEN CALL RKG(RO,COL,SU) :: GOTO 1360
1350 IF CV=14 THEN CALL RAC(RO,COL,SU) :: GOTO 1360
1360 SUBEND
1370 SUB R02(RO,COL,SU)
1380 CALL HCHAR(6+RO,4+COL,SU,1)
1390 CALL HCHAR(8+RO,4+COL,SU,1)
1400 SUBEND
1410 SUB R03(RO,COL,SU)
1420 CALL VCHAR(6+RO,4+COL,SU,3)
1430 SUBEND
1440 SUB R04(RO,COL,SU)
1450 CALL HCHAR(6+RO,3+COL,SU,1)
1460 CALL HCHAR(8+RO,3+COL,SU,1)
1470 CALL HCHAR(6+RO,5+COL,SU,1)
1480 CALL HCHAR(8+RO,5+COL,SU,1)
1490 SUBEND
1500 SUB R05(RO,COL,SU)
1510 CALL R04((RO),(COL),(SU))
1520 CALL HCHAR(7+RO,4+COL,SU,1)
1530 SUBEND
1540 SUB R06(RO,COL,SU)
1550 CALL VCHAR(6+RO,3+COL,SU,3)
1560 CALL VCHAR(6+RO,5+COL,SU,3)
1570 SUBEND
1580 SUB R07(RO,COL,SU)
1590 CALL R06((RO),(COL),(SU))
1600 CALL VCHAR(7+RO,4+COL,SU,1)
1610 SUBEND
1620 SUB R08(RO,COL,SU)
1630 CALL HCHAR(5+RO,3+COL,SU,1)
1640 CALL HCHAR(5+RO,5+COL,SU,1)
1650 CALL HCHAR(6+RO,4+COL,SU,1)
1660 CALL HCHAR(7+RO,3+COL,SU,1)
1670 CALL HCHAR(7+RO,5+COL,SU,1)
1680 CALL HCHAR(8+RO,4+COL,SU,1)
1690 CALL HCHAR(9+RO,3+COL,SU,1)
1700 CALL HCHAR(9+RO,5+COL,SU,1)
1710 SUBEND
1720 SUB R09(RO,COL,SU)
1730 CALL VCHAR(5+RO,3+COL,SU,2)
1740 CALL VCHAR(5+RO,5+COL,SU,2)
1750 CALL VCHAR(8+RO,3+COL,SU,2)
1760 CALL VCHAR(8+RO,5+COL,SU,2)
1770 CALL HCHAR(7+RO,4+COL,SU,1)
1780 SUBEND
1790 SUB R0A(RO,COL,SU)
1800 CALL VCHAR(5+RO,3+COL,SU,5)
1810 CALL VCHAR(5+RO,5+COL,SU,5)
1820 SUBEND
1830 SUB RJK(RO,COL,SU)
1840 CALL VCHAR(5+RO,5+COL,SU,5)
1850 CALL HCHAR(9+RO,3+COL,SU,2)
1860 CALL HCHAR(8+RO,3+COL,SU,1)
1870 SUBEND
1880 SUB RQN(RO,COL,SU)
1890 CALL VCHAR(5+RO,3+COL,SU,5)
1900 CALL VCHAR(5+RO,5+COL,SU,5)
1910 CALL HCHAR(5+RO,4+COL,SU,1)
1920 CALL HCHAR(9+RO,4+COL,SU,1)
1930 SUBEND
1940 SUB RKG(RO,COL,SU)
1950 CALL VCHAR(5+RO,3+COL,SU,5)
1960 CALL VCHAR(5+RO,5+COL,SU,2)
1970 CALL VCHAR(8+RO,5+COL,SU,2)
1980 CALL VCHAR(7+RO,4+COL,SU,1)
1990 SUBEND
2000 SUB RAC(RO,COL,SU)
2010 CALL HCHAR(7+RO,4+COL,SU,1)
2020 SUBEND
2030 SUB PLY
2040 DIM HD(1,4,1)
2050 DIM CA(1)
2060 DIM SM(1)
2070 CALL CTB(HD(,,),CA(),SM())
2080 CALL SHF(HD(,,))
2090 P=0 :: FD=0
2100 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2110 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2120 P=1 :: FD=0
2130 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2140 P=1 :: FD=1
2150 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2160 BU=0
2170 FOR J=3 TO 5
2180 DISPLAY AT(22,2):"DO YOU WANT ANOTHER CARD?"
2190 ACCEPT AT(23,14)VALIDATE("YN")BEEPSIZE(1):A$
2200 IF A$="" THEN 2190
2210 DISPLAY AT(22,2):"                         " :: DISPLAY AT(23,14):" "
2220 IF A$="N" THEN 2290
2230 P=0 :: FD=0
2240 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2250 IF SM(P)>21 THEN BU=1
2260 IF BU=1 THEN DISPLAY AT(12,4)BEEP:"YOU BUSTED. YOU LOSE."
2270 IF BU=1 THEN 2290
2280 NEXT J
2290 P=1 :: FD=0 :: CARD=1
2300 CALL RCD((P),(CARD),(FD),HD(,,))
2310 IF BU=1 THEN 2430
2320 BU=0
2330 FOR J=3 TO 5
2340 P=1 :: FD=0
2350 CALL CAS(SC,(P),SM(),CA(),HD(,,))
2360 IF SC>=17 THEN 2420
2370 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2380 IF SM(P)>21 THEN BU=1
2390 IF BU=1 THEN DISPLAY AT(12,3)BEEP:"DEALER BUSTED. YOU WIN."
2400 IF BU=1 THEN 2430
2410 NEXT J
2420 CALL WHW(SM(),CA(),HD(,,))
2430 SUBEND
2440 SUB CTB(HD(,,),CA(),SM())
2450 CALL CLEAR
2460 DISPLAY AT(1,10):"BLACKJACK"
2470 FOR I=0 TO 1 :: SM(I)=0 :: CA(I)=0 :: NEXT I
2480 FOR I=0 TO 1 :: FOR J=0 TO 4 :: FOR K=0 TO 1 :: HD(I,J,K)=0 :: NEXT K :: NEXT J :: NEXT I
2490 SUBEND
2500 SUB SHF(HD(,,))
2510 RANDOMIZE
2520 DIM DK(9) :: FOR I=0 TO 9 :: DK(I)=0 :: NEXT I
2530 FOR H=0 TO 9
2540 DISPLAY AT(12,6):"SHUFFLING THE DECK"
2550 X=INT(RND*4)+1
2560 X=X*16
2570 X=X+INT(RND*13)+2
2580 DU=0
2590 CALL CKD(DU,(X),DK())
2600 IF DU=1 THEN 2540
2610 DK(H)=X
2620 NEXT H
2630 FOR H=0 TO 4
2640 HD(0,H,1)=INT(DK(H)/16)
2650 HD(0,H,0)=DK(H)-HD(0,H,1)*16
2660 NEXT H
2670 FOR H=0 TO 4
2680 HD(1,H,1)=INT(DK(H+5)/16)
2690 HD(1,H,0)=DK(H+5)-HD(1,H,1)*16
2700 NEXT H
2710 DISPLAY AT(12,6):"                  "
2720 SUBEND
2730 SUB CKD(DU,X,DK())
2740 DU=0
2750 FOR I=0 TO 9
2760 IF DK(I)=X THEN DU=1 :: GOTO 2780
2770 NEXT I
2780 SUBEND
2790 SUB DEA(CA(),SM(),P,FD,HD(,,))
2800 CA(P)=CA(P)+1
2810 CARD=CA(P)-1
2820 CL=HD(P,CARD,0)
2830 IF CL>10 THEN IF CL=14 THEN CL=1 ELSE CL=10
2840 SM(P)=SM(P)+CL
2850 CALL RCD((P),(CARD),(FD),HD(,,))
2860 SUBEND
2870 SUB CAS(SC,P,SM(),CA(),HD(,,))
2880 SC=SM(P)
2890 IF SC+10>21 THEN 2930
2900 FOR I=0 TO CA(P)-1
2910 IF HD(P,I,0)=14 THEN SC=SC+10 :: GOTO 2930
2920 NEXT I
2930 SUBEND
2940 SUB WHW(SM(),CA(),HD(,,))
2950 P=0 :: PS=0
2960 CALL CAS(PS,(P),SM(),CA(),HD(,,))
2970 P=1 :: DS=0
2980 CALL CAS(DS,(P),SM(),CA(),HD(,,))
2990 IF PS=DS THEN DISPLAY AT(12,11):"TIE GAME"
3000 IF PS>DS THEN DISPLAY AT(12,11):"YOU WIN!"
3010 IF PS<DS THEN DISPLAY AT(12,11):"YOU LOSE"
3020 SUBEND
3030 SUB TRM
3040 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT(12,7)BEEP:"GAME TERMINATED" :: CALL SOUND(100,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3050 FOR I=1 TO 5 :: CALL SOUND(500,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3060 END
3070 SUBEND