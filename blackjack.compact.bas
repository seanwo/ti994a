100 REM COPYRIGHT (C) 1987-2023 SEAN WOHLGEMUTH
210 CALL TRO
220 CALL SCH
230 CALL SCS
250 CALL PLY
260 DISPLAY AT (22,2) BEEP: "DO YOU WISH TO PLAY AGAIN?"
270 A$=""
280 CALL HCHAR(23,16,30)
300 CALL KEY(0,K,S)
310 IF S=0 OR S=-1 THEN 300
320 IF K<>78 AND K<>89 THEN 300
330 CALL HCHAR (23,16,K)
340 A$=CHR$(K)
350 CALL HCHAR (22,4,32,25) :: CALL HCHAR (23,16,32)
360 IF A$="N" THEN CALL TRM
370 GOTO 250
380 END
400 SUB TRO
410 CALL CLEAR
420 DISPLAY AT (12,10): "BLACKJACK"
430 DISPLAY AT (13,1): "presented by Sean Wohlgemuth"
440 FOR I=1 TO 5 :: CALL SOUND(1000,110,30) :: CALL SOUND(1,110,30) :: NEXT I
450 CALL CLEAR
460 DISPLAY AT (1, 1): "PREFACE:"
470 DISPLAY AT (3, 1): "THIS IS A GAME BETWEEN YOU"
480 DISPLAY AT (4, 1): "AND THE DEALER (COMPUTER)."
490 DISPLAY AT (6, 1): "OBJECT OF THE GAME:"
500 DISPLAY AT (8, 1): "THE OBJECT OF THE GAME IS TO"
510 DISPLAY AT (9, 1): "COLLECT A SET OF CARDS LESS"
520 DISPLAY AT (10, 1): "THAN OR EQUAL TO 21. IF YOUR"
530 DISPLAY AT (11, 1): "SET OF CARDS IS HIGHER THAN"
540 DISPLAY AT (12, 1): "THE DEALER'S AND NOT OVER 21"
550 DISPLAY AT (13, 1): "THEN YOU WIN."
560 DISPLAY AT (15, 1): "CARD VALUES:"
570 DISPLAY AT (17, 1): "NUMBER CARDS = FACE VALUE"
580 DISPLAY AT (18, 1): "FACE CARDS   = 10 PTS"
590 DISPLAY AT (19, 1): "ACES         = 11 OR 1 PT(S)"
600 DISPLAY AT (24,1): "PRESS ENTER TO CONTINUE:"
610 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
620 CALL CLEAR
630 DISPLAY AT (1, 1): "HOW TO PLAY:"
640 DISPLAY AT (3, 1): "BOTH YOU AND THE DEALER WILL"
650 DISPLAY AT (4, 1): "BE GIVEN TWO CARDS TO START."
660 DISPLAY AT (5, 1): "TAKE AS MANY CARDS AS YOU"
670 DISPLAY AT (6, 1): "WISH (UP TO 5 CARDS). IF"
680 DISPLAY AT (7, 1): "THE VALUE OF YOUR HAND"
690 DISPLAY AT (8, 1): "EXCEEDS 21 YOU BUST. ONCE"
700 DISPLAY AT (9, 1): "YOU STOP TAKING CARDS, THE"
710 DISPLAY AT (10, 1): "DEALER WILL TAKE CARDS"
720 DISPLAY AT (11, 1): "TRYING NOT TO BUST AS WELL."
730 DISPLAY AT (12, 1): "THE PLAYER WITH THE HIGHEST"
740 DISPLAY AT (13, 1): "HAND VALUE WINS."
750 DISPLAY AT (24,1): "PRESS ENTER TO BEGIN:"
760 ACCEPT AT (24,28) BEEP VALIDATE ("") : A$
770 SUBEND
790 SUB SCH
800 CALL SCR
810 CALL SCB
820 CALL SCE
840 CALL CHAR (136, "FFFFFFFFFFFFFFFF")
850 SUBEND
870 SUB SCR
890 CALL CHAR (96, "00367F7F7F3E1C08" )
910 CALL CHAR (97, "00081C3E7F3E1C08" )
930 CALL SCV((98))
950 CALL CHAR (111, "AA55AA55AA55AA55")
960 SUBEND
980 SUB SCB
1000 CALL CHAR (112, "0010387CFEFED638" )
1020 CALL CHAR (113, "00103854EE541038" )
1040 CALL SCV((114))
1050 SUBEND
1070 SUB SCV(OF)
1090 CALL CHAR (OF, "003844040830407C") :: OF=OF+1
1100 CALL CHAR (OF, "0038040418040438") :: OF=OF+1
1110 CALL CHAR (OF, "002424243C040404") :: OF=OF+1
1120 CALL CHAR (OF, "003C20203C04043C") :: OF=OF+1
1130 CALL CHAR (OF, "001C202038242418") :: OF=OF+1
1140 CALL CHAR (OF, "003C040408081010") :: OF=OF+1
1150 CALL CHAR (OF, "003C24243C24243C") :: OF=OF+1
1160 CALL CHAR (OF, "001C24241C040404") :: OF=OF+1
1170 CALL CHAR (OF, "004C52525252524C") :: OF=OF+1
1180 CALL CHAR (OF, "003E080808084830") :: OF=OF+1
1190 CALL CHAR (OF, "0018242424241C02") :: OF=OF+1
1200 CALL CHAR (OF, "0024242830282424") :: OF=OF+1
1210 CALL CHAR (OF, "001824243C242424") :: OF=OF+1
1220 SUBEND
1240 SUB SCE
1260 CALL CHAR (128, "8080808080808080")
1280 CALL CHAR (129, "00000000000000FF")
1290 SUBEND
1310 SUB SCS
1320 CALL COLOR (9,7,16)
1330 CALL COLOR (10,7,16)
1340 CALL COLOR (11,2,16)
1350 CALL COLOR (12,2,16)
1360 CALL COLOR (13,2,1)
1370 CALL COLOR (14,16,16)
1380 CALL SCREEN (3)
1390 SUBEND
1410 SUB RCD(P,CARD,FD,HD(,,))
1420 IF FD=1 THEN FI=111 ELSE FI=136
1430 COL=CARD*6 :: RO=P*10
1450 FOR I=1 TO 5
1460 CALL VCHAR (4+RO, 1+COL+I, FI, 7)
1470 NEXT I
1500 CALL HCHAR (3+RO, 2+COL, 129, 5)
1510 CALL VCHAR (4+RO, 7+COL, 128, 7)
1520 IF FD=1 THEN 1790
1540 IF HD(P,CARD,1)=1 THEN SU=96
1550 IF HD(P,CARD,1)=2 THEN SU=97
1560 IF HD(P,CARD,1)=3 THEN SU=112
1570 IF HD(P,CARD,1)=4 THEN SU=113
1590 IF HD(P,CARD,1)<3 THEN CC=98
1600 IF HD(P,CARD,1)>2 THEN CC=114
1610 CV=HD(P,CARD,0)
1620 CC=CC+CV-2
1630 CALL HCHAR (4+RO, 2+COL, CC, 1)
1640 CALL HCHAR (10+RO, 6+COL, CC, 1)
1660 IF CV=2 THEN CALL R02(RO,COL,SU) :: GOTO 1790
1670 IF CV=3 THEN CALL R03(RO,COL,SU) :: GOTO 1790
1680 IF CV=4 THEN CALL R04(RO,COL,SU) :: GOTO 1790
1690 IF CV=5 THEN CALL R05(RO,COL,SU) :: GOTO 1790
1700 IF CV=6 THEN CALL R06(RO,COL,SU) :: GOTO 1790
1710 IF CV=7 THEN CALL R07(RO,COL,SU) :: GOTO 1790
1720 IF CV=8 THEN CALL R08(RO,COL,SU) :: GOTO 1790
1730 IF CV=9 THEN CALL R09(RO,COL,SU) :: GOTO 1790
1740 IF CV=10 THEN CALL R0A(RO,COL,SU) :: GOTO 1790
1750 IF CV=11 THEN CALL RJK(RO,COL,SU) :: GOTO 1790
1760 IF CV=12 THEN CALL RQN(RO,COL,SU) :: GOTO 1790
1770 IF CV=13 THEN CALL RKG(RO,COL,SU) :: GOTO 1790
1780 IF CV=14 THEN CALL RAC(RO,COL,SU) :: GOTO 1790
1790 SUBEND
1810 SUB R02(RO,COL,SU)
1820 CALL HCHAR (6+RO,4+COL,SU,1)
1830 CALL HCHAR (8+RO,4+COL,SU,1)
1840 SUBEND
1860 SUB R03(RO,COL,SU)
1870 CALL VCHAR (6+RO,4+COL,SU,3)
1880 SUBEND
1900 SUB R04(RO,COL,SU)
1910 CALL HCHAR (6+RO,3+COL,SU,1)
1920 CALL HCHAR (8+RO,3+COL,SU,1)
1930 CALL HCHAR (6+RO,5+COL,SU,1)
1940 CALL HCHAR (8+RO,5+COL,SU,1)
1950 SUBEND
1970 SUB R05(RO,COL,SU)
1980 CALL R04((RO),(COL),(SU))
1990 CALL HCHAR (7+RO,4+COL,SU,1)
2000 SUBEND
2020 SUB R06(RO,COL,SU)
2030 CALL vCHAR (6+RO,3+COL,SU,3)
2040 CALL vCHAR (6+RO,5+COL,SU,3)
2050 SUBEND
2070 SUB R07(RO,COL,SU)
2080 CALL R06((RO),(COL),(SU))
2090 CALL VCHAR (7+RO,4+COL,SU,1) 
2100 SUBEND
2120 SUB R08(RO,COL,SU)
2130 CALL HCHAR (5+RO,3+COL,SU,1)
2140 CALL HCHAR (5+RO,5+COL,SU,1)
2150 CALL HCHAR (6+RO,4+COL,SU,1)
2160 CALL HCHAR (7+RO,3+COL,SU,1)
2170 CALL HCHAR (7+RO,5+COL,SU,1)
2180 CALL HCHAR (8+RO,4+COL,SU,1)
2190 CALL HCHAR (9+RO,3+COL,SU,1)
2200 CALL HCHAR (9+RO,5+COL,SU,1)
2210 SUBEND
2230 SUB R09(RO,COL,SU)
2240 CALL VCHAR (5+RO,3+COL,SU,2)
2250 CALL VCHAR (5+RO,5+COL,SU,2)
2260 CALL VCHAR (8+RO,3+COL,SU,2)
2270 CALL VCHAR (8+RO,5+COL,SU,2)
2280 CALL HCHAR (7+RO,4+COL,SU,1)
2290 SUBEND
2310 SUB R0A(RO,COL,SU)
2320 CALL VCHAR (5+RO,3+COL,SU,5)
2330 CALL VCHAR (5+RO,5+COL,SU,5)
2340 SUBEND
2360 SUB RJK(RO,COL,SU)
2370 CALL VCHAR (5+RO,5+COL,SU,5)
2380 CALL HCHAR (9+RO,3+COL,SU,2)
2390 CALL HCHAR (8+RO,3+COL,SU,1)
2400 SUBEND
2420 SUB RQN(RO,COL,SU)
2430 CALL VCHAR (5+RO,3+COL,SU,5)
2440 CALL VCHAR (5+RO,5+COL,SU,5)
2450 CALL HCHAR (5+RO,4+COL,SU,1)
2460 CALL HCHAR (9+RO,4+COL,SU,1)
2470 SUBEND
2490 SUB RKG(RO,COL,SU)
2500 CALL VCHAR (5+RO,3+COL,SU,5)
2510 CALL VCHAR (5+RO,5+COL,SU,2)
2520 CALL VCHAR (8+RO,5+COL,SU,2)
2530 CALL VCHAR (7+RO,4+COL,SU,1)
2540 SUBEND
2560 SUB RAC(RO,COL,SU)
2570 CALL HCHAR (7+RO,4+COL,SU,1)
2580 SUBEND
2600 SUB PLY
2610 DIM HD(1,4,1)
2620 DIM CA(1)
2630 DIM SM(1)
2640 CALL CTB(HD(,,),CA(),SM())
2650 CALL SHF(HD(,,))
2660 P=0 :: FD=0
2670 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2680 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2690 P=1 :: FD=0
2700 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2710 P=1 :: FD=1
2720 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2740 BU=0
2750 FOR J=3 TO 5
2760 DISPLAY AT (22,2) BEEP: "DO YOU WANT ANOTHER CARD?"
2770 A$=""
2780 CALL HCHAR(23,16,30)
2800 CALL KEY(0,K,S)
2810 IF S=0 OR S=-1 THEN 2800
2820 IF K<>78 AND K<>89 THEN 2800
2830 CALL HCHAR (23,16,K)
2840 A$=CHR$(K)
2850 CALL HCHAR (22,4,32,25) :: CALL HCHAR (23,16,32)
2860 IF A$="N" THEN 2940
2870 P=0 :: FD=0
2880 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
2890 IF SM(P)>21 THEN BU=1
2900 IF BU=1 THEN DISPLAY AT (12,4) BEEP: "YOU BUSTED. YOU LOSE."
2910 IF BU=1 THEN 2940
2920 NEXT J
2940 P=1 :: FD=0 :: CARD=1
2950 CALL RCD((P),(CARD),(FD),HD(,,))
2960 IF BU=1 THEN 3110
2980 BU=0
2990 FOR J=3 TO 5
3000 P=1 :: FD=0
3010 CALL CAS(SC,(P),SM(),CA(),HD(,,))
3020 IF SC>=17 THEN 3090
3030 CALL DEA(CA(),SM(),(P),(FD),HD(,,))
3040 IF SM(P)>21 THEN BU=1
3050 IF BU=1 THEN DISPLAY AT (12,3) BEEP: "DEALER BUSTED. YOU WIN."
3060 IF BU=1 THEN 3110
3070 NEXT J
3090 CALL WHW(SM(),CA(),HD(,,))
3110 SUBEND
3130 SUB CTB(HD(,,),CA(),SM())
3140 CALL CLEAR
3150 DISPLAY AT (1, 10): "BLACKJACK"
3160 FOR I=0 TO 1 :: SM(I)=0 :: CA(I)=0 :: NEXT I
3170 FOR I=0 TO 1 :: FOR J=0 TO 4 :: FOR K=0 TO 1 :: HD(I,J,K)=0 :: NEXT K :: NEXT J :: NEXT I
3180 SUBEND
3200 SUB SHF(HD(,,))
3210 RANDOMIZE
3220 DIM DK(9) :: FOR I=0 TO 9 :: DK(I)=0 :: NEXT I
3230 FOR H=0 TO 9
3250 DISPLAY AT(12,6): "SHUFFLING THE DECK"
3260 X=INT(RND*4)+1
3270 X=X*16
3280 X=X+INT(RND*13)+2
3290 DU=0
3300 CALL CKD(DU,(X),DK())
3310 IF DU=1 THEN 3250
3320 DK(H)=X
3330 NEXT H
3340 FOR H=0 TO 4
3350 HD(0,H,1)=INT(DK(H)/16)
3360 HD(0,H,0)=DK(H)-HD(0,H,1)*16
3370 NEXT H
3380 FOR H=0 TO 4
3390 HD(1,H,1)=INT(DK(H+5)/16)
3400 HD(1,H,0)=DK(H+5)-HD(1,H,1)*16
3410 NEXT H
3420 DISPLAY AT(12,6): "                  "
3430 SUBEND
3450 SUB CKD(DU,X,DK())
3460 DU=0
3470 FOR I=0 TO 9
3480 IF DK(I)=X THEN DU=1 :: GOTO 3500
3490 NEXT I
3500 SUBEND
3520 SUB DEA(CA(),SM(),P,FD,HD(,,))
3530 CA(P)=CA(P)+1
3540 CARD=CA(P)-1
3550 CL=HD(P,CARD,0)
3560 IF CL>10 THEN IF CL=14 THEN CL=1 ELSE CL=10
3570 SM(P)=SM(P)+CL
3580 CALL RCD((P),(CARD),(FD),HD(,,))
3590 SUBEND
3610 SUB CAS(SC,P,SM(),CA(),HD(,,))
3620 SC=SM(P)
3630 IF SC+10>21 THEN 3670
3640 FOR I=0 TO CA(P)-1
3650 IF HD(P,I,0)=14 THEN SC=SC+10 :: GOTO 3670
3660 NEXT I
3670 SUBEND
3690 SUB WHW(SM(),CA(),HD(,,))
3700 P=0 :: PS=0
3710 CALL CAS(PS,(P),SM(),CA(),HD(,,))
3730 P=1 :: DS=0
3740 CALL CAS(DS,(P),SM(),CA(),HD(,,))
3760 IF PS=DS THEN DISPLAY AT (12,11): "TIE GAME"
3770 IF PS>DS THEN DISPLAY AT (12,11): "YOU WIN!"
3780 IF PS<DS THEN DISPLAY AT (12,11): "YOU LOSE"
3790 SUBEND
3810 SUB TRM
3820 FOR I=1 TO 5 :: CALL CLEAR :: DISPLAY AT (12,7) BEEP: "GAME TERMINATED" :: CALL SOUND(100,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3830 FOR I=1 TO 5 :: CALL SOUND(500,110,30) :: CALL SOUND(1,110,30) :: NEXT I
3840 END
3850 SUBEND
